#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Jaro_distance
#c# 2018-03-20 <RC
#m# MOAR: OK
#j# JVM:  OK

# Translation of: Perl

sub jaro ($s, $t) {

    return 1 if $s eq $t;

    my $s_len = + my @s = $s.comb;
    my $t_len = + my @t = $t.comb;

    my $match_distance = ($s_len max $t_len) div 2 - 1;

    my ($matches, @s_matches, @t_matches) = 0;  # arrays remain un-initialized

    for ^@s -> $i {

        my $start = 0 max $i - $match_distance;
        my $end = $i + $match_distance min $t_len;

        for $start .. $end -> $j {
            @t_matches[$j] and next;
            @s[$i] eq @t[$j] or next;
            @s_matches[$i] = 1;
            @t_matches[$j] = 1;
            $matches++;
            last;
        }
    }

    return 0 if $matches == 0;

    my $k              = 0;
    my $transpositions = 0;

    for ^@s -> $i {
        @s_matches[$i] or next;
        until @t_matches[$k] { ++$k }
        @s[$i] eq @t[$k] or ++$transpositions;
        ++$k;
    }

    ($matches / $s_len + $matches / $t_len +
        (($matches - $transpositions / 2) / $matches)) / 3;
}

my @res;
@res.push: sprintf("%f", jaro("MARTHA",    "MARHTA"));
@res.push: sprintf("%f", jaro("DIXON",     "DICKSONX"));
@res.push: sprintf("%f", jaro("JELLYFISH", "SMELLYFISH"));

my $ref = qq:to/END/;
0.944444
0.766667
0.896296
END

use Test;
is @res.join("\n"), chomp $ref;
