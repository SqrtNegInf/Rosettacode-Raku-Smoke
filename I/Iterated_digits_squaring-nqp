#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Iterated_digits_squaring
#c# 2016-05-10 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# removed from the 'slow' list
#   20161123: 185.73u 0.32s 3:06 99%
#   20180314: 183.11u 0.45s 3:03 99%
#   20180814: 163.36u 0.44s 2:44 99%
#   20181004: 141.83u 0.42s 2:22 99%
#   20220304:  73.67u 0.55s 1:14 99%

# We can reduce this to X minutes by writing in the NQP (Not Quite Perl6) subset of the language:

use nqp;

my $cache := nqp::list_i();
nqp::bindpos_i($cache, 650, 0);
nqp::bindpos_i($cache, 1, 1);
nqp::bindpos_i($cache, 89, 89);

sub Euler92(int $n) {
    $n < 650
	?? nqp::bindpos_i($cache,$n,ids($n))
	!! ids($n)
}

sub ids(int $num --> int) {
    my int $n = $num;
    my int $ten = 10;
    my int $sum = 0;
    my int $t;
    my int $c;
    repeat until $n == 89 or $n == 1 {
	$sum = 0;
	repeat {
	    $t = nqp::div_i($n, $ten);
	    $c = $n - $t × $ten;
	    $sum = $sum + $c × $c;
	} while $n = $t;
	$n = nqp::atpos_i($cache,$sum) || $sum;
    }
    $n;
}

my int $cnt = 0;
for 1 .. 100_000_000 -> int $n {
   $cnt = $cnt + 1 if Euler92($n) == 89;
}
say $cnt;

use Test;
is $cnt, 85744333;
