#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Iterated_digits_squaring
#c# 2016-05-10 <RC, 2017-05-01 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# removed from the 'slow' list (running 1E6, via the slower method, so not comparable to the NQP version which is doing 1E8)

# 'slower' method for 1 million (worse with 'race')
#    20161123: 488u 3.88s 9:32  86% 
#    20170123: 346u 1.08s 5:48  99%
#    20181004: 179u 1.06s 3:00 100%
#    20201012: 135u 0.71s 2:16 100%
#    20220304:  38s 0.44s 0:38 100%

# 'faster' method for 100 million, compare with *-nqp
#    20170613:  769u 2.07s 12:54 100% 
#    20181004:  359u 0.83s  6:00 100%
#    20201012:  463u 0.78s  7:44 100% slower?? 
#    20220304:  252u 1.48s  4:14 100%
#    _NEXT_

#This fairly abstract version does caching and filtering to reduce the number of values it needs to check 
# and moves calculations out of the hot loop, but is still fairly slow... even for just up to 1,000,000.

constant @sq = ^10 X** 2;
my $cnt = 0;

sub Euler92($n) {
    $n == any(1,89) ?? $n !!
    (state %){$n} //= Euler92( [+] @sq[$n.comb] )
}

if (True) {
for 1 .. 1_000_000 -> $n {
   my $i = +$n.comb.sort.join;
   ++$cnt if Euler92($i) == 89;
}
say $cnt;

use Test;
is $cnt, 856929, 'Slower method';
exit;
}

#All is not lost, however. Through the use of gradual typing, Perl 6 scales down as well as up, so this jit-friendly version is performant enough to brute force the larger calculation:

my @cache;
@cache[1] = 1;
@cache[89] = 89;

sub altEuler92(int $n) {
    $n < 649  # 99,999,999 sums to 648, so no point remembering more
        ?? (@cache.AT-POS($n) //= ids($n))
        !! ids($n)
}

sub ids(int $num --> int) {
    my int $n = $num;
    my int $ten = 10;
    my int $sum = 0;
    my int $t;
    my int $c;
    repeat until $n == 89 or $n == 1 {
        $sum = 0;
        repeat {
            $t = $n div $ten;
            $c = $n - $t × $ten;
            $sum = $sum + $c × $c;
        } while $n = $t;
        $n = @cache.AT-POS($sum) // $sum;
    }
    $n;
}

my int $c = 0;
for 1 .. 100_000_000 -> int $n {
#for 1 .. 1_000_000 -> int $n {
   $c = $c + 1 if altEuler92($n) == 89;
}
say $c;

use Test;
#is $c, 856929,   'Faster method'; # for   1_000_000
is $c, 85744333, 'Faster method'; # for 100_000_000
