#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Idiomatically_determine_all_the_lowercase_and_uppercase_letters
#c# 2016-02-29 <RC
#m# MOAR: OK
#j#  JVM: OK

# Raku supports all the Unicode categories of characters natively. The
# constant 0x1fffd is not a typo for 0x10ffff: we're restricting the range of
# characters to the first two Unicode planes, since the 3rd plane is reserved
# for ideographs (category Lo, "letter other"), and the subsequent planes
# contain no letters (yet).

my @lc; my @uc; my @tc;
given (0..0x1fffd).chrs {
    @lc = .comb(/<:Ll>/); say "Lowercase: ", @lc[^121].join;
    @uc = .comb(/<:Lu>/); say "Uppercase: ", @uc[^121].join;
    @tc = .comb(/<:Lt>/); say "Titlecase: ", @tc[^20].join;
}

use Test;
is (@lc[^121].join), 'abcdefghijklmnopqrstuvwxyzµßàáâãäåæçèéêëìíîïðñòóôõöøùúûüýþÿāăąćĉċčďđēĕėęěĝğġģĥħĩīĭįıĳĵķĸĺļľŀłńņňŉŋōŏőœŕŗřśŝşšţťŧũūŭůűųŵŷź';
is (@uc[^121].join), 'ABCDEFGHIJKLMNOPQRSTUVWXYZÀÁÂÃÄÅÆÇÈÉÊËÌÍÎÏÐÑÒÓÔÕÖØÙÚÛÜÝÞĀĂĄĆĈĊČĎĐĒĔĖĘĚĜĞĠĢĤĦĨĪĬĮİĲĴĶĹĻĽĿŁŃŅŇŊŌŎŐŒŔŖŘŚŜŞŠŢŤŦŨŪŬŮŰŲŴŶŸŹŻŽƁƂ';
is (@tc[^20].join), 'ǅǈǋǲᾈᾉᾊᾋᾌᾍᾎᾏᾘᾙᾚᾛᾜᾝᾞᾟ';
