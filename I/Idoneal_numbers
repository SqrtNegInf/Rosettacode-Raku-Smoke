#!/usr/bin/env raku
#u# https://rosettacode.org/wiki/Idoneal_numbers
#c# 2022-09-25 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# OEIS:A000926 - Euler's "numerus idoneus" (or "numeri idonei", or suitable/convenient)

my @res;

sub is-idoneal ($n) {
    my $idoneal = True;
    I: for 1 .. $n -> $a {
        for $a ^.. $n -> $b {
            last if $a × $b + $a + $b > $n; # short circuit
            for $b ^.. $n -> $c {
                $idoneal = False and last I if (my $sum = $a × $b + $b × $c + $c × $a) == $n;
                last if $sum > $n; # short circuit
            }
        }
    }
    $idoneal
}

@res.push: $_».fmt("%4d") for (1..1850).hyper(:32batch).grep( &is-idoneal ).batch(10);

.say for @res;

my $ref = q:to/END/;
   1    2    3    4    5    6    7    8    9   10
  12   13   15   16   18   21   22   24   25   28
  30   33   37   40   42   45   48   57   58   60
  70   72   78   85   88   93  102  105  112  120
 130  133  165  168  177  190  210  232  240  253
 273  280  312  330  345  357  385  408  462  520
 760  840 1320 1365 1848
END

use Test;
is @res.join("\n"), chomp $ref;
