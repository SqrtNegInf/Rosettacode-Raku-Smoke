#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Word_wheel
#c# 2020-07-04 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

#use Terminal::Boxer;

my %*SUB-MAIN-OPTS = :named-anywhere;

unit sub MAIN ($wheel = 'ndeokgelw', :$dict = 'ref/unixdict.txt', :$min = 3);

my $must-have = $wheel.comb[4].lc;

my $has = $wheel.comb».lc.Bag;

my %words;
$dict.IO.slurp.words».lc.map: {
    next if not .contains($must-have) or .chars < $min;
    %words{.chars}.push: $_ if .comb.Bag ⊆ $has;
};

@res.push: "Using $dict, minimum $min letters.";

@res.push: $wheel.comb».uc;

@res.push: sprintf "%d letters:  %s", .key, .value.sort.join(', ') for %words.sort;

.say for @res;

my $ref = q:to/END/;
Using ref/unixdict.txt, minimum 3 letters.
N D E O K G E L W
3 letters:  eke, elk, keg, ken, wok
4 letters:  keel, keen, keno, knee, knew, know, kong, leek, week, woke
5 letters:  kneel
9 letters:  knowledge
END

use Test;
is @res.join("\n"), chomp $ref;
