#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Wordle_comparison
#c# 2022-02-16 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

sub wordle ($answer,$guess where [==] ($answer,$guess)>>.chars ) {
   my %answer = $answer.comb.pairs;
   my %guess  = $guess.comb.pairs;
   my @return;
 
   for %guess -> \trial { # pair 
      if %answer{trial.key} eq trial.value {
         @return[trial.key] = 'green';
         given trial.key { %answer{$_}:delete; %guess{$_}:delete }
      }
   }
 
   for %guess.keys.sort -> \trial { # numeric pos 
      @return[trial] = 'grey';
      for %answer -> \actual { # pair
         if %guess{trial} eq actual.value {
            @return[trial] = 'yellow';
            %answer{actual.key}:delete;  
            last;
         }
      }
   }
   @return;
}

@res.push: .[0] ~ ' vs ' ~ .[1] ~ ' ' ~ wordle .[0],.[1] 
    for (<ALLOW LOLLY>,<ROBIN ALERT>, <ROBIN SONIC>,<ROBIN ROBIN>,<BULLY LOLLY>,<BBAABBB BBBBBAA>,<BBAABBB AABBBAA>);

.say for @res;

my $ref = q:to/END/;
ALLOW vs LOLLY yellow yellow green grey grey
ROBIN vs ALERT grey grey grey yellow grey
ROBIN vs SONIC grey green yellow green grey
ROBIN vs ROBIN green green green green green
BULLY vs LOLLY grey grey green green green
BBAABBB vs BBBBBAA green green yellow yellow green yellow yellow
BBAABBB vs AABBBAA yellow yellow yellow yellow green grey grey
END

use Test;
is @res.join("\n"), chomp $ref;
