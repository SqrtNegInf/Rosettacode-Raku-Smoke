#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Factorions
#c# 2019-08-12 <RC, 2019-09-09 <RC
#m# MOAR: OK
#j# JVM:  BROKEN
#n# OEIS:A014080 Factorions: equal to the sum of the factorials of their digits in base 10.
#n# OEIS:A193163 Irregular table read by rows, in which row n lists the factorions in base n, for n >= 2.
#n# 2021-08-25 .race starts causing failure
#n# 2021-09-10 pre-allocating @result, making @factorial non-lazy does not help
#n# 2022-05-20 .race slows things down?

constant @factorial = 1, |[\×] 1..*;
#constant @factorial = 1, |[\×] 1..10000;

constant $limit = 42_000; # 1_500_000;

constant $bases = 9 .. 12;

my @result; #[13];

#$bases.race.map: -> $base {
#$bases.race(:1batch).map: -> $base {
#$bases.race(:degree($bases.elems), :1batch).map: -> $base {
$bases.map: -> $base {

    @result[$base] = "\nFactorions in base $base:\n1 2";

    sink (1 .. $limit div $base).map: -> $i {
        my $product = $i × $base;
        my $partial;

        for $i.polymod($base xx *) {
            $partial += @factorial[$_];
            last if $partial > $product
        }

        next if $partial > $product;

        my $sum;

        for ^$base {
            last if ($sum = $partial + @factorial[$_]) > $product + $_;
            @result[$base] ~= " $sum" and last if $sum == $product + $_
        }
    }
}

.say for @result[$bases];

my $ref = q:to/END/;

Factorions in base 9:
1 2 41282

Factorions in base 10:
1 2 145 40585

Factorions in base 11:
1 2 26 48 40472

Factorions in base 12:
1 2
END

use Test;
is @result[$bases].join("\n"), chomp $ref;
