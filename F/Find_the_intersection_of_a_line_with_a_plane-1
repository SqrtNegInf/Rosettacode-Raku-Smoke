#!/usr/bin/env raku
#u# https://rosettacode.org/wiki/Find_the_intersection_of_a_line_with_a_plane
#c# 2016-12-21 <RC, 2023-01-06 <RC
#m# MOAR: OK
#j# JVM:  BROKEN
#n# Unicode variable name issues, replaced with plain ASCII

use Clifford:ver<6.2.1>;

class Line {
    has $.P0; # point
    has $.u;  # ray
}
class Plane {
    has $.V0; # point
    has $.n;  # normal
}

sub infix:<dot> ( @a, @b where +@a == +@b ) { [+] @a «*» @b } # dot product

sub line-plane-intersection ($L, $P) {
    my $cos = $P.n dot $L.u; # cosine between normal & ray
    return 'Vectors are orthogonal; no intersection or line within plane'
      if $cos == 0;
    my $W = $L.P0 «-» $P.V0;      # difference between P0 and V0
    my $SI = -($P.n dot $W) / $cos;  # line segment where it intersects the plane
    $W «+» $SI «*» $L.u «+» $P.V0; # point where line intersects the plane
 }

say 'Intersection at point: ', line-plane-intersection(
     Line.new( :P0(0,0,10), :u(0,-1,-1) ),
    Plane.new( :V0(0,0, 5), :n(0, 0, 1) )
  );

# We pick a (non-degenerate) projective basis and
# we define the dual and meet operators.
my $I = [∧] my ($i, $j, $k, $l) = @e;
sub prefix:<∗>($M) { $M/$I }
sub infix:<∨>($A, $B) { ∗((∗$B)∧(∗$A)) }

my $direction = -$j - $k;

# Homogeneous coordinates of (X, Y, Z) are (X, Y, Z, 1)
my $point = 10*$k + $l;

# A projective line is a bivector
my $line = $direction ∧ $point;

# A projective plane is a trivector
my $plane = (5*$k + $l) ∧ ($k*-$i∧$j∧$k);

# The intersection is the meet
my $m = $line ∨ $plane;

# Affine coordinates of (X, Y, Z, W) are (X/W, Y/W, Z/W)
say my $ref = ($m/($m·$l) X· ($i, $j, $k)).gist;

use Test;
is '(0 -5 5)', $ref;
