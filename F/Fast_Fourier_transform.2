#!/usr/bin/env perl6
#n# NOT CURRENT, NOT SMOKE-TESTED
#u# http://rosettacode.org/wiki/Fast_Fourier_transform
#c# 2015-12-06 <RC
#m# MOAR: OK
#j#  JVM: OK

# Works with: rakudo 2015-12
sub fft {
    return @_ if @_ == 1;
    my @evn = fft( @_[0, 2 ... *] );
    my @odd = fft( @_[1, 3 ... *] ) Z× map &cis, (0, 2×pi/ @_ ... *);
    return flat @evn »+« @odd, @evn »-« @odd;
}

my @seq    = ^16;
my $cycles = 3;
my @wave   = map { sin( 2×pi × $_ / @seq × $cycles ) }, @seq;
say "wave: ", @wave.fmt("%7.3f");

say "fft:  ", fft(@wave)».abs.fmt("%7.3f");


use Test;
is @wave.fmt("%7.3f"), '  0.000   0.924   0.707  -0.383  -1.000  -0.383   0.707   0.924   0.000  -0.924  -0.707   0.383   1.000   0.383  -0.707  -0.924';
is fft(@wave)».abs.fmt("%7.3f"), '  0.000   0.000   0.000   8.000   0.000   0.000   0.000   0.000   0.000   0.000   0.000   0.000   0.000   8.000   0.000   0.000';
