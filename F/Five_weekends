#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Five_weekends
#c# 2016-03-19 <RC
#m# MOAR: OK
#j#  JVM: OK

my @res;

# A month has 5 weekends iff it has 31 days and starts on Friday.
my @years = 1900 .. 2100;
my @has31 = 1, 3, 5, 7, 8, 10, 12;
my @happy = ($_ when *.day-of-week == 5 for (@years X @has31).map(-> ($y, $m) { Date.new: $y, $m, 1 }));

@res.push: 'Happy month count:  ', +@happy;
@res.push: 'First happy months: ' ~ @happy[^5];
@res.push: 'Last  happy months: ' ~ @happy[*-5 .. *];
@res.push: 'Dreary years count: ',  @years - @happyÂ».year.squish;

.say for @res;

my $ref = qq:to/END/;
Happy month count:  201
First happy months: 1901-03-01 1902-08-01 1903-05-01 1904-01-01 1904-07-01
Last  happy months: 2097-03-01 2098-08-01 2099-05-01 2100-01-01 2100-10-01
Dreary years count: 29
END

use Test;
is @res.join('').subst(/<ws>/, '', :g), $ref.subst(/<ws>/, '', :g);
