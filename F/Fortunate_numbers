#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Fortunate_numbers
#c# 2021-08-01 <RC
#m# MOAR: OK
#j# JVM:  BROKEN
#n# Limit of 75 primorials to get first 50 unique fortunates is arbitrary, found through trial and error.
#n# OEIS:A005235 Fortunate numbers
#n# OEIS:A046066 Fortunate numbers, sorted with duplicates removed

my @primorials = [\×] grep *.is-prime, ^∞;

say my $result = display :title("First 50 distinct fortunate numbers:\n"),
   (squish sort @primorials[^75].hyper.map: -> $primorial {
       (2..∞).first: (* + $primorial).is-prime
   })[^50];

sub display ($list, :$cols = 10, :$fmt = '%6d', :$title = "{+$list} matching:\n") {
    cache $list;
    $title ~ $list.batch($cols)».fmt($fmt).join: "\n"
}

my $ref = q:to/END/;
First 50 distinct fortunate numbers:
     3      5      7     13     17     19     23     37     47     59
    61     67     71     79     89    101    103    107    109    127
   151    157    163    167    191    197    199    223    229    233
   239    271    277    283    293    307    311    313    331    353
   373    379    383    397    401    409    419    421    439    443
END

use Test;
is $result, chomp $ref;
