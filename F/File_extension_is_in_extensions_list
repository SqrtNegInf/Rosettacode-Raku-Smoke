#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/File_extension_is_in_extensions_list
#c# 2016-09-03 <RC
#m# MOAR: OK
#j#  JVM: OK

sub check-extension ($filename, *@extensions) {
    so $filename ~~ /:i '.' @extensions $/
}

my @extensions = <zip rar 7z gz archive A## tar.bz2>;

my @files= <
    MyData.a##  MyData.tar.Gz  MyData.gzip  MyData.7z.backup  MyData...  MyData
    MyData_v1.0.tar.bz2  MyData_v1.0.bz2
>;
my @res.push: "{$_.fmt: '%-19s'} - {check-extension $_, @extensions}" for @files;

.say for @res;

my $ref = qq:to/END/;
MyData.a##          - True
MyData.tar.Gz       - True
MyData.gzip         - False
MyData.7z.backup    - False
MyData...           - False
MyData              - False
MyData_v1.0.tar.bz2 - True
MyData_v1.0.bz2     - False
END

use Test;
is @res.join("\n"), chomp $ref;
