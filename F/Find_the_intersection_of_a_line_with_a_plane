#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Find_the_intersection_of_a_line_with_a_plane
#c# 2016-12-21 <RC
#m# MOAR: OK
#j#  JVM: OK
#   Unicode issues, replaced U. characters with plain ASCII

class Line {
    has $.P0; # point
    has $.u;  # ray
}
class Plane {
    has $.V0; # point
    has $.n;  # normal
}

sub infix:<dot> ( @a, @b where +@a == +@b ) { [+] @a «×» @b }

sub line-plane-intersection ($L, $P) {
    my $cos = $P.n dot $L.u; # cosine between normal & ray
    return 'Vectors are orthoganol; no intersection or line within plane'
      if $cos == 0;
    my $W = $L.P0 «-» $P.V0;      # difference between P0 and V0
    my $SI = -($P.n dot $W) / $cos;  # line segment where it intersets the plane
    $W «+» $SI «×» $L.u «+» $P.V0; # point where line intersects the plane
 }

say 'Intersection at point: ', my $ref = line-plane-intersection(
     Line.new( :P0(0,0,10), :u(0,-1,-1) ),
    Plane.new( :V0(0,0, 5), :n(0, 0, 1) )
  );

use Test;
is '0 -5 5', $ref;
