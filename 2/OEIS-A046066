#!/usr/bin/env raku
#u# https://oeis.org/A046066
#c# 2023-02-04 NOT ON RC
#m# MOAR: OK
#j#  JVM: OK
#n# mirrored from ~/PWC/155/1/bruce-gray.raku
#n# OEIS:A000040 Prime numbers
#n# OEIS:A006862 Euclid numbers: 1 + product of the first n primes. 
#n# OEIS:A035345 Smallest prime > prime(1)*prime(2)*...*prime(n)+1.
#n# OEIS:A005235 Fortunate numbers: least m>1 such that m + prime(n)# is prime, where p# denotes the product of all primes <= p
#n# OEIS:A046066 Fortunate primes in numerical order with duplicates removed.

# Variable names are from https://mathworld.wolfram.com/FortunatePrime.html

sub first-prime-greater-than ( UInt $n ) { return first &is-prime, $n ^.. * }

constant @primes  = grep &is-prime, ^Inf;               # https://oeis.org/A000040

constant @Ek = map { $^product + 1 }, [\Ã—] @primes;     # https://oeis.org/A006862

constant @Qk = @Ek.map: &first-prime-greater-than;      # https://oeis.org/A035345

constant @Fk = (@Qk Z- @Ek) X+ 1;                       # https://oeis.org/A005235

my $count = first { @Fk.head($_).unique.elems >= 20 }, 1..*;
say my $result = @Fk.head($count).sort.squish;

use Test;
is $result, '3 5 7 13 17 19 23 37 47 59 61 67 71 79 89 103 107 109 151 197';
