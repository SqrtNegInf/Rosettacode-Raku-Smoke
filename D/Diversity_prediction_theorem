#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Diversity_prediction_theorem
#c# 2016-12-03 >RC
#m# MOAR: OK
#j#  JVM: OK

sub diversity-calc($truth, @pred) {
    my $ae = avg-error($truth, @pred); # average individual error
    my $cp = ([+] @pred)/+@pred;       # collective prediction
    my $ce = ($cp - $truth)Â²;          # collective error
    my $pd = avg-error($cp, @pred);    # prediction diversity
    <average-error crowd-error diversity> Z=> return $ae, $ce, $pd;
}

sub avg-error ($m, @v) { ([+] (@v X- $m) X**2) / +@v }

sub diversity-format (@stats) { 
gather {
    for <average-error crowd-error diversity> Z @stats -> ($label,$value) {
        take $label.fmt("%13s") ~ ':' ~ $value.fmt("%7.3f");
    }
}
}

my @res;
@res.push: $_ for diversity-format diversity-calc(49, <48 47 51>);
@res.push: $_ for diversity-format diversity-calc(49, <48 47 51 42>);

.say for @res;

my $ref = qq:to/END/;
average-error:  3.000
  crowd-error:  0.111
    diversity:  2.889
average-error: 14.500
  crowd-error:  4.000
    diversity: 10.500
END

use Test;
is @res.join("\n"), chomp $ref;
