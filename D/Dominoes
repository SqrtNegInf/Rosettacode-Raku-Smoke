#!/usr/bin/env raku
#u# https://rosettacode.org/wiki/Dominoes
#c# 2022-12-05 NOT ON RC
#m# MOAR: OK
#j#  JVM: OK
#n# just extra credit combinatorics, to demonstrate numeric issue
#n# repeated operations with cos values accumulates errors, even with 'cosPi'

my @res;

use TrigPi;

sub comma { $^i.flip.comb(3).join(',').flip }

my ($m,$n)= 7, 8;
my $arrangements = 1;
my $arrangements2 = 1;
for 1 .. $m/2 -> $j {
  for 1 .. $n/2  -> $k {
    $arrangements ×= 4×cos(π×$j/($m+1))² + 4×cos(π×$k/($n+1))²;
    $arrangements2×= 4×cosPi($j/($m+1))² + 4×cosPi($k/($n+1))²;
  }
}

@res.push: $arrangements2;

@res.push: comma $arrangements.round(1);

@res.push: comma my $permutations = [×] 2..28;
@res.push: comma my $flips = 2**28; # 2²⁸ also works

@res.push: comma sprintf "%d", $flips × $permutations × $arrangements.round(1);

.say for @res;

my $moar = qq:to/END/;
1292697.000000003
1,292,697
304,888,344,611,713,860,501,504,000,000
268,435,456
105,797,996,085,635,281,181,632,579,889,767,907,328,000,000
END

my $jvm = qq:to/END/;
1292697.0000000026
1,292,697
304,888,344,611,713,860,501,504,000,000
268,435,456
105,797,996,085,635,281,181,632,579,889,767,907,328,000,000
END

use Test;
is @res.join("\n"), chomp $*VM ~~ /jvm/ ?? $jvm !! $moar;

=finish

=={{header|Raku}}==
<syntaxhighlight lang="perl6" line>
</syntaxhighlight>
{{out}}
<pre>
</pre>
