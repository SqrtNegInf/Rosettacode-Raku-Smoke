#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Dutch_national_flag_problem
#c# 2016-04-22 <>RC
#m# MOAR: OK
#j#  JVM: OK
#n# 2022-12-31 just eliminate randonmess, so compelling reason for it

#Here are five ways to do it, all one liners (apart from the test apparatus).

enum NL <red white blue>;
my @colors;
my @res;

sub how'bout (&this-way) {
    sub show {
        @res.push: @colors;
        @res.push: "Ordered: " ~ [<=] @colors;
    }

   #@colors = NL.roll(6);
    @colors = [NL::white, NL::white, NL::blue, NL::white, NL::white, NL::red];
    show;
    this-way;
    show;
}

@res.push: "Using functional sort";
how'bout { @colors = sort *.value, @colors }

@res.push: "Using in-place sort";
how'bout { @colors .= sort: *.value }

@res.push: "Using a Bag";
how'bout { @colors = flat red, white, blue Zxx bag(@colorsÂ».key)<red white blue> }

@res.push: "Using the classify method";
how'bout { @colors = flat (.list for %(@colors.classify: *.value){0,1,2}) }

@res.push: "Using multiple greps";
how'bout { @colors = flat (.grep(red), .grep(white), .grep(blue) given @colors) }

.put for @res;

my $ref = q:to/END/;
Using functional sort
red white white white white blue
Ordered: False
red white white white white blue
Ordered: True
Using in-place sort
red white white white white blue
Ordered: False
red white white white white blue
Ordered: True
Using a Bag
red white white white white blue
Ordered: False
red white white white white blue
Ordered: True
Using the classify method
red white white white white blue
Ordered: False
red white white white white blue
Ordered: True
Using multiple greps
red white white white white blue
Ordered: False
red white white white white blue
Ordered: True
END

use Test;
is @res.join("\n"), chomp $ref;
