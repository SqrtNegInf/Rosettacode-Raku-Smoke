#!/usr/bin/env perl6
## display tasks implemented, for selected languages (for now, the members of the '1000' club [as of late 2019])
#  David H.  Fri Sep 13 16:05:56 PDT 2019
## not a race, not a race, not a race...

use HTTP::UserAgent;
use URI::Escape;
use JSON::Fast;

my $client = HTTP::UserAgent.new;
my $url = 'http://rosettacode.org/mw';

my %langs = ( 
'Go'     => 'Go',
'Julia'  => 'Ju',
'Kotlin' => 'Ko',
'Perl'   => 'P5',
'Perl_5' => 'P5',
'Perl_6' => 'P6',
'Phix'   => 'Ph',
'Python' => 'Py',
'Racket' => 'Ra',
#'Java'   => 'Ja',  # will add someday
#'Zkl'    => 'Zk',  # will add someday
#'C'      => 'C ',  # least useful, from porting perspective
#'Sidef'  => 'Si',  # most useful, from porting perspective
);

my (%impl,%h);
for %langs.keys -> $lang { 
    next if $lang eq 'Perl_5'; 
    %impl{$lang} = get-cat $lang; 
}

for %impl.keys -> $lang { 
    %h{$_}.push: ($lang ~ ('_5' if $lang eq 'Perl')) for %impl{$lang}.List 
}

#spurt "yyy.json", to-json(%h); exit;
#my $xyz = slurp "yyy.json"; my %h = from-json($xyz);

%langs{'Perl'}:delete;

for %h.keys.sort: { %h{$^b}.elems > %h{$^a}.elems }  {
    next if %h{$_}.elems == +%langs.keys or /Category:/;
    printf "%23s  %s\n", whodunit(%h{$_}), $_;
}

sub whodunit (@done) { join '', ((index(" {join ' ',@done}", $_) ?? %langs{$_} !! ' ').fmt("%3s") for %langs.keys.sort) }

sub get-cat ($category) {
    flat mediawiki-query(
        $url, 'pages',
        :generator<categorymembers>,
        :gcmtitle("Category:$category"),
        :gcmlimit<350>,
        :rawcontinue(),
        :prop<title>
    ).map({ .<title> });
}

sub mediawiki-query ($site, $type, *%query) {
    my $url = "$site/api.php?" ~ uri-query-string(
        :action<query>, :format<json>, :formatversion<2>, |%query);
    my $continue = '';

    gather loop {
        my $response = $client.get("$url&$continue");
        my $data = from-json($response.content);
        take $_ for $data.<query>.{$type}.values;
        $continue = uri-query-string |($data.<query-continue>{*}Â».hash.hash or last);
    }
}

sub uri-query-string (*%fields) { %fields.map({ "{.key}={uri-escape .value}" }).join("&") }
