#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Knights_tour
#c# 2015-11-05 <RC
#m# MOAR: OK
#j#  JVM: OK

srand 123456;
my $result;

my @board;

my $I = 8;
my $J = 8;
my $F = $I×$J > 99 ?? "%3d" !! "%2d";

# Choose starting position - may be passed in on command line; if
# not, choose random square.
my ($i, $j);

if my $sq = shift @*ARGS {
  die "$*PROGRAM-NAME: illegal start square '$sq'\n" unless ($i, $j) = from_algebraic($sq);
}
else {
  ($i, $j) = (^$I).pick, (^$J).pick;
}

# Move sequence
my @moves = ();

for 1 .. $I × $J -> $move {
  # Record current move
  push @moves, to_algebraic($i,$j);
  # @board[$i] //= [];   # (uncomment if autoviv is broken)
  @board[$i][$j] = $move;

  # Find move with the smallest degree
  my @min = (9);
  for possible_moves($i,$j) -> @target {
      my ($ni, $nj) = @target;
      my $next = possible_moves($ni,$nj);
      @min = $next, $ni, $nj if $next < @min[0];
  }

  # And make it
  ($i, $j) = @min[1,2];
}

# Print the move list
for @moves.kv -> $i, $m {
    #$result ~= ',', $i %% 16 ?? "\n" !! " " if $i;
    $result ~= "$m ";
}
say $result; 
say "\n";

# And the board, with move numbers
for ^$I -> $i {
  for ^$J -> $j {
    # Assumes (1) ANSI sequences work, and (2) output 
    # is light text on a dark background.
    print "\e[7m" if $i % 2 == $j % 2; 
    printf $F, @board[$i][$j];
    print "\e[0m";
  }
  print "\n";
}

# Find the list of positions the knight can move to from the given square
sub possible_moves($i,$j) {
  grep -> [$ni, $nj] { $ni ~~ ^$I and $nj ~~ ^$J and !@board[$ni][$nj] }, 
    [$i-2,$j-1], [$i-2,$j+1], [$i-1,$j-2], [$i-1,$j+2],
    [$i+1,$j-2], [$i+1,$j+2], [$i+2,$j-1], [$i+2,$j+1];
}

# Return the algebraic name of the square identified by the coordinates
# i=rank, 0=black's home row; j=file, 0=white's queen's rook
sub to_algebraic($i,$j) {
    chr(ord('a') + $j) ~ ($I - $i);
}

# Return the coordinates matching the given algebraic name
sub from_algebraic($square where /^ (<[a..z]>) (\d+) $/) {
   $I - $1, ord(~$0) - ord('a');
}

use Test;
if $*VM ~~ /jvm/ {
is $result, 'g1 h3 g5 h7 f8 d7 b8 a6 c7 a8 b6 c8 a7 b5 a3 b1 d2 f1 h2 f3 h4 g2 e1 c2 a1 b3 a5 b7 d8 f7 h8 g6 e7 g8 h6 g4 e5 c6 b4 a2 c1 d3 c5 a4 b2 c4 d6 e8 g7 e6 d4 f5 e3 d1 f2 h1 g3 h5 f6 e4 c3 d5 f4 e2 ';
} else {
is $result, 'b8 a6 c7 a8 b6 c8 a7 b5 a3 b1 d2 f1 h2 g4 h6 g8 e7 c6 d8 b7 a5 c4 b2 a4 c5 d7 f8 h7 f6 e8 g7 h5 g3 h1 f2 d1 c3 e4 d6 f7 h8 g6 e5 d3 b4 a2 c1 b3 a1 c2 e1 g2 h4 f5 e3 d5 f4 e6 g5 h3 g1 f3 d4 e2 ';
}
