#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Knuth's_algorithm_S
#c# 2016-01-05 <RC
#m# MOAR: OK
#j#  JVM: OK

srand 123456;

sub s_of_n_creator($n) {
    my @sample;
    my $i = 0;
    -> $item {
        if ++$i <= $n {
            push @sample, $item;
        }
        elsif $i.rand < $n {
            @sample[$n.rand] = $item;
        }
        @sample;
    }
}

my @items = 0..9;
my @bin;

for ^100000 {
    my &s_of_n = s_of_n_creator(3);
    my @sample;
    for @items -> $item {
        @sample = s_of_n($item);
    }
    for @sample -> $s {
        @bin[$s]++;
    }
}

say @bin.join(" ");

my $moar = '29760 29796 30111 30237 29904 29916 29863 29890 30379 30144';
my $jvm  = '30155 30207 30016 30169 30044 29973 29725 29932 30041 29738';

use Test;
is @bin.join(" "), $*VM ~~ /jvm/ ?? $jvm !! $moar;
