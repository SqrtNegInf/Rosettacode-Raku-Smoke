#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Knuth's_algorithm_S
#c# 2016-01-05 <RC,  2022-08-21 >RC
#m# MOAR: OK
#j#  JVM: OK
#n# this version on RC; here not worrying about maintaining old results

srand 123456;

sub s_of_n_creator($n) {
    my (@sample, $i);
    -> $item {
        if    ++$i      <= $n { @sample.push:      $item } # .append gives different result?
        elsif   $i.rand <  $n { @sample[$n.rand] = $item }
        @sample
    }
}

my @bin;

for ^100000 {
    my &s_of_n = s_of_n_creator 3;
    sink .&s_of_n for ^9;
    @bin[$_]++ for s_of_n 9;
}

say my $result = @bin.join: ' ';

my $moar-terminal = '29763 29792 30107 30243 29898 29909 29864 29885 30384 30155';
my $moar-cronjob  = '29761 29795 30107 30243 29898 29910 29863 29887 30385 30151';
my $jvm           = '30155 30207 30016 30169 30044 29973 29725 29932 30041 29738';

use Test;
is $result, chomp $*VM ~~ /jvm/ ?? $jvm !! $*IN.t ?? $moar-terminal !! $moar-cronjob;
