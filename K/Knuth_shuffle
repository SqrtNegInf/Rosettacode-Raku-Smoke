#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Knuth_shuffle
#c# 2015-11-03 <RC
#m# MOAR: OK
#j#  JVM: OK

srand 123456;

sub shuffle (@a is copy) {
    for 1 ..^ @a -> $n {
        my $k = (0 .. $n).pick;
        $k == $n or @a[$k, $n] = @a[$n, $k];
    }
    @a
}
my @cards = <H1 H2 H3 D1 D2 D3 S1 S2 S3 C1 C2 C3>;
put my @res1 = shuffle @cards;

#The shuffle is also built into the pick method on lists when you pass it a "whatever" for the number to pick:
put my @res2 = @cards.pick(*);

use Test;
if $*VM ~~ /jvm/ {
is @res1.join(' '), 'S2 C3 C1 S1 H3 D2 H1 D1 D3 H2 C2 S3';
is @res2.join(' '), 'H2 C3 S1 H3 H1 C2 D3 D1 S3 C1 D2 S2';
} else {
is @res1.join(' '), 'S1 H1 C2 C3 S3 H2 S2 D2 C1 D1 H3 D3';
is @res2.join(' '), 'S3 S1 C3 H1 C1 S2 D2 D1 H3 D3 H2 C2';
}

