#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Special_factorials
#c# 2021-03-17 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# OEIS:A000178 Super-factorials: product of first n factorials.
#n# OEIS:A002109 Hyper-factorials: Product_{k = 1..n} k^k.
#n# OEIS:A005165 Alternating factorials: n! - (n-1)! + (n-2)! - ... 1!.
#n# OEIS:A049384 Exponential factorials: a(0)=1, a(n+1) = (n+1)^a(n).

my @res;

sub postfix:<!> ($n) {   [*] 1 .. $n }
sub postfix:<$> ($n) { [R**] 1 .. $n }

sub sf ($n) { [*] map {                      $_! }, 1 .. $n }
sub H  ($n) { [*] map {                $_ ** $_  }, 1 .. $n }
sub af ($n) { [+] map { (-1) ** ($n - $_) *  $_! }, 1 .. $n }
sub rf ($n) {
    state @f = 1, |[\*] 1..*;
    $n == .value ?? .key !! Nil given @f.first: :p, * >= $n;
}

@res.push: 'sf : '~ map &sf , 0..9;
@res.push: 'H  : '~ map &H  , 0..9;
@res.push: 'af : '~ map &af , 0..9;
@res.push: '$  : '~ map *$  , 1..4;

@res.push: '5$ has ' ~ 5$.chars ~ ' digits';

@res.push: 'rf : '~ map &rf, 1, 2, 6, 24, 120, 720, 5040, 40320, 362880, 3628800;
@res.push: 'rf(119) = '~ rf(119).raku;

.say for @res;

my $ref = q:to/END/;
sf : 1 1 2 12 288 34560 24883200 125411328000 5056584744960000 1834933472251084800000
H  : 1 1 4 108 27648 86400000 4031078400000 3319766398771200000 55696437941726556979200000 21577941222941856209168026828800000
af : 0 1 1 5 19 101 619 4421 35899 326981
$  : 1 2 9 262144
5$ has 183231 digits
rf : 0 2 3 4 5 6 7 8 9 10
rf(119) = Nil
END

use Test;
is @res.join("\n"), chomp $ref;
