#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Successive_prime_differences
#c# 2019-04-29 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

# Precomputed Differences

constant $max    = 1_000_000;
constant @primes = 2, |(3, 5, 7 ... $max).grep: *.is-prime;
constant @diffs  = @primes.skip Z- @primes;

say "Given all ordered primes <= $max, sets with successive differences of:";
for (2,), (1,), (2,2), (2,4), (4,2), (6,4,2) -> @succ {
    my $size = @succ.elems;

    my @group_start_offsets = @diffs.rotor( $size => 1-$size )
                                    .grep(:k, { $_ eqv @succ });

    my ($first, $last) = @group_start_offsets[0, *-1]
                         .map: { @primes.skip($_).head($size + 1) };

    @res.push: sprintf '%10s has %5d sets: %15s … %s',
        @succ.gist, @group_start_offsets.elems, $first, $last;
}

say join "\n", @res;

my $ref = q:to/END/;
       (2) has  8169 sets:             3 5 … 999959 999961
       (1) has     1 sets:             2 3 … 2 3
     (2 2) has     1 sets:           3 5 7 … 3 5 7
     (2 4) has  1393 sets:          5 7 11 … 999431 999433 999437
     (4 2) has  1444 sets:         7 11 13 … 997807 997811 997813
   (6 4 2) has   306 sets:     31 37 41 43 … 997141 997147 997151 997153
END

use Test;
is @res.join("\n"), chomp $ref;
