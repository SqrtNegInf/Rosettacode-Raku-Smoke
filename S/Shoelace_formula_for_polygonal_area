#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Shoelace_formula_for_polygonal_area
#c# 2017-08-11 <RC
#m# MOAR: OK
#j# JVM:  OK

# Index and mod offset
sub area-by-shoelace1(@p) {
    (^@p).map({@p[$_;0] × @p[($_+1)%@p;1] - @p[$_;1] × @p[($_+1)%@p;0]}).sum.abs / 2
}

say my $result1 = area-by-shoelace1( [ (3,4), (5,11), (12,8), (9,5), (5,6) ] );

# Slice and rotation
sub area-by-shoelace2(@p) {
    my @x := @p».[0];
    my @y := @p».[1];

    my $s := ( @x Z× @y.rotate( 1) ).sum
           - ( @x Z× @y.rotate(-1) ).sum;

    return $s.abs / 2;
}

say my $result2 = area-by-shoelace2( [ (3,4), (5,11), (12,8), (9,5), (5,6) ] );

use Test;
is $result1, 30;
is $result2, 30;
