#!/usr/bin/env raku
#u# https://rosettacode.org/wiki/Selection_bias_in_clinical_sciences
#c# 2022-10-25 <RC, 2022-10-29 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# 2023-01-08 replace call to built-in 'rand' for output stability (really slows down, fewer trials)

my $result;

use myRNG;
my $rng = myRNG.new( :seed(123456) );

enum <UNTREATED REGULAR IRREGULAR>;
my \DOSE_FOR_REGULAR = 100;
my ($nSubjects,$duration,$interval) = 2500, 180, 30;
my (@dosage,@category,@hadcovid) := (0,UNTREATED,False)>>.&{($_ xx $nSubjects).Array};

#sub update($pCovid=1e-3, $pStartTreatment=5e-3, $pRedose=¼, @dRange=<3 6 9>) { # newer JVM might handle this
sub update($pCovid=1e-3, $pStartTreatment=5e-3, $pRedose=1/4, @dRange=<3 6 9>) {
   for 0 ..^ @dosage.elems -> \i { 
      unless @hadcovid[i] { 
         if $rng.next-rat < $pCovid { 
            @hadcovid[i] = True
         } else {
            my $dose = @dosage[i];
            if $dose==0 && $rng.next-rat < $pStartTreatment or $dose > 0 && $rng.next-rat < $pRedose {               
	           @dosage[i]   = $dose += @dRange[@dRange.elems × $rng.next-rat];
               @category[i] = ($dose > DOSE_FOR_REGULAR) ?? REGULAR !! IRREGULAR
            } 
         } 
      } 
   } 
}

sub kruskal (@sets) {
   my $n          = ( my @ranked = @sets>>.List.flat.sort ).elems;
   my @sr         = 0 xx @sets.elems;
   my $ix         = (@ranked.first: * == True, :k)+1, 
   my ($arf,$art) = ($ix, $ix+$n) >>/>> 2;

   for @sets.kv -> \i,@set { for @set -> $b { @sr[i] += $b ?? $art !! $arf } } 

   my $H = [+] @sr.kv.map: -> \i,\s { s×s/@sets[i].elems }
  #my $H = [+] gather for @sr.kv -> \i,\s { take s×s/@sets[i].elems } # OK (but slower?)
   return 12/($n×($n+1)) × $H - 3 × ($n + 1)
}

$result ~= "Total subjects: $nSubjects\n";

my ($midpoint,$unt,$irr,$reg,$hunt,$hirr,$hreg,@sunt,@sirr,@sreg)=$duration div 2;

for 1 .. $duration -> \day { 
   update();
   if day %% $interval or day == $duration or day == $midpoint { 
      @sunt = @hadcovid[ @category.grep: UNTREATED,:k ];
      @sirr = @hadcovid[ @category.grep: IRREGULAR,:k ]; 
      @sreg = @hadcovid[ @category.grep: REGULAR,  :k ]; 
     #($unt,$irr,$reg)    = (@sunt,@sirr,@sreg).map: *.elems;
     #($hunt,$hirr,$hreg) = (@sunt,@sirr,@sreg).map: *.sum;
      ($unt,$hunt,$irr,$hirr,$reg,$hreg)=(@sunt,@sirr,@sreg).map:{|(.elems,.sum)} # works fine, but messes with 'srand'
   } 
   if day %% $interval {
      $result ~= sprintf "Day %d:\n",day;
      $result ~= sprintf "Untreated:     N = %4d, with infection = %4d\n",  $unt,$hunt;
      $result ~= sprintf "Irregular Use: N = %4d, with infection = %4d\n",  $irr,$hirr;
      $result ~= sprintf "Regular Use:   N = %4d, with infection = %4d\n\n",$reg,$hreg
   } 
   if day == $midpoint | $duration { 
      my $stage = day == $midpoint ?? 'midpoint' !! 'study end';
      $result ~= sprintf "At $stage, Infection case percentages are:\n";
      $result ~= sprintf "  Untreated : %f\n",  100×$hunt/$unt;
      $result ~= sprintf "  Irregulars: %f\n",  100×$hirr/$irr;
      $result ~= sprintf "  Regulars  : %f\n\n",100×$hreg/$reg
   }    
}

$result ~= sprintf "Final statistics: H = %f\n", kruskal ( @sunt, @sirr, @sreg );

say $result;

my $ref = q:to/END/;
Total subjects: 2500
Day 30:
Untreated:     N = 2166, with infection =   74
Irregular Use: N =  334, with infection =    6
Regular Use:   N =    0, with infection =    0

Day 60:
Untreated:     N = 1868, with infection =  141
Irregular Use: N =  600, with infection =   19
Regular Use:   N =   32, with infection =    0

Day 90:
Untreated:     N = 1615, with infection =  194
Irregular Use: N =  628, with infection =   33
Regular Use:   N =  257, with infection =    1

At midpoint, Infection case percentages are:
  Untreated : 12.012384
  Irregulars: 5.254777
  Regulars  : 0.389105

Day 120:
Untreated:     N = 1427, with infection =  237
Irregular Use: N =  534, with infection =   48
Regular Use:   N =  539, with infection =   14

Day 150:
Untreated:     N = 1258, with infection =  269
Irregular Use: N =  465, with infection =   58
Regular Use:   N =  777, with infection =   27

Day 180:
Untreated:     N = 1126, with infection =  288
Irregular Use: N =  390, with infection =   69
Regular Use:   N =  984, with infection =   60

At study end, Infection case percentages are:
  Untreated : 25.577265
  Irregulars: 17.692308
  Regulars  : 6.097561

Final statistics: H = 59.895495

END

use Test;
is $result.subst(/<ws>/, '', :g), $ref.subst(/<ws>/, '', :g);
