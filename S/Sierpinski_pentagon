#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Sierpinski_pentagon
#c# 2016-01-22 <RC, 2018-11-17 >RC
#t# graphical
#m# MOAR: OK
#j#  JVM: OK
#f# RC file: sierpinski_pentagon.svg
#n# fixed co-ordinates problem  ('transform' directive goes with each 'polygon')

constant order  = 5;
constant $dim   = 250;
constant $sides = 5;
constant scaling-factor = ( 3 - 5**.5 ) / 2;
my @orders = ((1 - scaling-factor) × $dim) «×» scaling-factor «**» (^order);

my $fh = open('run/sierpinski_pentagon.svg', :w);

$fh.say: qq|<svg height="{$dim×2}" width="{$dim×2}" style="fill:blue" version="1.1" xmlns="http://www.w3.org/2000/svg">|;

my @vertices = map { cis( $_ × τ / $sides ) }, ^$sides;

for 0 ..^ $sides ** order -> $i {
   my $vector = [+] @vertices[$i.base($sides).fmt("%{order}d").comb] «×» @orders;
   $fh.say: pgon ((@orders[*-1] × (1 - scaling-factor)) «×» @vertices «+» $vector)».reals».fmt("%0.3f");
};

sub pgon (@q) { qq|<polygon points="{@q}" transform="translate({$dim},{$dim}) rotate(-18)"/>| }

$fh.say: '</svg>'; 
$fh.close;

