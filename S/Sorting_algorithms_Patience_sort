#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Sorting_algorithms/Patience_sort
#c# 2015-10-23 <RC
#m# MOAR: OK
#j#  JVM: OK

#srand 123456;

multi patience(*@deck) {
    my @stacks;
    for @deck -> $card {
        with @stacks.first: $card before *[*-1] -> $stack {
            $stack.push: $card;
        }
        else {
            @stacks.push: [$card];
        }
    }
    gather while @stacks {
        take .pop given min :by(*[*-1]), @stacks;
        @stacks .= grep: +*;
    }
}

#say ~patience ^10 . pick(*);

say patience ^10 . pick(*);

use Test;
is (patience ^10 . pick(*)), qw<0 1 2 3 4 5 6 7 8 9>, 'Zero to nine.';

my @weights = (^50).map: { 100 + ( 1000.rand.Int / 10 ) };
say @weights.sort.Str eq @weights.&patience.Str ?? 'ok' !! 'not ok';

