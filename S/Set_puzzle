#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Set_puzzle
#c# 2016-01-19 <>RC
#m# MOAR: OK
#j#  JVM: OK

my @res;

srand 123456;

# The trick here is to allocate three different bits for each enum, with
# the result that the cards of a matching set OR together to produce a
# 4-digit octal number that contains only the digits 1, 2, 4, or 7. This OR
# is done by funny looking [+|], which is the reduction form of +|, which is
# the numeric bitwise OR. (Because Raku stole the bare | operator for
# composing junctions instead.)

enum Color (red => 0o1000, green =>  0o2000, purple => 0o4000);
enum Count (one =>  0o100, two =>     0o200, three =>   0o400);
enum Shape (oval =>  0o10, squiggle => 0o20, diamond =>  0o40);
enum Style (solid =>  0o1, open =>      0o2, striped =>   0o4);

my @deck = sort Color.enums X Count.enums X Shape.enums X Style.enums;  # DH change

my $DRAW = 9, my $GOAL = $DRAW div 2;

sub show-cards(@c) { { @res.push: sprintf "%9s%7s%10s%9s", @c[$_;*]».key } for ^@c }

my @combinations = [^$DRAW].combinations(3);

my @draw;
repeat until (my @sets) == $GOAL {
    @draw = @deck.pick($DRAW);
    my @bits = @draw.map: { [+] @^enums».value }
    @sets = gather for @combinations -> @c {
        take @draw[@c].item when /^ <[1247]>+ $/ given ( [+|] @bits[@c] ).base(8);
    }
}

@res.push: "Drew $DRAW cards:";
show-cards @draw;
for @sets.kv -> $i, @cards {
    @res.push: "\nSet {$i+1}:";
    show-cards @cards;
}

.say for @res;

my $moar-terminal = qq:to/END/;
Drew 9 cards:
   purple  three   diamond    solid
   purple    one   diamond  striped
    green    one  squiggle  striped
   purple  three  squiggle  striped
      red    two      oval     open
   purple  three   diamond  striped
      red    two      oval  striped
    green  three  squiggle  striped
      red  three  squiggle  striped

Set 1:
   purple  three   diamond    solid
    green    one  squiggle  striped
      red    two      oval     open

Set 2:
   purple    one   diamond  striped
      red    two      oval  striped
    green  three  squiggle  striped

Set 3:
    green    one  squiggle  striped
   purple  three   diamond  striped
      red    two      oval  striped

Set 4:
   purple  three  squiggle  striped
    green  three  squiggle  striped
      red  three  squiggle  striped
END

my $moar-cronjob = qq:to/END/;
Drew 9 cards:
    green    one   diamond     open
    green    two      oval    solid
   purple    one      oval  striped
    green  three      oval  striped
    green  three   diamond     open
   purple  three  squiggle     open
      red  three      oval     open
   purple  three      oval    solid
   purple    two      oval     open

Set 1:
    green    two      oval    solid
   purple    one      oval  striped
      red  three      oval     open

Set 2:
   purple    one      oval  striped
   purple  three      oval    solid
   purple    two      oval     open

Set 3:
    green  three      oval  striped
      red  three      oval     open
   purple  three      oval    solid

Set 4:
    green  three   diamond     open
   purple  three  squiggle     open
      red  three      oval     open
END

my $jvm = qq:to/END/;
Drew 9 cards:
   purple  three  squiggle     open
    green    one   diamond  striped
      red    two  squiggle  striped
      red    two   diamond  striped
      red    one  squiggle    solid
      red  three  squiggle     open
      red    one      oval    solid
    green  three  squiggle     open
    green    two      oval    solid

Set 1:
   purple  three  squiggle     open
      red  three  squiggle     open
    green  three  squiggle     open

Set 2:
    green    one   diamond  striped
    green  three  squiggle     open
    green    two      oval    solid

Set 3:
      red    two  squiggle  striped
      red    one  squiggle    solid
      red  three  squiggle     open

Set 4:
      red    two   diamond  striped
      red  three  squiggle     open
      red    one      oval    solid
END

use Test;
is @res.join("\n"), chomp $*VM ~~ /jvm/ ?? $jvm !! $*IN.t ?? $moar-terminal !! $moar-cronjob;

=finish

grep -A 1 'got:' bench/2022*-m/Set_puzzle.out | grep -v got: | grep bench | pf 's/^.*#//' | sortcnt

2022-08-24 
So far in 2022, distribution of the 1st card dealt:

  74        red    one  squiggle    solid
  42     purple  three   diamond    solid
  26        red    one   diamond    solid
  24      green  three      oval    solid
  19      green    one  squiggle  striped
   9     purple  three   diamond     open
   9      green    one   diamond     open
   6        red  three      oval     open
   4     purple    one      oval  striped
   2     purple    two  squiggle    solid
   2        red    two      oval    solid
   1      green  three      oval  striped
