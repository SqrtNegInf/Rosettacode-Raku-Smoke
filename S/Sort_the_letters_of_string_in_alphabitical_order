#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Sort_the_letters_of_string_in_alphabitical_order
#c# 2021-07-24 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

sub sort_within_string ( $_ is copy ) {
    constant @lexographic_order = sort *.fc, map &chr, 1..255;

    return join '', gather for @lexographic_order -> $l {
        my $count = s:g/$l//;
        take $l x $count;
        LAST { warn "Original string had non-ASCII chars: {.raku}" if .chars }
    }
}
@res.push: trim .&sort_within_string for q:to/END/.lines;
    The quick brown fox jumps over the lazy dog, apparently
    Now is the time for all good men to come to the aid of their country.
    END

.say for @res;

my $ref = q:to/END/;
,aaabcdeeeefghhijkllmnnoooopppqrrrsTttuuvwxyyz
.aaccddeeeeeeffghhhiiiillmmmNnnooooooooorrrstttttttuwy
END

use Test;
is @res.join("\n"), chomp $ref;
