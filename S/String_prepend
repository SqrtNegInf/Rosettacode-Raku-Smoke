#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/String_prepend
#c# 2015-12-06 <RC, 2017-03-09 >RC
#m# MOAR: OK
#j#  JVM: OK

my @res;

# explicit concatentation
$_ = 'byte';
$_ = 'kilo' ~ $_;
@res.push: $_;

# interpolation as concatenation
$_ = 'buck';
$_ = "mega$_";
@res.push: $_;

# lvalue substr
$_ = 'bit';
substr-rw($_,0,0) = 'nano';
@res.push: $_;

# regex substitution
$_ = 'fortnight';
s[^] = 'micro';
@res.push: $_;

# reversed append assignment
# was a bug for a while: 2017-03-09 Potential difficulties: Useless use of [R~]= in sink context
$_ = 'cooper'; 
$_ [R~]= 'mini';
@res.push: $_;

.say for @res;

my $ref = qq:to/END/;
kilobyte
megabuck
nanobit
microfortnight
minicooper
END

use Test;
is @res.join("\n"), chomp $ref;
