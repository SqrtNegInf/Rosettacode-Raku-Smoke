#!/usr/bin/env perl6
#u# https://rosettacode.org/wiki/Sisyphus_sequence
#c# 2023-0x-xx NOT ON RC
#n# just skip saving the sequence, grab only the checkpoints, much faster
#n# 3..7:  139.95s user  1.53s system  95% cpu  2:27.51
#n# 3..8: 1429.53s user 13.33s system  95% cpu 25:03.61

my @res;

use Math::Primesieve;
use Lingua::EN::Numbers;

my $iterator = Math::Primesieve::iterator.new;

my ($lo, $hi, $n, $s0, $s1, $p, @S100, %S) = 3, 5, 1, 1, Any, Any, 1;
my @Nth = ($lo..$hi)».exp(10);

repeat {
    $n++;
    $s1 = $s0 %% 2 ?? $s0 div 2 !! $s0 + ($p = $iterator.next);
    push @S100, $s1 if $n ≤ 100;
    %S{$n}{'value', 'prime'} = $s1, $p if $n ∈ @Nth;
    $s0 = $s1;
} until $n == 10**$hi;

@res.push: 'The first 100 members of the Sisyphus sequence are:';
@res.push: @S100.batch(10)».fmt('%4d').join("\n");
@res.push: '';

push @res, sprintf "%12sth member is: %10s with prime: %10s", ($_, %S{$_}{'value'}, %S{$_}{'prime'})».&comma for @Nth;

say my $result = join "\n", @res;
#exit;

my $ref = q:to/END/;
The first 100 members of the Sisyphus sequence are:
   1    3    6    3    8    4    2    1    8    4
   2    1   12    6    3   16    8    4    2    1
  18    9   28   14    7   30   15   44   22   11
  42   21   58   29   70   35   78   39   86   43
  96   48   24   12    6    3   62   31   92   46
  23   90   45  116   58   29  102   51  130   65
 148   74   37  126   63  160   80   40   20   10
   5  106   53  156   78   39  146   73  182   91
 204  102   51  178   89  220  110   55  192   96
  48   24   12    6    3  142   71  220  110   55

       1,000th member is:        990 with prime:      2,273
      10,000th member is:     24,975 with prime:     30,713
     100,000th member is:    265,781 with prime:    392,111
END
#  1,000,000th member is:  8,820,834 with prime:  4,761,697
# 10,000,000th member is: 41,369,713 with prime: 55,900,829
#100,000,000th member is: 1,179,614,168 with prime: 640,692,323

use Test;
is @res.join("\n"), chomp $ref;

=finish

=={{header|Raku}}==
{{libheader|ntheory}}
<syntaxhighlight lang="perl" line>
</syntaxhighlight>
{{out}}
<pre>
</pre>
