#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Strip_control_codes_and_extended_characters_from_a_string
#c# 2016-03-19 <RC, 2018-04-08 <RC
#m# MOAR: OK
#j#  JVM: OK

use myRNG;

# Knuth shuffle
sub pick-all (@a is copy) is export {
    my $rng = myRNG.new( :seed(123456) );
    for 1 ..^ @a -> $n {
        my $k = (0 .. $n)[($n+1) × $rng.next-rat];
        $k == $n or @a[$k, $n] = @a[$n, $k];
    }
    @a
}

my $str = (0..400).&pick-all.head(80)».chr.join;

say $str;
say $str.subst(/<:Cc>/,     '', :g); # unicode property: control character
say '----';
say my $result = $str.subst(/<-[ \ ..~ ]>/, '', :g);

use Test;
is $result, 'whB{;ME^sp1d!jm=Yk';
