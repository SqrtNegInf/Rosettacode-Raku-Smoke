#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/SEDOLs
#c# 2015-12-17 <>RC
#m# MOAR: OK
#j#  JVM: OK

#Translation of: Perl

sub sedol( Str $s ) {
    die 'No vowels allowed' if $s  ~~ /<[AEIOU]>/;
    die 'Invalid format'    if $s !~~ /^ <[0..9B..DF..HJ..NP..TV..Z]>**6 $ /;

    #my %base36 = flat (flat 0..9, 'A'..'Z') Z ^36;
    my %base36 = (flat 0..9, 'A'..'Z') »=>« ^36;
    my @weights = 1, 3, 1, 7, 3, 9;

    my @vs = %base36{ $s.comb };
    my $checksum = [+] @vs Z× @weights;
    my $check_digit = (10 - $checksum % 10) % 10;
    return $s ~ $check_digit;
}

my @res.push: sedol($_) for <710889 B0YBKJ 406566 B0YBLH 228276 B0YBKL 557910 B0YBKR 585284 B0YBKT B00030>;
.say for @res;

my $ref = qq:to/END/;
7108899
B0YBKJ7
4065663
B0YBLH2
2282765
B0YBKL9
5579107
B0YBKR5
5852842
B0YBKT7
B000300
END

use Test;
is @res.join("\n"), chomp $ref;
