#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Selectively_replace_multiple_instances_of_a_character_within_a_string
#c# 2022-05-31 <RC
#m# MOAR: OK
#j# JVM:  OK

srand 123456;

my @res;

sub mangle ($str is copy) {
    $str.match(:ex, 'a')».from.map: { $str.substr-rw($_, 1) = 'ABaCD'.comb[$++] };
    $str.=subst('b', 'E');
    $str.substr-rw($_, 1) = 'F' given $str.match(:ex, 'r')».from[1];
    $str
}

@res.push: $_ ~ ' -> ' ~ .&mangle given 'abracadabra';

@res.push: $_ ~ ' -> ' ~ .&mangle given 'abracadabra'.comb.pick(*).join;

.say for @res;

my $moar-terminal = q:to/END/;
abracadabra -> AErBcadCbFD
dbacbarraaa -> dEAcbBrFaCD
END

my $moar-cronjob = q:to/END/;
abracadabra -> AErBcadCbFD
abdcraaraba -> AEdcrBaFCbD
END

my $jvm = q:to/END/;
abracadabra -> AErBcadCbFD
crraaabaabd -> crFABaECDbd
END

use Test;
is @res.join("\n"), chomp $*VM ~~ /jvm/ ?? $jvm !! $*IN.t ?? $moar-terminal !! $moar-cronjob;
