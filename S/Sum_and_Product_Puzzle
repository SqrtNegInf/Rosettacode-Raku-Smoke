#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Sum_and_Product_Puzzle
#c# 2016-08-06 <RC
#m# MOAR: OK
#j#  JVM: OK

# Translation of: Python

sub grep-unique (&by, @list) { @list.classify(&by).values.grep(* == 1).map(*[0]) }
sub sums        ($n)         { ($_, $n - $_ for 2 .. $n div 2) }
sub sum         ([$x, $y])   { $x + $y }
sub product     ([$x, $y])   { $x Ã— $y }

my @all-pairs = (|($_ X $_+1 .. 98) for 2..97);

# Fact 1:
my %p-unique := Set.new: map ~*, grep-unique &product, @all-pairs;
my @s-pairs = @all-pairs.grep: { none (%p-unique{~$_} for sums sum $_) };

# Fact 2:
my @p-pairs = grep-unique &product, @s-pairs;

# Fact 3:
my @final-pairs = grep-unique &sum, @p-pairs;

printf "X = %d, Y = %d\n", |$_ for @final-pairs;

# X = 4, Y = 13

use Test;
is @final-pairs, '4 13';
