#!/usr/bin/env raku
#u# https://rosettacode.org/wiki/SEND_+_MORE_=_MONEY
#c# 2023-02-10 <RC
#m# MOAR: BROKEN OK
#j# JVM:  BROKEN OK
#n# Fast

my @res;

# Alternately, a version written in 2015 by Carl Mäsak. 
# Not very concise but quite speedy. 
# Applying the observation that M must be 1 and S must be either 8 or 9 gets the runtime under a tenth of a second.

my int $s = 7;
while ++$s <= 9 {
    next if $s == 0;

    my int $e = -1;
    while ++$e <= 9 {
        next if $e == $s;

        my int $n = -1;
        while ++$n <= 9 {
            next if $n == $s;
            next if $n == $e;

            my int $d = -1;
            while ++$d <= 9 {
                next if $d == $s;
                next if $d == $e;
                next if $d == $n;

                my int $send = $s×1000 + $e×100 + $n×10 + $d;

                my int $m = 1;

                my int $o = -1;
                while ++$o <= 9 {
                    next if $o == $s;
                    next if $o == $e;
                    next if $o == $n;
                    next if $o == $d;
                    next if $o == $m;

                    my int $r = -1;
                    while ++$r <= 9 {
                        next if $r == $s;
                        next if $r == $e;
                        next if $r == $n;
                        next if $r == $d;
                        next if $r == $m;
                        next if $r == $o;

                        my int $more = $m×1000 + $o×100 + $r×10 + $e;

                        my int $y = -1;
                        while ++$y <= 9 {
                            next if $y == $s;
                            next if $y == $e;
                            next if $y == $n;
                            next if $y == $d;
                            next if $y == $m;
                            next if $y == $o;
                            next if $y == $r;

                            my int $money =
                                $m×10000 + $o×1000 + $n×100 + $e×10 + $y;
                            next unless $send + $more == $money;
                            @res.push: 'SEND + MORE == MONEY';
                            @res.push: "$send + $more == $money";
                        }
                    }
                }
            }
        }
    }
}
#printf "%.3f elapsed seconds", now - INIT now;

.say for @res;

my $ref = q:to/END/;
SEND + MORE == MONEY
9567 + 1085 == 10652
END

use Test;
is @res.join("\n"), chomp $ref;
