#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Search_a_list
#c# 2016-01-19 <RC
#m# MOAR: OK
#j#  JVM: OK

# The ~~ operator does smart matching based on the type of the matcher; in
# the general case, you can pass any predicate you like to force the
# semantics one way or another.

sub find ($matcher, $container) {
    for $container.kv -> $k, $v {
        $v ~~ $matcher and return $k;
    }
    fail 'No values matched';
}

my Str @haystack = <Zig Zag Wally Ronald Bush Krusty Charlie Bush Bozo>;

my @res;
for <Washingston Bush> -> $needle {
    my $pos = find $needle, @haystack;
    if defined $pos {
        @res.push: "Found '$needle' at index $pos";
        @res.push: ('Largest index: ', @haystack.end - find { $needle eq $^x }, reverse @haystack);
    }
    else {
        @res.push: "'$needle' not in haystack";
    }
}

my $ref = qq:to/END/;
'Washingston' not in haystack
Found 'Bush' at index 4
Largest index:  7
END

use Test;
is @res.join("\n"), chomp $ref;
