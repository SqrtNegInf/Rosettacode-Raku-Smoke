#!/usr/bin/env raku
#u# https://rosettacode.org/wiki/Set,_the_card_game
#c# 2023-08-31 <RC
#m# MOAR: OK
#j# JVM:  BROKEN

my @res;

use myRNG;
my $rng = myRNG.new( :seed(123456) );

my @attributes = <one two three>, <solid striped open>, <red green purple>, <diamond oval squiggle>;

sub face ($_) { .polymod(3 xx 3).kv.map({ @attributes[$^k;$^v] }) ~ ('s' if $_%3) }

sub sets (@cards) { @cards.combinations(3).race.grep: { !(sum ([Z+] $_».polymod(3 xx 3)) »%» 3) } }

for 4,8,12 -> $deal {
    my @cards = (^81).&pickall.head($deal);
    my @sets = @cards.&sets;
    @res.push: "\nCards dealt: $deal";
    for @cards { @res.push: .&face };
    @res.push: "\nSets found: {+@sets}";
    for @sets { @res.push: .map(&face).join("\n"), "\n" };
}

@res.push: "\nIn the whole deck, there are {+(^81).&sets} sets.";

.say for @res;

my $ref = q:to/END/;

Cards dealt: 4
one striped purple diamond
one solid red diamond
two solid red ovals
three solid purple squiggles

Sets found: 0

Cards dealt: 8
one striped purple diamond
one solid red diamond
two solid red ovals
three solid purple squiggles
three open purple ovals
two solid red squiggles
one striped red diamond
three open green squiggles

Sets found: 1
one striped purple diamond
two solid red ovals
three open green squiggles



Cards dealt: 12
one striped purple diamond
one solid red diamond
two solid red ovals
three solid purple squiggles
three open purple ovals
two solid red squiggles
one striped red diamond
three open green squiggles
one striped green squiggle
two striped purple diamonds
two open green ovals
two open purple squiggles

Sets found: 3
two solid red squiggles
two striped purple diamonds
two open green ovals


one striped purple diamond
two solid red ovals
three open green squiggles


three solid purple squiggles
one striped red diamond
two open green ovals



In the whole deck, there are 1080 sets.
END

use Test;
is @res.join("\n"), chomp $ref;
