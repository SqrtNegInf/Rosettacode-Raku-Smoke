#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Simulated_annealing
#c# 2020-01-23 <RC, 2020-01-27 >RC
#m# MOAR: OK
#j# JVM:  OK
#n# 2020-10-07 different answers when run from interactive shell vs cron-launch?
#n# 2022-11-04 replace call to built-in 'rand' for output stability (slower this way, so reduce iterations)
#n#
#n# Notes on revisions (see .1 for original version as uploaded by Hkdtam++)
#n#
#n# more flexible on input grid (but must be perfect square), 'magic' numbers keyed to 10x10 grid go away
#n# make distance array available globally (no lookup function)
#n# more idiomatic syntax (e.g. no C-style loops)
#n# remove intermediate variables when feasible
#n# rename some variables to better reflect their roles
#n# reformat code to emphasize parallelism
#n# .rotor suffices to make grid output (no separate print routine)
#n# output follows task spec (less is more)
#n# limit displayed values to non-redonkulous precision (this is not the LIGO experiment)
#n# extended sigil-less style (temp. variable kludges clobbered with 'anonymous containers', thundergnat++)
#n# single/double quote style: former for static strings, latter for ones needing interpolation
#n# eliminated 'sa' as routine (to eliminate need for globals could have packed *everything* into it, but 6-of-one, 1/2-dozen...)

my @res;

class splitmix64 {
    has $!state;
    submethod BUILD ( Int :$seed where * >= 0 = 1 ) { $!state = $seed }
    method next-int {
        my $next = $!state = ($!state + 0x9e3779b97f4a7c15) +& (2⁶⁴ - 1);
        $next = ($next +^ ($next +> 30)) × 0xbf58476d1ce4e5b9 +& (2⁶⁴ - 1);
        $next = ($next +^ ($next +> 27)) × 0x94d049bb133111eb +& (2⁶⁴ - 1);
        ($next +^ ($next +> 31)) +& (2⁶⁴ - 1);
    }
    method next-rat { self.next-int / 2⁶⁴ }
}

my $rng = splitmix64.new( :seed(987654321) );

# Knuth shuffle
sub shuffle (@a is copy) {
    for 1 ..^ @a -> $n {
        my $k = (0 .. $n)[($n+1) × $rng.next-rat];
        $k == $n or @a[$k, $n] = @a[$n, $k];
    }
    @a
}

# simulation setup
my \cities = 100;  # number of cities
my \kmax   = 1e5;  # iterations to run
my \kT     = 1;    # initial 'temperature'

die 'City count must be a perfect square.' if cities.sqrt != cities.sqrt.Int;

# locations of (up to) 8 neighbors, with grid size derived from number of cities
my \gs = cities.sqrt;
my \neighbors = [1, -1, gs, -gs, gs-1, gs+1, -(gs+1), -(gs-1)];

# matrix of distances between cities
my \D = [;];
for 0 ..^ cities² -> \j {
    my (\ab, \cd)       = (j/cities, j%cities)».Int;
    my (\a, \b, \c, \d) = (ab/gs, ab%gs, cd/gs, cd%gs)».Int;
    D[ab;cd] = sqrt (a - c)² + (b - d)²
}

sub T(\k, \kmax, \kT)      { (1 - k/kmax) × kT }                                 # temperature function, decreases to 0
sub P(\ΔE, \k, \kmax, \kT) { exp -ΔE / T(k, kmax, kT) }                          # probability to move from s to s_next
sub Es(\path)              { sum (D[ path[$_]; path[$_+1] ] for 0 ..^ +path-1) } # energy at s, to be minimized

# variation of E, from state s to state s_next
sub delta-E(\s, \u, \v) {
    my (\a,   \b,  \c,  \d) = D[s[u-1];s[u]], D[s[u+1];s[u]], D[s[v-1];s[v]], D[s[v+1];s[v]];
    my (\na, \nb, \nc, \nd) = D[s[u-1];s[v]], D[s[u+1];s[v]], D[s[v-1];s[u]], D[s[v+1];s[u]];
    if    v == u+1 { return (na + nd) - (a + d) }
    elsif u == v+1 { return (nc + nb) - (c + b) }
    else           { return (na + nb + nc + nd) - (a + b + c + d) }
}

# E(s0), initial state
my \s = @ = flat 0, shuffle(1 ..^ cities), 0;
#my \s = @ = flat 0, (1 ..^ cities).pick(*), 0;  # use anonymous array container to create sigil-less variable
my \E-min-global = my \E-min = $ = Es(s);       # likewise, with scalar

# redundant -- 1st row of progress table shows the same information
#@res.push: 'kT = ' ~ \kT;  
#@res.push: 'E(s0): ' ~ E-min.fmt('%.1f');

for 0 ..^ kmax -> \k {
    @res.push: sprintf 'k:%8u  T:%4.1f  Es: %3.1f' , k, T(k, kmax, kT), Es(s)
        if k % (kmax/10) == 0;

    # valid candidate cities (exist, adjacent)
    my \cv = neighbors[8×$rng.next-rat] + s[ my \u = 1 + floor((cities-1)×$rng.next-rat)];
    next if cv ≤ 0 or cv ≥ cities or D[s[u];cv] > sqrt(2);

    my \v  = s[cv];
    my \ΔE = delta-E(s, u, v);
    if ΔE < 0 or P(ΔE, k, kmax, kT) ≥ $rng.next-rat { # always move if negative
        (s[u], s[v]) = (s[v], s[u]);
        E-min += ΔE;
        E-min-global = E-min if E-min < E-min-global;
    }
}

@res.push: "\nE(s_final): " ~ E-min-global.fmt('%.1f');
@res.push: "Path:\n" ~ s».fmt('%2d').rotor(20,:partial).join: "\n";

say my $result = @res.join("\n");

my $ref = q:to/END/;
k:       0  T: 1.0  Es: 504.1
k:   10000  T: 0.9  Es: 216.9
k:   20000  T: 0.8  Es: 202.9
k:   30000  T: 0.7  Es: 185.8
k:   40000  T: 0.6  Es: 161.9
k:   50000  T: 0.5  Es: 154.4
k:   60000  T: 0.4  Es: 144.4
k:   70000  T: 0.3  Es: 141.8
k:   80000  T: 0.2  Es: 132.3
k:   90000  T: 0.1  Es: 132.3

E(s_final): 131.9
Path:
 0 11 10 20 51 65 67 69 59 58 47 66 77 78 88 97 96 95 94 86
87 79 89 99 98 85 84 83 93 92 91 81 90 80 70 71 72 82 62 73
63 64 74 75 76 68 57 56 55 54 53 43 44 48 49 39 38 37 46 45
52 61 60 50 40 21 12  2  3 13 22 32 31 30 41 42 33 34 35 36
28 29 19  9 17 16 15 14  4  5  6  7  8 18 27 26 25 24 23  1
 0
END

use Test;
is @res.join("\n"), chomp $ref;
