#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Seven-sided_dice_from_five-sided_dice
#c# 2015-10-26 <RC, 2018-04-08 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# could switch back to 1_000_000 iterations, fast enough now
#n# new-disp 'srand' shift

srand 123456;

my $d5 = 1..5;
sub d5() { $d5.roll; }  # 1d5

sub d7() {
    my $flat = 21;
    $flat = 5 × d5() - d5() until $flat < 21;
    $flat % 7 + 1;
}

my @dist;
my $n = 100_000;
#my $n = 1_000_000; # results not stable (and verrrry slow for JVM)
my $expect = $n / 7;

loop ($_ = $n; $n; --$n) { @dist[d7()]++; }

my @res.push: "Expect " ~ $expect.fmt("%.3f");
for @dist.kv -> $i, $v {
    @res.push: "$i $v " ~ (($v - $expect)/$expect×100).fmt("%+.2f%%") if $v;
}

.say for @res;

my $moar-terminal = qq:to/END/;
Expect 14285.714
1 14376 +0.63%
2 14311 +0.18%
3 14295 +0.07%
4 14325 +0.28%
5 14412 +0.88%
6 14084 -1.41%
7 14197 -0.62%
END

my $moar-cronjob = qq:to/END/;
Expect 14285.714
1 14376 +0.63%
2 14311 +0.18%
3 14296 +0.07%
4 14325 +0.28%
5 14409 +0.86%
6 14084 -1.41%
7 14199 -0.61%
END

my $jvm = qq:to/END/;
Expect 14285.714
1 14247 -0.27%
2 14190 -0.67%
3 14082 -1.43%
4 14352 +0.46%
5 14261 -0.17%
6 14430 +1.01%
7 14438 +1.07%
END

use Test;
is @res.join("\n"), chomp $*VM ~~ /jvm/ ?? $jvm !! $*IN.t ?? $moar-terminal !! $moar-cronjob;
