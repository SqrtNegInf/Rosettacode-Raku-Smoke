#!/usr/bin/env raku
#u# https://rosettacode.org/wiki/SEND_+_MORE_=_MONEY
#c# 2023-02-10 <RC
#m# MOAR: BROKEN OK
#j# JVM:  BROKEN OK
#n# Idiomatic

my @res;

enum <D E M N O R S Y>;

sub find_solution ( ) {
    for ('0'..'9').combinations(8) -> @c {
        .return with @c.permutations.first: -> @p {
            @p[M] !== 0 and

            @p[  S,E,N,D].join
          + @p[  M,O,R,E].join
         == @p[M,O,N,E,Y].join
        }
    }
}

my @s = find_solution();
@res.push: "    {@s[  S,E,N,D].join}";
@res.push: " +  {@s[  M,O,R,E].join}";
@res.push: "== { @s[M,O,N,E,Y].join}";

.say for @res;

my $ref = q:to/END/;
    9567
 +  1085
== 10652
END

use Test;
is @res.join("\n"), chomp $ref;
