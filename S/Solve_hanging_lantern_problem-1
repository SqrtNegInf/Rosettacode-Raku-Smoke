#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Lantern_Problem
#c# 2022-05-22 <RC, 2022-05-25 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# OEIS:A022915 Multinomial coefficients (0, 1, ..., n)! = C(n+1,2)!/(0!×1!×2!×...×n!)

my @res;

unit sub MAIN(@columns = (1, 2, 3) );

my @sequences = @columns
              . pairs
              . map({ (.key+1) xx .value })
              . flat
              . permutations
              . map( *.join(',') )
              . unique;

sub postfix:<!>($n) { [×] 1..$n }
 
# no space after '[+]' (aka 'sum'), but like [+]/[×] pairing so don't replace with 'sum'
# OTOH [×] needs either parens or a space
@res.push: "Direct calculation: " ~ [+](@columns)! / [×] @columns»!;  # or:: [×](@columns»!);  

@res.push: "\nThere are {+@sequences} possible takedown sequences:";
@res.push: "[$_]" for @sequences.head: 6;
@res.push: '...';
@res.push: "[$_]" for @sequences.tail: 6;

.say for @res;

my $ref = q:to/END/;
Direct calculation: 60

There are 60 possible takedown sequences:
[1,2,2,3,3,3]
[1,2,3,2,3,3]
[1,2,3,3,2,3]
[1,2,3,3,3,2]
[1,3,2,2,3,3]
[1,3,2,3,2,3]
...
[3,3,2,2,3,1]
[3,3,2,3,1,2]
[3,3,2,3,2,1]
[3,3,3,1,2,2]
[3,3,3,2,1,2]
[3,3,3,2,2,1]
END

use Test;
is @res.join("\n"), chomp $ref;
