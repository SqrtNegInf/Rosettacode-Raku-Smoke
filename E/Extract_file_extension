#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Extract_file_extension
#c# 2016-01-06 <RC
#m# MOAR: OK
#j#  JVM: OK

sub extension ( Str $filename --> Str ) {
    given $filename.split(/\./)[*-1] {
        when $filename   { '' }
        when / <[\/_]> / { '' }
        default          { '.' ~ $_ }
    }
}

my @res.push: "$_ -> "~ extension($_) for (
    'mywebsite.com/picture/image.png',
    'http://mywebsite.com/picture/image.png',
    'myuniquefile.longextension',
    'IAmAFileWithoutExtension',
    '/path/to.my/file',
    'file.odd_one',
);

.say for @res;

my $ref = qq:to/END/;
mywebsite.com/picture/image.png -> .png
http://mywebsite.com/picture/image.png -> .png
myuniquefile.longextension -> .longextension
IAmAFileWithoutExtension ->
/path/to.my/file ->
file.odd_one ->
END

use Test;
is @res.join('').subst(/<ws>/, '', :g), $ref.subst(/<ws>/, '', :g);
