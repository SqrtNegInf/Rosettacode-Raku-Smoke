#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Exponentiation_with_infix_operators_in_(or_operating_on)_the_base
#c# 2020-11-05 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# Unicode symbol '↑' is what sinks use of .EVAL syntax.

use MONKEY-SEE-NO-EVAL;

my @res;

sub infix:<↑> is looser(&prefix:<->) { $^a ** $^b }
sub infix:<^> is looser(&infix:<->)  { $^a ** $^b }

for 'Default precedence: infix exponentiation is tighter (higher) precedence than unary negation.',
    ('1 + -$x**$p', '1 + (-$x)**$p', '1 + (-($x)**$p)', '(1 + -$x)**$p', '1 + -($x**$p)'),

    "\nEasily modified: custom loose infix exponentiation is looser (lower) precedence than unary negation.",
    ('1 + -$x↑$p ', '1 + (-$x)↑$p ', '1 + (-($x)↑$p) ', '(1 + -$x)↑$p ', '1 + -($x↑$p) '),

    "\nEven moreso: custom looser infix exponentiation is looser (lower) precedence than infix subtraction.",
    ('1 + -$x^$p ', '1 + (-$x)^$p ', '1 + (-($x)^$p) ', '(1 + -$x)^$p ', '1 + -($x^$p) ')
    -> $message, $ops {
    @res.push: $message;
    for -5, 5 X 2, 3 -> ($x, $p) {
        my $line = sprintf "x = %2d  p = %d", $x, $p;
        -> $op { $line ~= sprintf " │ %s = %4d", $op, EVAL $op } for |$ops;
        #-> $op { $line ~= sprintf " │ %s = %4d", $op, $op.EVAL } for |$ops;
        #for |$ops -> $op { $line ~= sprintf " │ %s = %4d", $op, EVAL $op } 
        @res.push: $line;
    }
}

.say for @res;

my $ref = q:to/END/;
Default precedence: infix exponentiation is tighter (higher) precedence than unary negation.
x = -5  p = 2 │ 1 + -$x**$p =  -24 │ 1 + (-$x)**$p =   26 │ 1 + (-($x)**$p) =  -24 │ (1 + -$x)**$p =   36 │ 1 + -($x**$p) =  -24
x = -5  p = 3 │ 1 + -$x**$p =  126 │ 1 + (-$x)**$p =  126 │ 1 + (-($x)**$p) =  126 │ (1 + -$x)**$p =  216 │ 1 + -($x**$p) =  126
x =  5  p = 2 │ 1 + -$x**$p =  -24 │ 1 + (-$x)**$p =   26 │ 1 + (-($x)**$p) =  -24 │ (1 + -$x)**$p =   16 │ 1 + -($x**$p) =  -24
x =  5  p = 3 │ 1 + -$x**$p = -124 │ 1 + (-$x)**$p = -124 │ 1 + (-($x)**$p) = -124 │ (1 + -$x)**$p =  -64 │ 1 + -($x**$p) = -124

Easily modified: custom loose infix exponentiation is looser (lower) precedence than unary negation.
x = -5  p = 2 │ 1 + -$x↑$p  =   26 │ 1 + (-$x)↑$p  =   26 │ 1 + (-($x)↑$p)  =   26 │ (1 + -$x)↑$p  =   36 │ 1 + -($x↑$p)  =  -24
x = -5  p = 3 │ 1 + -$x↑$p  =  126 │ 1 + (-$x)↑$p  =  126 │ 1 + (-($x)↑$p)  =  126 │ (1 + -$x)↑$p  =  216 │ 1 + -($x↑$p)  =  126
x =  5  p = 2 │ 1 + -$x↑$p  =   26 │ 1 + (-$x)↑$p  =   26 │ 1 + (-($x)↑$p)  =   26 │ (1 + -$x)↑$p  =   16 │ 1 + -($x↑$p)  =  -24
x =  5  p = 3 │ 1 + -$x↑$p  = -124 │ 1 + (-$x)↑$p  = -124 │ 1 + (-($x)↑$p)  = -124 │ (1 + -$x)↑$p  =  -64 │ 1 + -($x↑$p)  = -124

Even moreso: custom looser infix exponentiation is looser (lower) precedence than infix subtraction.
x = -5  p = 2 │ 1 + -$x^$p  =   36 │ 1 + (-$x)^$p  =   36 │ 1 + (-($x)^$p)  =   26 │ (1 + -$x)^$p  =   36 │ 1 + -($x^$p)  =  -24
x = -5  p = 3 │ 1 + -$x^$p  =  216 │ 1 + (-$x)^$p  =  216 │ 1 + (-($x)^$p)  =  126 │ (1 + -$x)^$p  =  216 │ 1 + -($x^$p)  =  126
x =  5  p = 2 │ 1 + -$x^$p  =   16 │ 1 + (-$x)^$p  =   16 │ 1 + (-($x)^$p)  =   26 │ (1 + -$x)^$p  =   16 │ 1 + -($x^$p)  =  -24
x =  5  p = 3 │ 1 + -$x^$p  =  -64 │ 1 + (-$x)^$p  =  -64 │ 1 + (-($x)^$p)  = -124 │ (1 + -$x)^$p  =  -64 │ 1 + -($x^$p)  = -124
END

use Test;
is @res.join("\n"), chomp $ref;
