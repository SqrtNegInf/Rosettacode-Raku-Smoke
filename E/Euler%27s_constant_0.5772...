#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Euler%27s_constant_0.5772...
#c# 2021-11-26 <RC, 2021-11-27 <RC, 2022-03-22 <RC
#m# MOAR: OK
#j# JVM:  BROKEN
#n# OEIS:A001620 Euler's constant (or the Euler-Mascheroni constant), gamma.
#n# Vacca series: https://w.wiki/4ybp (see also: 'M/Meissel–Mertens_constant')
#n# *VERY* slow with FatRat

sub gamma (\N where N > 1) { 
   return  (1/2 - 1/3) + [+] (2..N).race.map: -> \n {
      my ($power, $sign, $term) = 2**n, -1;
      for ($power..^2×$power) { $term += ($sign = -$sign) / $_ }
      n×$term
   }
}

sub gamma_fat (\N --> FatRat) {
   my $sum =  (1/2 - 1/3).FatRat;    # for arbitrary precision 
   return $sum + [+] (2..N).race.map: -> \n {
      my $power = 2**n;
      my $sign = -1;
      my FatRat $term;               # for arbitrary precision
      for ($power..^2×$power) { $term += (1 / (($sign ×= -1)×$_)).FatRat }
      n×$term 
   } 
}

say my $result = gamma 23;
#say my          gamma_fat 23; # too slow

use Test;
is $result, 0.5772149198434515;
#           0.57721566490153286
