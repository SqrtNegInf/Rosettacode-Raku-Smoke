#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Extensible_prime_generator
#c# 2015-11-03 <RC
#m# MOAR: OK
#j#  JVM: OK

my @res;

my @primes = lazy gather for 1 .. * { .take if .is-prime }

@res.push: "The first twenty primes:\n   "~ "[{@primes[^20].fmt("%d", ', ')}]";
@res.push: "The primes between 100 and 150:\n   "~ "[{@primes.&between(100, 150).fmt("%d", ', ')}]";
@res.push: "The number of primes between 7,700 and 8,000:\n   "~ my $num = +@primes.&between(7700, 8000);
@res.push: "The 10,000th prime:\n   "~ @primes[9999];

sub between (@p, $l, $u) {
    gather for @p { .take if $l < $_ < $u; last if $_ >= $u }
}

.say for @res;

my $ref = qq:to/END/;
The first twenty primes:
   [2, 3, 5, 7, 11, 13, 17, 19, 23, 29, 31, 37, 41, 43, 47, 53, 59, 61, 67, 71]
The primes between 100 and 150:
   [101, 103, 107, 109, 113, 127, 131, 137, 139, 149]
The number of primes between 7,700 and 8,000:
   30
The 10,000th prime:
   104729
END

use Test;
is @res.join("\n"), chomp $ref;
