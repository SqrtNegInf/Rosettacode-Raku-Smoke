#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Erdős–Woods_numbers
#c# 2022-03-07 <RC, 2024-09-16 <RC
#m# MOAR: OK
#j# JVM:  BROKEN
#n# OEIS:A059756 length of an interval of consecutive integers such that every element has a factor in common with one of the endpoints.
#n# OEIS:A059757 smallest values of a for a given E-W number k
#n# OEIS:A111042 odd terms of A059757 sequence [not calculated below]
#n# 2022-07-13 some tweaks, not on RC

my @res;

sub ew (\n) {

   my @primes = (^n).race.grep: *.is-prime;  

   # rosettacode.org/wiki/Horner%27s_rule_for_polynomial_evaluation#Raku
   my @divs = (^n).map: -> \p { ([o] map { p%%$_.Int + 2 × * }, @primes)(0) }

   my @partitions = [ 0, 0, 2**@primes.elems - 1 ] , ;

   sub ort(\x) { (@divs[x] +| @divs[n -x]).base(2).flip.index(1) } 

   for ((1..^n).sort: *.&ort).reverse {
   #for ((n^...1).sort: *.&ort).reverse  # no special reason for this
      my @newPartitions; # = @ = (); 
      my ($factors,$otherFactors) = @divs[$_, n-$_];

      for @partitions -> @p {
         my ($setA, $setB, $rPrimes) = @p[0..2];

         @newPartitions.push: @p and next if $factors +& $setA or $otherFactors +& $setB;

         for ($factors +& $rPrimes).base(2).flip.comb.kv -> $k,$v {
            (@newPartitions.push: [ $setA +^ $_, $setB, $rPrimes +^ $_ ] given 1 +< $k) if $v == 1
         }

         for ($otherFactors +& $rPrimes).base(2).flip.comb.kv -> $k,$v {
            (@newPartitions.push: [ $setA, $setB +^ $_, $rPrimes +^ $_ ] given 1 +< $k) if $v == 1
         }
      }
      @partitions = @newPartitions
   }

   my \result = $ = -1;
   for @partitions -> @p {
      my ($px,$py)  = @p[0,1];
      my ($x , $y ) = 1 xx *;
      for @primes -> $p {
         $px % 2 and $x ×= $p;
         $py % 2 and $y ×= $p;
         ($px,$py) >>div=>> 2
      }
     #my \newresult = ((n × invmod($x, $y)) % $y) × $x - n;
      my \newresult = ((n × expmod($x, -1, $y)) % $y) × $x - n;
      result = result == -1 ?? newresult !! min(result, newresult)
   }
   return result 
}

@res.push: "The first 20 Erdős–Woods numbers and their minimum interval start values are:";
for (16..116) { if (my $ew = ew $_) > 0 { @res.push: sprintf "%3d -> %d",$_,$ew } }

.say for @res;

my $ref = q:to/END/;
The first 20 Erdős–Woods numbers and their minimum interval start values are:
 16 -> 2184
 22 -> 3521210
 34 -> 47563752566
 36 -> 12913165320
 46 -> 21653939146794
 56 -> 172481165966593120
 64 -> 808852298577787631376
 66 -> 91307018384081053554
 70 -> 1172783000213391981960
 76 -> 26214699169906862478864
 78 -> 27070317575988954996883440
 86 -> 92274830076590427944007586984
 88 -> 3061406404565905778785058155412
 92 -> 549490357654372954691289040
 94 -> 38646299993451631575358983576
 96 -> 50130345826827726114787486830
100 -> 35631233179526020414978681410
106 -> 200414275126007376521127533663324
112 -> 1022681262163316216977769066573892020
116 -> 199354011780827861571272685278371171794
END

use Test;
is @res.join("\n"), chomp $ref;
