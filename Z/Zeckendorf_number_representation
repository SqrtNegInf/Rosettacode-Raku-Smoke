#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Zeckendorf_number_representation
#c# 2016-01-05 <>RC
#m# MOAR: OK
#j#  JVM: OK
#n# OEIS:A014417
#n# OEIS:A003714
#n# here must 'cache' with sigil-less form?  How is this different from 'C/Catalan_numbers'?

#constant @FIBS = (1,2, *+* ... *);
 constant  FIBS = (1,2, *+* ... *).cache; 

multi zeckendorf(0) { '0' }

multi zeckendorf($n is copy) {
    [~] map {
        $n -= $_ if my $digit = $n â‰¥ $_;
        +$digit;
   #}, reverse (@FIBS ...^ * > $n); # OK
   #}, reverse snip * < $n, @FIBS;  # no, not on infinite lists
    }, reverse FIBS ...^ * > $n;
}

my @res.push: sprintf "%2d:%9s", $_, .&zeckendorf for ^21;

.say for @res;

my $ref = qq:to/END/;
 0:        0
 1:        1
 2:       10
 3:      100
 4:      101
 5:     1000
 6:     1001
 7:     1010
 8:    10000
 9:    10001
10:    10010
11:    10100
12:    10101
13:   100000
14:   100001
15:   100010
16:   100100
17:   100101
18:   101000
19:   101001
20:   101010
END

use Test;
is @res.join("\n"), chomp $ref;
