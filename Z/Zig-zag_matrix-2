#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Zig-zag_matrix
#c# 2016-12-13 <RC
#m# MOAR: OK
#j#  JVM: OK

sub zig_zag ($n is copy) {
    my $max = $n**2;
    my $number = 0;
    my @m;
    for (0 .. --$n) -> $j {
        for ( $j%2 ?? 0 .. $j !! reverse 0 .. $j) -> $i {
            @m[   $i;     $j-$i ] = $number++;
            @m[$n-$i; $n-($j-$i)] = $max - $number;
        }
    }
    @m;
}

say .fmt("%3d") for zig_zag(5);
my @res.push: .fmt("%3d") for zig_zag(5);

my $ref = qq:to/END/;
  0   1   5   6  14
  2   4   7  13  15
  3   8  12  16  21
  9  11  17  20  22
 10  18  19  23  24
END
use Test;
is @res.join("\n"), chomp $ref;
