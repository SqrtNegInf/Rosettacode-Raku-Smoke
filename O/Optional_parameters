#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Optional_parameters
#c# 2016-01-05 <>RC, 2018-06-25 <RC
#m# MOAR: OK
#j# JVM:  OK

# Using named parameters:
my method sorttable(:$column = 0, :$reverse, :&ordering = &infix:<cmp>) {
    my @result = self»[$column].sort: &ordering;
    return $reverse ?? @result.reverse !! @result;
}

# Using optional positional parameters:
my method sorttable-pos($column = 0, $reverse?, &ordering = &infix:<cmp>) {
    my @result = self»[$column].sort: &ordering;
    return $reverse ?? @result.reverse !! @result;
}

my @a = [['a', 'b', 'c'], ['zap', 'zip', 'Zot'], ['', 'q', 'z']];
my @b = sorttable(@a, column => 1, reverse=>1);
say @b;
my @c = sorttable-pos(@a, 1, 1);
say @c;

use Test;
is @b.gist, '[zip q b]';
is @c.gist, '[zip q b]';
