#!/usr/bin/env raku
#u# https://rosettacode.org/wiki/Ormiston_triples
#t# toodamnslow
#c# 2023-02-03 <RC
#m# MOAR: OK
#j# JVM:  BROKEN
#n# OEIS:A075093 Smallest member of Ormiston prime triple
#n# this is thundergant's 'Big Sigh' version on RC

my @res;

use Lingua::EN::Numbers;
use List::Divvy;

my @primes = lazy (^∞).hyper.grep( &is-prime ).map: { $_ => .comb.sort.join };
my @Ormistons = @primes.kv.map: { $^value.key if $^value.value eq @primes[$^key+1].value eq @primes[$^key+2].value};

@res.push: "First twenty-five Ormiston triples:"; 
@res.push: @Ormistons[^25].batch(5)».join(', ').join: "\n";
@res.push: '';
@res.push: +@Ormistons.&before( *[0] > $_ ) ~ " Ormiston triples before " ~ .Int.&cardinal for 1e8, 1e9;

.say for @res;

my $ref = q:to/END/;
First twenty-five Ormiston triples:
11117123, 12980783, 14964017, 32638213, 32964341
33539783, 35868013, 44058013, 46103237, 48015013
50324237, 52402783, 58005239, 60601237, 61395239
74699789, 76012879, 78163123, 80905879, 81966341
82324237, 82523017, 83279783, 86050781, 92514341

25 Ormiston triples before one hundred million
368 Ormiston triples before one billion
END

use Test;
is @res.join("\n"), chomp $ref;
