#!/usr/bin/env raku
#u# https://rosettacode.org/wiki/One-two_primes
#c# 2023-05-13 <RC
#m# MOAR: OK
#j# JVM:  OK
#n#  2nd half of task result in: java.lang.StackOverflowError in sub onetwo 

my @res;

sub condense ($n) { my $i = $n.index(2); $i ?? "(1 x $i) {$n.substr($i)}" !! $n }

sub onetwo ($d, $s='') { take $s and return unless $d; onetwo($d-1,$s~$_) for 1,2 }

sub get-onetwo ($d) { (gather onetwo $d).grep(&is-prime)[0] }

@res.push: sprintf "%4d: %s", $_, get-onetwo($_) for 1..15;
#@res.push: sprintf "%4d: %s", $_, condense get-onetwo($_) for (1..10) »×» 100;

.say for @res;

my $ref = q:to/END/;
   1: 2
   2: 11
   3: 211
   4: 2111
   5: 12211
   6: 111121
   7: 1111211
   8: 11221211
   9: 111112121
  10: 1111111121
  11: 11111121121
  12: 111111211111
  13: 1111111121221
  14: 11111111112221
  15: 111111112111121
END

use Test;
is @res.join("\n"), chomp $ref;
