#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Own_digits_power_sum
#c# 2021-10-27 <RC, 2021-10-28 >RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

(3..6).map: -> $p {
    my %pow = (^10).map: { $_ => $_ ** $p };
    my $start = 10 ** ($p - 1);
    my $end   = 10 ** $p;
    my @temp;
    for ^9 -> $i {
        ([X] ($i..9) xx $p).map: {
            next unless [<=] $_;
            my $sum = %pow{$_}.sum;
            next if $sum < $start;
            next if $sum > $end;
            @temp.push: $sum if $sum.comb.Bag eqv $_Â».Str.Bag
        }
    }
    @res.push: $_ for unique sort @temp;
}

.say for @res;

my $ref = q:to/END/;
153
370
371
407
1634
8208
9474
54748
92727
93084
548834
END

use Test;
is @res.join("\n"), chomp $ref;
