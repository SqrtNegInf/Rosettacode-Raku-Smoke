#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Own_digits_power_sum
#c# 2021-10-27 <RC, 2021-10-28 >RC
#m# MOAR: OK
#j# JVM:  BROKEN
#n# removed stray '$str'
#n# 3,7:   566.27s user   14.97s system 554% cpu    1:44 total
#n# 3,8:  7538.45s user  236.54s system 527% cpu   24:34 total
#n# 3,9: 51093.73s user 1770.76s system 434% cpu 3:22:55 total

my @res;

(3..6).map: -> $p {
    my %pow = (^10).map: { $_ => $_ ** $p };
    my $start = 10 ** ($p - 1);
    my $end   = 10 ** $p;
    my @temp;
    for ^9 -> $i {
        ([X] ($i..9) xx $p).race.map: {
            next unless [<=] $_;
            my $sum = %pow{$_}.sum;
            next if $sum < $start;
            next if $sum > $end;
            @temp.push: $sum if $sum.comb.Bag eqv $_Â».Str.Bag
        }
    }
    @res.push: $_ for unique sort @temp;
}

.say for @res;

my $ref = q:to/END/;
153
370
371
407
1634
8208
9474
54748
92727
93084
548834
END

use Test;
is @res.join("\n"), chomp $ref;
