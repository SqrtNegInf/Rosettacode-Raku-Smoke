#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Unbias_a_random_generator
#c# 2016-01-06 <RC, 2020-08-26 >RC
#m# MOAR: OK
#j#  JVM: OK
#n# different results when initiated from command line vs cron

srand 123456;

sub randN ( $n where 3..6 ) {
    return ( $n.rand / ($n - 1) ).Int;
}

sub unbiased ( $n where 3..6 ) {
    my $n1;
    repeat { $n1 = randN($n) } until $n1 != randN($n);
    return $n1;
}

my @res;
my $iterations = 1000;
for 3 .. 6 -> $n {
    my ( @raw, @fixed );
    for ^$iterations {
        @raw[      randN($n) ]++;
        @fixed[ unbiased($n) ]++;
    }
    @res.push: sprintf "N=%d   randN:%s,%4.1f%%   unbiased:%s,%4.1f%%",
        $n, map { .raku, .[1] Ã— 100 / $iterations }, @raw, @fixed;
}

.say for @res;

my $moar-cronjob = qq:to/END/;
N=3   randN:[677, 323],32.3%   unbiased:[492, 508],50.8%
N=4   randN:[750, 250],25.0%   unbiased:[510, 490],49.0%
N=5   randN:[781, 219],21.9%   unbiased:[515, 485],48.5%
N=6   randN:[823, 177],17.7%   unbiased:[518, 482],48.2%
END

my $moar-terminal = qq:to/END/;
N=3   randN:[677, 323],32.3%   unbiased:[492, 508],50.8%
N=4   randN:[750, 250],25.0%   unbiased:[511, 489],48.9%
N=5   randN:[781, 219],21.9%   unbiased:[515, 485],48.5%
N=6   randN:[822, 178],17.8%   unbiased:[515, 485],48.5%
END

my $jvm = qq:to/END/;
N=3   randN:[656, 344],34.4%   unbiased:[515, 485],48.5%
N=4   randN:[757, 243],24.3%   unbiased:[496, 504],50.4%
N=5   randN:[804, 196],19.6%   unbiased:[502, 498],49.8%
N=6   randN:[848, 152],15.2%   unbiased:[517, 483],48.3%
END

use Test;
#is @res.join("\n"), chomp $*VM ~~ /jvm/ ?? $jvm !! $moar;
is @res.join("\n"), chomp $*VM ~~ /jvm/ ?? $jvm !! $*IN.t ?? $moar-terminal !! $moar-cronjob;
