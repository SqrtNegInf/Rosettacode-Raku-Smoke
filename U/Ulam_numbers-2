#!/usr/bin/env raku
##u# http://rosettacode.org/wiki/Ulam_numbers
#c# 2021-12-27 NOT ON RC
#n# https://github.polettix.it/ETOOBUSY/2021/12/23/pwc144-ulam-sequence/
#m# MOAR: OK
#j# JVM:  OK

class Ulam {
   has @!items is built;
   has $!n = 0;

   method new ($v, $w) { self.bless(items => [$v, $w]) }

   method next () {
      ITEM:
      while $!n > @!items.end {
         @!items.push: (@!items X @!items).grep({ $_[0] < $_[1] })
            .map({$_.sum}).grep({$_ > @!items[*-1]})
            .Bag.pairs.grep({.value == 1}).map({.key}).min;
      }
      return @!items[$!n++];
   }
}

sub MAIN (*@args) {
   my ($v, $w) = @args.elems == 2 ?? @args !! (1, 2);
   my $ulam = Ulam.new($v, $w);
   say my $result = (1..10).map({$ulam.next}).join(' ');

   use Test;
   is $result, '1 2 3 4 6 8 11 13 16 18';
}
