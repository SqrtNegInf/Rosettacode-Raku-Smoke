#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Bifid_cipher
#c# 2022-07-13 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# 2023-09-11 results are for cronjob, not terminal
die 'BROKEN on Mac-Pro' if qx/uname -a/ ~~ /'Mac-Pro'/;

my @res;

srand 123456;

sub polybius ($text) {
    my $n = $text.chars.sqrt.narrow;
    $text.comb.kv.map: { $^v => ($^k % $n, $k div $n).join: ' ' }
}

sub encrypt ($message, %poly) {
    %poly.invert.hash{(flat reverse [Z] %poly{$message.comb}».words).batch(2)».reverse».join: ' '}.join
}

sub decrypt ($message, %poly) {
   %poly.invert.hash{reverse [Z] (reverse flat %poly{$message.comb}».words».reverse).batch($message.chars)}.join
}

for 'ABCDEFGHIKLMNOPQRSTUVWXYZ', 'ATTACKATDAWN',
    'BGWKZQPNDSIOAXEFCLUMTHYVR', 'FLEEATONCE',
   #(flat '_', '.', 'A'..'Z', 'a'..'z', 0..9).&pickall.join, 'The invasion will start on the first of January 2023.'.subst(/' '/, '_', :g)
    (flat '_', '.', 'A'..'Z', 'a'..'z', 0..9).pick(*).join, 'The invasion will start on the first of January 2023.'.subst(/' '/, '_', :g)
 -> $polybius, $message {
    my %polybius = polybius $polybius;
    @res.push: "\nUsing polybius:\n\t" ~ $polybius.comb.batch($polybius.chars.sqrt.narrow).join: "\n\t";
    @res.push: "\n  Message : $message";
    @res.push: "Encrypted : " ~ my $encrypted = encrypt $message, %polybius;
    @res.push: "Decrypted : " ~ decrypt $encrypted, %polybius;
}

.say for @res;

my $moar-ubuntu = q:to/END/;

Using polybius:
	A B C D E
	F G H I K
	L M N O P
	Q R S T U
	V W X Y Z

  Message : ATTACKATDAWN
Encrypted : DQBDAXDQPDQH
Decrypted : ATTACKATDAWN

Using polybius:
	B G W K Z
	Q P N D S
	I O A X E
	F C L U M
	T H Y V R

  Message : FLEEATONCE
Encrypted : UAEOLWRINS
Decrypted : FLEEATONCE

Using polybius:
	T 0 x y 4 r l G
	. s Y o 6 q n c
	u S f e J d I 8
	D 2 L h O V 7 X
	m C B 9 v k b M
	a p w i Z Q g W
	z K t U A 5 1 E
	N P R 3 H F _ j

  Message : The_invasion_will_start_on_the_first_of_January_2023.
Encrypted : y8pkqsFaGnaEs_LRanP8dYaGDX.hUA0h1e1KuwU1e7epILAlTiKsD
Decrypted : The_invasion_will_start_on_the_first_of_January_2023.
END

my $moar-imac = q:to/END/;

Using polybius:
	A B C D E
	F G H I K
	L M N O P
	Q R S T U
	V W X Y Z

  Message : ATTACKATDAWN
Encrypted : DQBDAXDQPDQH
Decrypted : ATTACKATDAWN

Using polybius:
	B G W K Z
	Q P N D S
	I O A X E
	F C L U M
	T H Y V R

  Message : FLEEATONCE
Encrypted : UAEOLWRINS
Decrypted : FLEEATONCE

Using polybius:
	p H . x b t 4 w
	s g 0 V j i K 2
	v y D 8 N n R Y
	6 c W C B X 3 I
	u P Z L d T a l
	5 o f Q E O 1 k
	h e M U S m A r
	_ J F 9 7 q G z

  Message : The_invasion_will_start_on_the_first_of_January_2023.
Encrypted : ar0Ngf_jlsawf_AqKsqk7NaYgVtHt5ho5qzp1qH55sn_50Hm4JwYM
Decrypted : The_invasion_will_start_on_the_first_of_January_2023.
END

use Test;
my $ref = qx/uname -a/ ~~ /'Linux'/ ?? $moar-ubuntu !! $moar-imac;
is @res.join('').subst(/<ws>/, '', :g), $ref.subst(/<ws>/, '', :g);
