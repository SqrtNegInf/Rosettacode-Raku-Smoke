#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Best_shuffle
#c# 2016-04-17 <RC, 2022-08-18 >RC
#m# MOAR: OK
#j#  JVM: OK
#n# 2022-12-19 replace call to built-in 'rand' for output stability

use myRNG;

sub best-shuffle(Str $orig) {
    my @s = $orig.comb;
    my @t = pickall @s;

    for flat ^@s X ^@s -> \i,\j {
        if i != j and @t[i] ne @s[j] and @t[j] ne @s[i] {
            @t[i,j] = @t[j,i] and last
        }
    }

    my $count = 0;
    for @t.kv -> $k,$v {
        ++$count if $v eq @s[$k]
    }

    @t.join, $count;
}

my @res.push: sprintf "%s, %s, (%d)", $_, best-shuffle $_ for <abracadabra seesaw elk grrrrrr up a>;
.say for @res;

my $ref = qq:to/END/;
abracadabra, baaardaracb, (1)
seesaw, aswees, (0)
elk, kel, (0)
grrrrrr, rgrrrrr, (5)
up, pu, (0)
a, a, (1)
END

use Test;
is @res.join("\n"), chomp $ref;
