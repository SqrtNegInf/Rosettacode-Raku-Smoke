#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Biorhythms
#c# 2020-09-05 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

my %cycles = ( :23Physical, :28Emotional, :33Mental );
my @quadrants = [ ('up and rising',    'peak'),
                  ('up but falling',   'transition'),
                  ('down and falling', 'valley'),
                  ('down but rising',  'transition') ];
 
#unit sub MAIN($birthday=%*ENV<BIRTHDAY>, $date = Date.today()) { # Markjreed 2021-07-17
#sub MAIN($birthday, $date = Date.today()) {
  my $birthday = '1809-01-12'; 
  my $date     = '1863-11-19';
  if !$birthday {
    die "Birthday not specified.\n" ~
        "Supply --birthday option or set \$BIRTHDAY in environment.\n";
  }
 
  my ($bday, $target) = ($birthday, $date).map: { Date.new($_) };
  my $days = $target - $bday;
 
  @res.push: "Day $days:";
  for %cycles.sort(+*.value)».kv -> ($label, $length) {
    my $position = $days % $length;
    my $quadrant = floor($position / $length × 4);
    my $percentage = floor(sin($position / $length × 2 × π )×1000)/10;
    my $description;
    if $percentage > 95 {
      $description = 'peak';
    } elsif $percentage < -95 {
      $description = 'valley'; 
    } elsif abs($percentage) < 5 {
      $description = 'critical transition'
    } else {
      my $transition = $target + floor(($quadrant + 1)/4 × $length) - $position;
      my ($trend, $next) = @quadrants[$quadrant];
      $description = "$percentage% ($trend, next $next $transition)";
    }
    @res.push: "$label day $position: $description";
  }
#}

.say for @res;

my $ref = q:to/END/;
Day 20034:
Physical day 1: 26.9% (up and rising, next peak 1863-11-23)
Emotional day 14: critical transition
Mental day 3: 54% (up and rising, next peak 1863-11-24)
END

use Test;
is @res.join("\n"), chomp $ref;
