#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Babylonian_spiral
#c# 2022-06-02 <RC
#m# MOAR: OK
#j# JVM:  OK
#f# RC file: babylonian-spiral.svg
#n# thundergnat++ 'independent implemenation' (original)

use SVG;

my @next = {:x(1), :y(1), :radius(2.sqrt)},;

sub next-interval (Int $int) {
     @next.append: (0..$int).map: { %(:x($int), :y($_), :radius(sqrt( $int² + .² )))};
     @next = |@next.sort: { .<radius> };
}

my @spiral = [\»+«] lazy gather {
    my $radius = 1;
    take [0,0];
    take my @tail = 0, 1;
    loop {
        my \Θ = atan2 |@tail[1,0];
        my @this = @next.shift;
        @this.push: @next.shift while @next and @next[0]<radius> == @this[0]<radius>;
        my @candidates = |@this.map: -> $t { # test -> t
            next-interval(++$radius) if $radius == $t<x>;
            |((1, 1), (1, -1), (-1, 1), (-1, -1) X«*» ($t<x>, $t<y>), ($t<y>, $t<x>))
#           |((($t<x>,-$t<x>) X ($t<y>,-$t<y>)), |(($t<y>,-$t<y>) X ($t<x>,-$t<x>))); # output same, but doesn't work?
        }
        take @tail = |@candidates.min: { ( Θ - atan2 |.[1,0] ) % τ };
    }
}

say my $result = "The first $_ Babylonian spiral points are:\n" ~
(@spiral[^$_].map: { sprintf '(%3d,%4d)', @$_ }).batch(10).join("\n") given 40;

my $ref = q:to/END/;
The first 40 Babylonian spiral points are:
(  0,   0) (  0,   1) (  1,   2) (  3,   2) (  5,   1) (  7,  -1) (  7,  -4) (  6,  -7) (  4, -10) (  0, -10)
( -4,  -9) ( -7,  -6) ( -9,  -2) ( -9,   3) ( -8,   8) ( -6,  13) ( -2,  17) (  3,  20) (  9,  20) ( 15,  19)
( 21,  17) ( 26,  13) ( 29,   7) ( 29,   0) ( 28,  -7) ( 24, -13) ( 17, -15) ( 10, -12) (  4,  -7) (  4,   1)
(  5,   9) (  7,  17) ( 13,  23) ( 21,  26) ( 28,  21) ( 32,  13) ( 32,   4) ( 31,  -5) ( 29, -14) ( 24, -22)
END

use Test;
chomp $ref;
is $result, chomp $ref;

'run/babylonian-spiral.svg'.IO.spurt: SVG.serialize(
    svg => [
        :width<100%>, :height<100%>,
        :rect[:width<100%>, :height<100%>, :style<fill:white;>],
        :polyline[ :points((flat @spiral[^10000]).join: ','),
          :style("stroke:red; stroke-width:6; fill:white;"),
          :transform("scale (.05, -.05) translate (1000,-10000)")
        ],
    ],
);

