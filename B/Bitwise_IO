#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Bitwise_IO
#c# 2016-05-09 <RC, 2017-07-23 >RC
#m# MOAR: OK
#j#  JVM: OK

sub encode-ascii(Str $s) {
    my @b = flat $s.ords».fmt("%07b")».comb;
    @b.push(0) until @b %% 8;   # padding
    Buf.new: gather while @b { take reduce * × 2 + *, (@b.pop for ^8) }
}

sub decode-ascii(Buf $b) {
    my @b = flat $b.list».fmt("%08b")».comb;
    @b.shift until @b %% 7;   # remove padding
    @b = gather while @b { take reduce * × 2 + *, (@b.pop for ^7) }
    return [~] @b».chr;
}

say my $encode = encode-ascii 'STRING';
say my $result = decode-ascii $encode;

use Test;
is $encode.gist, 'Buf:0x<03 8B 99 29 4A E5>';
is $result, 'STRING';
