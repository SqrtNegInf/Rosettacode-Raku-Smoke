#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Bulls_and_cows
#c# 2016-05-10 <RC
#m# MOAR: OK
#j#  JVM: OK
#=# RC pipe: 1234\n7234\n7254

srand 123456;

# Translation of: Python
# Works with: Rakudo version 2015.12
my $size = 4;
my @secret = pick $size, '1' .. '9';

 
for 1..* -> $guesses {
    my @guess;
    loop {
        @guess = (prompt("Guess $guesses: ") // exit).comb;
        last if @guess == $size and
            all(@guess) eq one(@guess) & any('1' .. '9');
        say 'Malformed guess; try again.';
    }
    my ($bulls, $cows) = 0, 0;
    for ^$size {
        when @guess[$_] eq @secret[$_] { ++$bulls; }
        when @guess[$_] eq any @secret { ++$cows; }
    }
    last if $bulls == $size;
    say "$bulls bulls, $cows cows.";
}
 
say 'A winner is you!';
say 'ok 1 -';
