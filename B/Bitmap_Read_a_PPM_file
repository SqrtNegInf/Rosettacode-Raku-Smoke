#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Bitmap/Read_a_PPM_file
#c# 2017-10-21 <RC
#t# graphical
#m# MOAR: OK
#j# JVM:  OK
#f# RC file: bitmap-gs.pgm

# Works with: Rakudo version 2017.09
# Uses pieces from  Bitmap,  Write a PPM file and  Grayscale image tasks. Included here to make a complete, runnable program.

class Pixel { has UInt ($.R, $.G, $.B) }
class Bitmap {
    has UInt ($.width, $.height);
    has Pixel @.data;
}

role PGM {
    has @.GS;
    method P5 returns Blob {
	"P5\n{self.width} {self.height}\n255\n".encode('ascii')
	~ Blob.new: self.GS
    }
}

sub load-ppm ( $ppm ) {
    my $fh    = $ppm.IO.open( :enc('ISO-8859-1') );
    my $type  = $fh.get;
    my ($width, $height) = $fh.get.split: ' ';
    my $depth = $fh.get;
    Bitmap.new( width => $width.Int, height => $height.Int,
      data => ( $fh.slurp.ords.rotor(3).map:
        { Pixel.new(R => .[0], G => .[1], B => .[2]) } )
    )
}

sub grayscale ( Bitmap $bmp ) {
    $bmp.GS = map { (.R×0.2126 + .G×0.7152 + .B×0.0722).round(1) min 255 }, $bmp.data;
}

my $filename = 'ref/bitmap.ppm';

my Bitmap $b = load-ppm( $filename ) but PGM;

grayscale($b);

'run/bitmap-gs.pgm'.IO.open(:bin, :w).write: $b.P5;
