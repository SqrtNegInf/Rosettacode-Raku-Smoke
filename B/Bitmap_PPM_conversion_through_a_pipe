#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Bitmap/PPM_conversion_through_a_pipe
#c# 2018-12-29 <RC
#m# MOAR: OK
#j# JVM:  OK
#f# RC file: output_piped.jpg
#n# output trivially different on Ubuntu
#n# doesn't have 'srand', why was this broken?

class Pixel { has uint8 ($.R, $.G, $.B) }
class Bitmap {
    has UInt ($.width, $.height);
    has Pixel @!data;

    method fill(Pixel $p) {
        @!data = $p.clone xx ($!width × $!height)
    }
    method pixel(
          $i where ^$!width,
          $j where ^$!height
          --> Pixel
      ) is rw { @!data[$i × $!height + $j] }

    method data { @!data }
}

role PPM {
    method P6 returns Blob {
        "P6\n{self.width} {self.height}\n255\n".encode('ascii')
        ~ Blob.new: flat map { .R, .G, .B }, self.data
    }
}

my Bitmap $b = Bitmap.new(width => 125, height => 125) but PPM;
for flat ^$b.height X ^$b.width -> $i, $j {
    $b.pixel($i, $j) = Pixel.new: :R($i×2), :G($j×2), :B(255-$i×2);
}

my $convert = chomp qx/which convert/;
my $proc = run $convert,'-','run/output_piped.jpg', :in;
$proc.in.write: $b.P6;
$proc.in.close;
