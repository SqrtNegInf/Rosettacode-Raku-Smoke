#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Burrowsâ€“Wheeler_transform
#c# 2018-08-07 <RC
#m# MOAR: OK
#j# JVM:  BROKEN

my @res;

# STX and ETX can be any characters that don't appear
# in the text as long as STX sorts before ETX.
 
constant \STX = 'ğŸ‘';
constant \ETX = 'ğŸ‘';
 
# Burrows-Wheeler transform
sub transform (Str $s is copy) {
    die "String can't contain STX or ETX characters." if $s.contains: STX|ETX;
    $s = STX ~ $s ~ ETX;
    my $l = $s.chars;
    (^$l).map({ $s.comb.list.rotate: $_ }).sort[*;*-1].join
}
 
# Burrows-Wheeler inverse transform
sub É¯É¹oÉŸsuÉÉ¹Ê‡ (Str $s) {
    my @t = '' xx my $l = $s.chars;
    @t = ($s.comb Z~ @t).sort for ^$l;
    @t.first( *.starts-with: STX ).substr(1, $l - 2)
}
 
# TESTING
for |<BANANA dogwood SIX.MIXED.PIXIES.SIFT.SIXTY.PIXIE.DUST.BOXES>,
    'TO BE OR NOT TO BE OR WANT TO BE OR NOT?' #, "Oops{STX}"
    -> $phrase {
    @res.push: 'Original:            '~ $phrase;
    @res.push: 'Transformed:         '~ transform $phrase;
    @res.push: 'Inverse transformed: '~ É¯É¹oÉŸsuÉÉ¹Ê‡ transform $phrase;
    @res.push: '';
}

.say for @res;

my $ref = qq:to/END/;
Original:            BANANA
Transformed:         BNNğŸ‘AAğŸ‘A
Inverse transformed: BANANA

Original:            dogwood
Transformed:         ğŸ‘ooodwgğŸ‘d
Inverse transformed: dogwood

Original:            SIX.MIXED.PIXIES.SIFT.SIXTY.PIXIE.DUST.BOXES
Transformed:         TEXYDST.E.IXIXIXXSSMPPS.B..E.ğŸ‘.UESFXDIIOIIITğŸ‘S
Inverse transformed: SIX.MIXED.PIXIES.SIFT.SIXTY.PIXIE.DUST.BOXES

Original:            TO BE OR NOT TO BE OR WANT TO BE OR NOT?
Transformed:         OOORREEETTRTW   BBB  ATTT   NNOOONOOğŸ‘   ğŸ‘?
Inverse transformed: TO BE OR NOT TO BE OR WANT TO BE OR NOT?

END

use Test;
is @res.join("\n"), chomp $ref;
