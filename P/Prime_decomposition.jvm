#!/usr/bin/env perl6
#n# NOT CURRENT, NOT SMOKE-TESTED
#u# http://rosettacode.org/wiki/Prime_decomposition
#c# 2016-06-28 <RC, 2016-09-31 <RC, 2018-04-15 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# 2018-11-03 no benefit with 'race' here
#n# 2018-08-22 Inline::Perl5 version works fine (and fast) but clobbers JVM

# Pure Perl 6[edit]
# This is a pure perl 6 version that uses no outside libraries. It uses a variant of Pollard's rho factoring algorithm and is fairly performent when factoring numbers < 2⁸⁰; typically taking well under a second on an i7. It starts to slow down with larger numbers, but really bogs down factoring numbers that have more than 1 factor larger than about 2⁴⁰.

sub prime-factors ( Int $n where * > 0 ) {
    return $n if $n.is-prime;
    return [] if $n == 1;
    my $factor = find-factor( $n );
    sort flat prime-factors( $factor ), prime-factors( $n div $factor );
}

sub find-factor ( Int $n, $constant = 1 ) {
    my $x      = 2;
    my $rho    = 1;
    my $factor = 1;
    while $factor == 1 {
        $rho *= 2;
        my $fixed = $x;
        for ^$rho {
            $x = ( $x * $x + $constant ) % $n;
            $factor = ( $x - $fixed ) gcd $n;
            last if 1 < $factor;
        }
    }
    $factor = find-factor( $n, $constant + 1 ) if $n == $factor;
    $factor;
}

my @res;

for 2²⁹-1, 2⁴¹-1, 2⁵⁹-1, 2⁷¹-1, 2⁷⁹-1, 2⁹⁷-1, 2¹¹⁷-1,
5465610891074107968111136514192945634873647594456118359804135903459867604844945580205745718497
 ->  $n {
    my $start = now;
    @res.push: "factors of $n: "~ prime-factors($n).join(' × ');
    say @res[*-1] ~ " \t in ", (now - $start).fmt("%0.3f"), " sec."
    #say "factors of $n: ",
    #prime-factors($n).join(' × '), " \t in ", (now - $start).fmt("%0.3f"), " sec."
}

my $ref = qq:to/END/;
factors of 536870911: 233 × 1103 × 2089
factors of 2199023255551: 13367 × 164511353
factors of 576460752303423487: 179951 × 3203431780337
factors of 2361183241434822606847: 228479 × 48544121 × 212885833
factors of 604462909807314587353087: 2687 × 202029703 × 1113491139767
factors of 158456325028528675187087900671: 11447 × 13842607235828485645766393
factors of 166153499473114484112975882535043071: 7 × 73 × 79 × 937 × 6553 × 8191 × 86113 × 121369 × 7830118297
factors of 5465610891074107968111136514192945634873647594456118359804135903459867604844945580205745718497: 165901 × 10424087 × 18830281 × 53204737 × 56402249 × 59663291 × 91931221 × 95174413 × 305293727939 × 444161842339 × 790130065009
END

use Test;
is @res.join("\n"), chomp $ref;

#`{{
# There is a Perl 6 module available: Prime::Factor, that uses essentially this algorithm with some minor performance tweaks.
# External library[edit]
# If you really need a speed boost, load the highly optimized Perl 5 ntheory module. It needs a little extra plumbing to deal with the lack of built-in big integer support, but for large number factoring the interface overhead is worth it.

exit if $*VM ~~ /jvm/;

use Inline::Perl5;
my $p5 = Inline::Perl5.new();
$p5.use( 'ntheory' );

sub prime-factors5 ($i) {
    my &primes = $p5.run('sub { map { ntheory::todigitstring $_ } sort {$a <=> $b} ntheory::factor $_[0] }');
    primes("$i");
}

my @res-p5;
for 2²⁹-1, 2⁴¹-1, 2⁵⁹-1, 2⁷¹-1, 2⁷⁹-1, 2⁹⁷-1, 2¹¹⁷-1,
5465610891074107968111136514192945634873647594456118359804135903459867604844945580205745718497
 ->  $n {
    my $start = now;
    @res-p5.push: "factors of $n: "~ prime-factors5($n).join(' × ');
    say @res-p5[*-1] ~ " \t in ", (now - $start).fmt("%0.3f"), " sec."
    #say "factors of $n: ",
    #prime-factors($n).join(' × '), " \t in ", (now - $start).fmt("%0.3f"), " sec."
}

is @res-p5.join("\n"), chomp $ref;
}}
