#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Percolation/Mean_cluster_density
#c# 2017-03-11 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# downgraded Unicode names to ASCII
#n# 2023-01-13 replace call to built-in 'rand' for output stability

my @res;

use myRNG;
my $rng = myRNG.new( :seed(123456) );

my @perc;
my $fill = 'x';

enum Direction <DeadEnd Up Right Down Left>;

sub infix:<deq> ( $a, $b ) { $a.defined && ($a eq $b) }

sub perctest ( $grid ) {
    generate $grid;
    my $block = 1;
    for ^$grid X ^$grid -> ($y, $x) {
        fill( [$x, $y], $block++ ) if @perc[$y; $x] eq $fill
    }
    ($block - 1) / $grid²;
}

sub generate ( $grid ) {
    @perc = ();
    @perc.push: [ ( $rng.next-rat < .5 ?? '.' !! $fill ) xx $grid ] for ^$grid;
}

sub fill ( @cur, $block ) {
    @perc[@cur[1]; @cur[0]] = $block;
    my @stack;
    my $current = @cur;

    loop {
        if my $dir = direction( $current ) {
            @stack.push: $current;
            $current = move $dir, $current, $block
        }
        else {
            return unless @stack;
            $current = @stack.pop
        }
    }

    sub direction( [$x, $y] ) {
        ( Down  if @perc[$y + 1][$x] deq $fill ) ||
        ( Left  if @perc[$y][$x - 1] deq $fill ) ||
        ( Right if @perc[$y][$x + 1] deq $fill ) ||
        ( Up    if @perc[$y - 1][$x] deq $fill ) ||
        DeadEnd
    }

    sub move ( $dir, @cur, $block ) {
        my ( $x, $y ) = @cur;
        given $dir {
            when Up    { @perc[--$y; $x] = $block }
            when Down  { @perc[++$y; $x] = $block }
            when Left  { @perc[$y; --$x] = $block }
            when Right { @perc[$y; ++$x] = $block }
        }
        [$x, $y]
    }
}

my $K = perctest(15);
@res.push: $_.fmt("%-2s") for @perc;
@res.push: "𝘱 = 0.5, 𝘕 = 15, 𝘒 = $K";

my $trials = 5;
#for 10, 30, 100, 300, 1000 -> $N {
for 10, 30, 100 -> $N {
    my $K = ( [+] perctest($N) xx $trials ) / $trials;
    @res.push: "𝘱 = 0.5, trials = $trials, 𝘕 = $N, 𝘒 = $K";
}

.say for @res;

my $ref = qq:to/END/;
.  .  1  1  .  2  .  3  3  3  3  .  .  .  4
5  .  .  1  1  .  .  3  .  .  3  .  .  4  4
5  5  .  .  1  1  .  .  6  .  3  3  .  .  .
5  .  .  7  .  .  8  .  .  .  .  3  3  .  9
5  .  7  7  .  .  8  8  .  10 .  3  3  .  .
5  .  7  .  .  .  8  8  8  .  11 .  3  3  .
5  .  .  .  8  8  8  8  8  .  11 11 .  .  12
5  5  .  13 .  .  .  8  .  .  .  11 11 .  12
.  .  .  13 .  14 .  8  .  15 .  11 11 11 .
.  .  .  13 13 .  .  .  .  .  11 11 .  11 .
.  .  13 13 13 13 13 13 13 .  .  11 11 11 .
.  .  .  13 13 13 .  .  .  .  .  .  11 .  .
16 16 .  .  .  .  17 17 .  18 .  .  .  19 .
.  .  .  17 .  17 17 .  18 18 .  20 .  19 19
.  .  17 17 17 17 .  18 18 18 .  20 20 .  19
𝘱 = 0.5, 𝘕 = 15, 𝘒 = 0.088889
𝘱 = 0.5, trials = 5, 𝘕 = 10, 𝘒 = 0.104
𝘱 = 0.5, trials = 5, 𝘕 = 30, 𝘒 = 0.073111
𝘱 = 0.5, trials = 5, 𝘕 = 100, 𝘒 = 0.07036
END

use Test;
is @res.join('').subst(/<ws>/, '', :g), $ref.subst(/<ws>/, '', :g);
