#!/usr/bin/env perl6
#n# NOT CURRENT, NOT SMOKE-TESTED
#u# http://rosettacode.org/wiki/Pentagram
#c# 2015-11-02 <>RC
#m# MOAR: OK
#j#  JVM: OK
#f# RC file: pentagram.svg
#j#  JVM: BROKEN UnwindException
#   2017-01-19 fixed bug revealed by change in .rotor, but code superseded

#Generate an SVG file to STDOUT. Redirect to a file to capture and display it.

constant $dim = 200;
constant $sides = 5;

my $out = open('pentagram.1.svg', :w);

$out.say: qq:to /STOP/;
<?xml version="1.0" standalone="no" ?>
 <!DOCTYPE svg PUBLIC "-//W3C//DTD SVG 1.0//EN"
 "http://www.w3.org/TR/2001/PR-SVG-20010719/DTD/svg10.dtd">
<svg height="{$dim×2}" width="{$dim×2}" xmlns="http://www.w3.org/2000/svg"> # DH change
STOP

END $out.say: '</svg>';
#END '</svg>'.say(:$out); # why not? 

my @vertices = map { 0.9×$dim×cis($_ × 2×pi/$sides) }, ^$sides;
@vertices = |@vertices[0, 2 ... *], |@vertices[1, 3 ... *];
@vertices.=map(|*.reals».fmt("%0.3f"));
@vertices.push(|@vertices[0,1]);

$out.say: pline( flat @vertices );
for @vertices.rotor(4 => -2) -> @p {
    $out.say: pline( flat @p ) unless 1 == +@p;
};

sub pline (@q) {
  qq|<polyline points="{ join ' ', @q }"
  style="fill:seashell;stroke:{<black red orange blue purple green>[state $++% 6]};stroke-width:2"
  transform="translate($dim, $dim) rotate(180)" />\n|
}
