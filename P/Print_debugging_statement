#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Print_debugging_statement
#c# 2019-08-29 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# can only test Moar or JVM, not both, because the answers for one affect the results for the other

my @res;

my &pdb = &die;

CATCH {
    when X::AdHoc {
        my @frames = .backtrace[*];
        @res.push: .payload;
        for @frames {
            # Filter out exception handing and dispatcher frames
            next if .file.contains: 'SETTING' or .subname.chars < 1;
            @res.push: sprintf "%sfrom file: %s,%s line: %s",
              (' ' x $++), .file,
              (my $s = .subname) eq '<unit>' ?? '' !! " sub: $s,", .line;
        }
        @res.push: '';
        .resume;
    }
    default {}
}

## Testing / demonstration

# helper subs                #line 22
sub alpha ($a) {             #line 23
    pdb ('a =>', $a + 3);    #line 24
    pdb 'string';            #line 25
    beta(7);                 #line 26
}                            #line 27
sub beta  ($b) { pdb $b    } #line 28
sub gamma ($c) { beta $c   } #line 29
sub delta ($d) { gamma $d  } #line 30
                             #line 31
my $a = 10;                  #line 32
pdb (.VAR.name, $_) with $a; #line 33
alpha($a);                   #line 34
delta("D");                  #line 35
.&beta for ^3;               #line 36

.say for @res;

my $ref = q:to/END/;
$a 10
from file: P/Print_debugging_statement, line: 42

a => 13
from file: P/Print_debugging_statement, sub: alpha, line: 33
 from file: P/Print_debugging_statement, line: 43

string
from file: P/Print_debugging_statement, sub: alpha, line: 34
 from file: P/Print_debugging_statement, line: 43

7
from file: P/Print_debugging_statement, sub: beta, line: 37
 from file: P/Print_debugging_statement, sub: alpha, line: 35
  from file: P/Print_debugging_statement, line: 43

D
from file: P/Print_debugging_statement, sub: beta, line: 37
 from file: P/Print_debugging_statement, sub: gamma, line: 38
  from file: P/Print_debugging_statement, sub: delta, line: 39
   from file: P/Print_debugging_statement, line: 44

0
from file: P/Print_debugging_statement, sub: beta, line: 37
 from file: P/Print_debugging_statement, line: 45

1
from file: P/Print_debugging_statement, sub: beta, line: 37
 from file: P/Print_debugging_statement, line: 45

2
from file: P/Print_debugging_statement, sub: beta, line: 37
 from file: P/Print_debugging_statement, line: 45

END

use Test;
is @res.join("\n"), chomp $ref;
