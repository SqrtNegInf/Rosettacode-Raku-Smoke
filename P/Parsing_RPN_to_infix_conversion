#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Parsing/RPN_to_infix_conversion
#c# 2015-11-29 <>RC, 2019-10-28 >RC
#m# MOAR: OK
#j#  JVM: OK

my @res;

sub p ($pair, $prec) { $pair.key < $prec ?? "( {$pair.value} )" !! $pair.value }

sub rpm-to-infix($string) {
    my @stack;
    for $string.words {
        when /\d/      { @stack.push: 9 => $_ }
        my ($y,$x) = @stack.pop, @stack.pop;
        when '^'       { @stack.push: 4 => ~(p($x,5), $_, p($y,4)) }
        when '*' | '/' { @stack.push: 3 => ~(p($x,3), $_, p($y,3)) }
        when '+' | '-' { @stack.push: 2 => ~(p($x,2), $_, p($y,2)) }
    }
    ($string, @stackÂ».value).join("\n") ~ "\n";
}

@res.push: rpm-to-infix $_ for 
    '3 4 2 * 1 5 - 2 3 ^ ^ / +', 
    '1 2 + 3 4 + ^ 5 6 + ^';

.say for @res;

my $ref = q:to/END/;
3 4 2 * 1 5 - 2 3 ^ ^ / +
3 + 4 * 2 / ( 1 - 5 ) ^ 2 ^ 3

1 2 + 3 4 + ^ 5 6 + ^
( ( 1 + 2 ) ^ ( 3 + 4 ) ) ^ ( 5 + 6 )

END

use Test;
is @res.join("\n") , chomp $ref;
