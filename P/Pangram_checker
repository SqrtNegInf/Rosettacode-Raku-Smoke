#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Pangram_checker
#c# 2016-03-12 <RC
#m# MOAR: OK
#j#  JVM: OK

constant Eng = set 'a' .. 'z';
constant Hex = set 'a' .. 'f';
#constant Cyr = set <а б в г д е ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я ё>;
constant Cyr = (set 'а' .. 'ё') (-) (set 'ъ', 'ѐ');

sub pangram($str, Set $alpha = Eng) {
  $alpha ⊆ $str.lc.comb;
}

use Test;
is pangram("The quick brown fox jumps over the lazy dog."), 'True';
is pangram("My dog has fleas."), 'False';
is pangram("My dog has fleas.", Hex), 'False';
is pangram("My dog backs fleas.", Hex), 'True';
is pangram("Съешь же ещё этих мягких французских булок, да выпей чаю", Cyr), 'True';
