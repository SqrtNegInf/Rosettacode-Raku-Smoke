#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Primes_which_sum_of_digits_is_25
#c# 2021-03-21 <RC
#m# MOAR: OK
#j# JVM:  OK

unit sub MAIN ($limit = 5000);

say my $result = "{+$_} primes < $limit with digital sum 25:\n{$_».fmt("%" ~ $limit.chars ~ "d").batch(10).join("\n")}",
    with ^$limit .grep: { .is-prime and .comb.sum == 25 }

my $ref = q:to/END/;
17 primes < 5000 with digital sum 25:
 997 1699 1789 1879 1987 2689 2797 2887 3499 3697
3769 3877 3967 4597 4759 4957 4993
END

use Test;
is $result, chomp $ref;

#`{{
# slow...
use Math::Primesieve;
my $sieve = Math::Primesieve.new;
my @p = $sieve.primes(10**9);
my @p25 = @p.hyper(:8degree).grep: { .comb ∌ '0' and .comb.sum == 25 };
say @p25[*-1];
say +@p25;
}}
