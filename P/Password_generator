#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Password_generator
#c# 2016-06-05 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# 2018.04-13-ga51112ebc built on MoarVM version 2018.04-18-ge7d79d869
#n# for Rakudo, exact tests no longer work, replace with weaker ones
#n# mutating .= must not be white-space separated from args
#n# there's a 2nd version ('functional') that's sub-par, marked as needing attention

srand 123456;

# Works with: Rakudo version 2016.05
my @chars =
  set('a' .. 'z'),
  set('A' .. 'Z'),
  set('0' .. '9'),
  set(<!"#$%&'()*+,-./:;<=>?@[]^_{|}~>.comb);


sub MAIN ( Int :$l = 8, Int :$c = 1, Str :$x = '' ) {
    note 'Password length must be >= 4' and exit if $l < 4;
    note 'Can not generate fewer than 0 passwords' and exit if $c < 0;
    my $chars = [∪] @chars».=&filter;
    note 'Can not exclude an entire required character group' and exit
      if any(@chars».elems) == 0;
    for ^$c {
        my @pswd;
        @pswd.push( @chars[$_].roll ) for ^4;
        @pswd.push( $chars    .roll ) for 4 ..^ $l;
        say my $result = [~] @pswd.pick(*);

        use Test;
        ok $result.chars ≥ 8;
        ok 1 ≤ (set($result.comb) ∩ @chars[0]).elems;
        ok 1 ≤ (set($result.comb) ∩ @chars[1]).elems;
        ok 1 ≤ (set($result.comb) ∩ @chars[2]).elems;
        ok 1 ≤ (set($result.comb) ∩ @chars[3]).elems;
    #   is $result, q{j+euA6)'} if $*VM ~~ /moar/; 
        is $result, q{Ey5m:R(F} if $*VM ~~ /jvm/;;
    }

    sub filter (Set $set) { $set ∖ set($x.comb) }
}

sub USAGE() {
    say qq:to/END/;
    Specify a length:              --l=8 (default 8),
    Specify a count:               --c=1 (default 1),
    Specify characters to exclude: --x=
    (must escape characters significant to the shell)
    E.G.
    {$*PROGRAM-NAME} --l=14 --c=5 --x=0O\\\"\\\'1l\\\|I
    END
}
