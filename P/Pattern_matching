#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Pattern_matching
#c# 2015-10-12 <>RC
#m# MOAR: OK
#j#  JVM: OK

srand 12345;

enum RedBlack <R B>;

multi balance(B,[R,[R,$a,$x,$b],$y,$c],$z,$d) { [R,[B,$a,$x,$b],$y,[B,$c,$z,$d]] }
multi balance(B,[R,$a,$x,[R,$b,$y,$c]],$z,$d) { [R,[B,$a,$x,$b],$y,[B,$c,$z,$d]] }
multi balance(B,$a,$x,[R,[R,$b,$y,$c],$z,$d]) { [R,[B,$a,$x,$b],$y,[B,$c,$z,$d]] }
multi balance(B,$a,$x,[R,$b,$y,[R,$c,$z,$d]]) { [R,[B,$a,$x,$b],$y,[B,$c,$z,$d]] }

multi balance($col, $a, $x, $b) { [$col, $a, $x, $b] }

multi ins( $x, @s [$col, $a, $y, $b] ) {
    when $x before $y     { balance $col, ins($x, $a), $y, $b }
    when $x after $y      { balance $col, $a, $y, ins($x, $b) }
    default               { @s }
}
multi ins( $x, Any:U ) { [R, Any, $x, Any] }

multi insert( $x, $s ) {
    [B, |ins($x,$s)[1..3]];
}

my $t = Any;
$t = insert($_, $t) for (1..10).pick(*);
say $t.gist; 

use Test;
if $*VM ~~ /jvm/ {
is $t.gist, '[B [B [B [R (Any) 1 (Any)] 2 (Any)] 3 [B (Any) 4 (Any)]] 5 [B [B (Any) 6 (Any)] 7 [B [R (Any) 8 (Any)] 9 [R (Any) 10 (Any)]]]]';
} else {
is $t.gist, '[B [R [B [R (Any) 1 (Any)] 2 (Any)] 3 [B [R (Any) 4 (Any)] 5 (Any)]] 6 [R [B (Any) 7 (Any)] 8 [B (Any) 9 [R (Any) 10 (Any)]]]]';
}

