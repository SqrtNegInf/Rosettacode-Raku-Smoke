#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Peripheral_drift_illusion
#c# 2021-08-30 <RC
#m# MOAR: OK
#j# JVM:  BROKEN
#f# RC file: peripheral-drift-raku.svg

use SVG;

my @blocks = (1..15 X 1..10).map: -> ($X, $Y) {
    my $r = (($X + $Y) div 2) % 4 * 90;
    my $x = $X * 75;
    my $y = $Y * 75;
    :use['xlink:href' => '#block', 'transform' => "rotate($r,$x,$y) translate($x,$y)"]
}

'run/peripheral-drift-raku.svg'.IO.spurt: SVG.serialize(
    svg => [
        :1200width, :825height,
        :rect[:width<100%>, :height<100%>, :fill<#d3d004>],
        :defs[
            :g[
                :id<block>,
                :polygon[:points<-25,-25,-25,25,25,25>, :style<fill:white;stroke:white>],
                :polygon[:points<25,25,25,-25,-25,-25>, :style<fill:black;stroke:black>],
                :rect[:x<-20>, :y<-20>, :width<40>, :height<40>, :fill<#3250ff>]
            ]
        ],
        |@blocks,
    ]
)
