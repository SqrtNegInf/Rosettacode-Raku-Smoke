#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Palindrome_detection
#c# 2015-12-08 <RC
#m# MOAR: OK
#j#  JVM: OK

my @res;

subset Palindrom of Str where {
    .flip eq $_ given .comb(/\w+/).join.lc
}

my @tests = q:to/END/.lines;
    A man, a plan, a canal: Panama.
    My dog has fleas
    Madam, I'm Adam.
    1 on 1
    In girum imus nocte et consumimur igni
    A man, a plan, a canal: Panama.
    A man, a plan, a canal: Oregon!
    END

@res.push: ($_ ~~ Palindrom) ~ "\t" ~ $_ for @tests;

.say for @res;

my $ref = qq:to/END/;
True	A man, a plan, a canal: Panama.
False	My dog has fleas
True	Madam, I'm Adam.
False	1 on 1
True	In girum imus nocte et consumimur igni
True	A man, a plan, a canal: Panama.
False	A man, a plan, a canal: Oregon!
END

use Test;
is @res.join("\n"), chomp $ref;
