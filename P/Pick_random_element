#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Pick_random_element
#c# 2015-12-07 <>RC
#m# MOAR: OK
#j#  JVM: OK

srand 123456;
my @res;

# In a nutshell, picking an element from a list
# is implemented with a method conveniently called "pick":

@res.push: (1, 2, 3).pick;
# There are various ways of doing something similar, though.
# Perl 6 has actually two methods (with associated functional forms)
# to return random elements depending on whether you are doing selection
# with or without replacement.
# Selection with replacement: (roll of a die)

@res.push: (1..6).roll;    # return 1 random value in the range 1 through 6
@res.push: (1..6).roll(3); # return a list of 3 random values in the range 1 through 6
#(1..6).roll(*); # return a lazy infinite list of random values in the range 1 through 6

# Selection without replacement: (pick a card from a deck)
# define the deck
my @deck = (<2 3 4 5 6 7 8 9 J Q K A>) X~ <S C H D>;
@res.push: @deck;
@res.push: @deck.pick;    # Pick a card
@res.push: @deck.pick(5); # Draw 5
@res.push: @deck.pick(*); # Get a shuffled deck

# Or you can always use the normal rand built-in
# to generate a subscript (which automatically truncates any fractional part):

my @array = <1 2 3 3 4 5 6>;
@res.push: @array[@array Ã— rand];

# However, the pick and roll methods (not to be confused
# with the pick-and-roll method in basketball) are more general
# insofar as they may be used on any enumerable type:

@res.push: Bool.pick;  # returns either True or False

.say for @res;

my $moar = qq:to/END/;
1
1
2 3 4
2S 2C 2H 2D 3S 3C 3H 3D 4S 4C 4H 4D 5S 5C 5H 5D 6S 6C 6H 6D 7S 7C 7H 7D 8S 8C 8H 8D 9S 9C 9H 9D JS JC JH JD QS QC QH QD KS KC KH KD AS AC AH AD
5D
8D AD 8S KH 5D
2D AD AC JH KD 9H 6S KS AH 6C 7D QC 5H QD 9D 2S 8H JD 4S 3D 8S JC 6H 9S QH 8D 7H 5S 3S 7S KH 7C 6D QS 3H 9C 4D 2C 4H 4C 8C 2H 5D 3C AS KC 5C JS
2
False
END

my $jvm = qq:to/END/;
2
3
6 2 2
2S 2C 2H 2D 3S 3C 3H 3D 4S 4C 4H 4D 5S 5C 5H 5D 6S 6C 6H 6D 7S 7C 7H 7D 8S 8C 8H 8D 9S 9C 9H 9D JS JC JH JD QS QC QH QD KS KC KH KD AS AC AH AD
5S
5D 2S JH KS AD
AD QH 5S 3D 2H JD 7H 2C 6S 3C 2D KC 3H 4S KD JS QC 4D 8C 7S 7D 9H 2S 4C KH QS 6D 5C 8H 9C 6H JH KS 6C JC 8S 5D AC 5H 7C 8D 9S 4H AH AS 9D QD 3S
4
False
END

use Test;
if $*VM ~~ /jvm/ {
is @res.join("\n"), chomp $jvm;
} else {
is @res.join("\n"), chomp $moar;
}
