#!/usr/bin/env perl6
#n# NOT CURRENT, NOT SMOKE-TESTED
#u# http://rosettacode.org/wiki/Prime_conspiracy
#c# 2016-03-19 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# clear, but slow...
# 52.872u 0.440s 0:53.16 100.2

my @primes := 2, |(3,5,7 ... *).grep: *.is-prime;

my %conspiracy;
#my $upto = 1_000_000;
my $upto = 10_000;

@primes[^($upto+1)].reduce: -> $a, $b {
    my $d = $b % 10;
    %conspiracy{"$a → $d count:"}++;
    $d;
}

my @res;
push @res, "$_\tfrequency: {($_.value/$upto×100).round(.01)} %" for %conspiracy.sort;

.say for @res;

my $ref = qq:to/END/;
1 → 1 count:	365	frequency: 3.65 %
1 → 3 count:	833	frequency: 8.33 %
1 → 7 count:	889	frequency: 8.89 %
1 → 9 count:	397	frequency: 3.97 %
2 → 3 count:	1	frequency: 0.01 %
3 → 1 count:	529	frequency: 5.29 %
3 → 3 count:	324	frequency: 3.24 %
3 → 5 count:	1	frequency: 0.01 %
3 → 7 count:	754	frequency: 7.54 %
3 → 9 count:	907	frequency: 9.07 %
5 → 7 count:	1	frequency: 0.01 %
7 → 1 count:	655	frequency: 6.55 %
7 → 3 count:	722	frequency: 7.22 %
7 → 7 count:	323	frequency: 3.23 %
7 → 9 count:	808	frequency: 8.08 %
9 → 1 count:	935	frequency: 9.35 %
9 → 3 count:	636	frequency: 6.36 %
9 → 7 count:	541	frequency: 5.41 %
9 → 9 count:	379	frequency: 3.79 %
END

use Test;
is @res.join("\n"), chomp $ref;
