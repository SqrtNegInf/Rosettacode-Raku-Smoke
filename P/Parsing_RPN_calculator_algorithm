#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Parsing/RPN_calculator_algorithm
#c# 2015-12-03 <RC
#m# MOAR: OK
#j#  JVM: OK

my $proggie = '3 4 2 * 1 5 - 2 3 ^ ^ / +';

class RPN is Array {

    method binop(&op) { self.push: self.pop R[&op] self.pop }

    method run($p) {
        for $p.words {
            say "$_ ({self})";
            when /\d/ { self.push: $_ }
            when '+'  { self.binop: &[+] }
            when '-'  { self.binop: &[-] }
            when '*'  { self.binop: &[Ã—] }
            when '/'  { self.binop: &[/] }
            when '^'  { self.binop: &[**] }
            default   { die "$_ is bogus" }
        }
        return self;
    }
}

use Test;
is RPN.new.run($proggie), '3.000122';
