#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Playing_cards
#c# 2016-08-03 <>RC
#m# MOAR: OK
#j#  JVM: OK
#   made some new methods...
#n#  2023-02-20 replace call to built-in 'rand' for output stability

use myRNG;
my $rng = myRNG.new( :seed(123456) );

# Knuth shuffle
sub pick-all (@a is copy) is export {
    for 1 ..^ @a -> $n {
        my $k = (0 .. $n)[($n+1) × $rng.next-rat];
        $k == $n or @a[$k, $n] = @a[$n, $k];
    }
    @a
}

enum Pip <A 2 3 4 5 6 7 8 9 10 J Q K>;
enum Suit<♦ ♣ ♥ ♠>;

class Card {
    has Pip $.pip;
    has Suit $.suit;

    method Str { $!pip ~ $!suit }
}

class Deck {
    has Card @.cards = map { Card.new(:$^pip, :$^suit) }, flat (Pip.pick(*).sort X Suit.pick(*).sort);

    method shuffle { @!cards .= &pick-all }
   #method shuffle { @!cards .= pick: * }

    method deal { shift @!cards }

    method cheat { splice @!cards, @!cards.elems-1, 1 }

    method peek { @!cards[0] }

    method Str  { ~@!cards }
    method gist { ~@!cards }
}

my Deck $d = Deck.new;
my @res.push: "Deck: $d";

my $top = $d.deal;
@res.push: "Deal top card: $top";

$d.shuffle;
@res.push: "Deck, re-shuffled: $d";

my $peek = $d.peek;
@res.push: "Peek at top card: $peek";

my $bottom = $d.cheat;
@res.push: "Bottom of deck card: $bottom";

@res.push: "Deck state now: $d";

.say for @res;

my $ref = qq:to/END/;
Deck: A♦ A♣ A♥ A♠ 2♦ 2♣ 2♥ 2♠ 3♦ 3♣ 3♥ 3♠ 4♦ 4♣ 4♥ 4♠ 5♦ 5♣ 5♥ 5♠ 6♦ 6♣ 6♥ 6♠ 7♦ 7♣ 7♥ 7♠ 8♦ 8♣ 8♥ 8♠ 9♦ 9♣ 9♥ 9♠ 10♦ 10♣ 10♥ 10♠ J♦ J♣ J♥ J♠ Q♦ Q♣ Q♥ Q♠ K♦ K♣ K♥ K♠
Deal top card: A♦
Deck, re-shuffled: 6♥ A♣ 8♣ K♦ 10♠ 5♠ 2♦ 3♥ 3♠ 6♠ Q♦ 2♣ Q♥ 5♣ 9♠ 8♥ 10♥ Q♣ K♣ 4♥ J♦ K♠ 4♦ 7♠ 4♠ 10♦ 4♣ 9♦ 9♣ 3♣ A♠ 7♣ 5♥ J♠ 6♦ A♥ J♥ Q♠ 7♥ 10♣ 6♣ 7♦ 2♥ 5♦ J♣ K♥ 3♦ 8♦ 2♠ 9♥ 8♠
Peek at top card: 6♥
Bottom of deck card: 8♠
Deck state now: 6♥ A♣ 8♣ K♦ 10♠ 5♠ 2♦ 3♥ 3♠ 6♠ Q♦ 2♣ Q♥ 5♣ 9♠ 8♥ 10♥ Q♣ K♣ 4♥ J♦ K♠ 4♦ 7♠ 4♠ 10♦ 4♣ 9♦ 9♣ 3♣ A♠ 7♣ 5♥ J♠ 6♦ A♥ J♥ Q♠ 7♥ 10♣ 6♣ 7♦ 2♥ 5♦ J♣ K♥ 3♦ 8♦ 2♠ 9♥
END

use Test;
is @res.join("\n"), chomp $ref;
