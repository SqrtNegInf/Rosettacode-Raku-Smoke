#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Test_a_function
#c# 2016-02-11 <>RC, 2018-04-08 <RC, 2022-10-05 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# switch to testing with 'subset', just to be different

use Test;

subset Palindrom of Str where {
    .flip eq $_ given .comb(/\w+/).join.lc
}

sub palin( Str $string) { so $string.lc.comb(/\w/) eq  $string.flip.lc.comb(/\w/) }

my %tests =
    'A man, a plan, a canal: Panama.'           => True,
    'My dog has fleas'                          => False,
    "Madam, I'm Adam."                          => True,
    '1 on 1'                                    => False,
    'In girum imus nocte et consumimur igni'    => True,
    ''                                          => True,
    ;

plan %tests.elems;

for %tests.kv -> $test, $expected-result {
    #is $test ~~ palin($test), $expected-result,
    is $test ~~ Palindrom, $expected-result,
        "\"$test\" is {$expected-result??''!!'not '}a palindrome.";
}

done-testing;

#`{
1..6
ok 1 - "1 on 1" is not a palindrome.
ok 2 - "My dog has fleas" is not a palindrome.
ok 3 - "A man, a plan, a canal: Panama." is a palindrome.
ok 4 - "" is a palindrome.
ok 5 - "Madam, I'm Adam." is a palindrome.
ok 6 - "In girum imus nocte et consumimur igni" is a palindrome.
}
