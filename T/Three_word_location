#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Three_word_location
#c# 2020-07-22 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# 2023-01-07 replace call to built-in 'rand' for output stability

my @res;

use myRNG;
my $rng = myRNG.new( :seed(123456) );

# SYNTHETICS HANDLING
my @synth = flat < b d f h j k l m n p r s t w y z > X~ < a e i o u >;
my %htnys = @synth.antipairs;
my $exp   = @synth.elems;

#sub synth (Int $v) { @synth[($v + (^18).pick × 28126).polymod($exp xx *).reverse || 0].join }
sub synth (Int $v) { @synth[($v + floor(18×$rng.next-rat) × 28126).polymod($exp xx *).reverse || 0].join }

sub thnys (Str $v) { (sum %htnys{$v.comb(2).reverse} Z× 1, $exp, $exp**2) % 28126 }

# ENCODE / DECODE
sub w-encode ( Rat(Real) $lat, Rat(Real) $lon, :&f = &synth ) {
    my $bits = (($lat +  90) × 10000).round.fmt('%021b') ~ (($lon + 180) × 10000).round.fmt('%022b');
    #(:2(.substr(0,15)), :2(.substr(15,14)), :2(.substr(29)))».&f
    ([(0,15), (15,14), 29].map: { :2($bits.substr(|$_)) } )».&f       # whoop-de-doo
}

sub w-decode ( *@words, :&f = &thnys ) {
    my $bin = (@words».&f Z, <0 1 1>).map({.[0].fmt('%015b').substr(.[1])}).join;
    #(:2($bin.substr(0,21))/10000) - 90, (:2($bin.substr(21))/10000) - 180
    (((0,21), 21).map: { :2($bin.substr(|$_))/10_000 }) Z- (90, 180)  # doo-de-whoop
}

# TESTING
for 51.4337,  -0.2141, # Wimbledon
    21.2596,-157.8117, # Diamond Head crater
   -55.9652, -67.2256, # Monumento Cabo De Hornos
    59.3586,  24.7447, # Lake Raku
    29.2021,  81.5324, # Village Raku
    -7.1662,  53.9470, # The Indian ocean, south west of Seychelles
    28.3852, -81.5638  # Walt Disney World
  -> $lat, $lon {
    my @words = w-encode $lat, $lon;
    my @index = w-encode $lat, $lon, :f( { $_ } );
    @res.push: sprintf "Coordinates: %s, %s\n   To Index: %s\n  To 3-word: %s\nFrom 3-word: %s, %s\n From Index: %s, %s\n",
      $lat, $lon, @index.Str, @words.Str, w-decode(@words), w-decode @index, :f( { $_ } );
}

.say for @res;

my $ref = q:to/END/;
Coordinates: 51.4337, -0.2141
   To Index: 22099 365 12003
  To 3-word: jebino hirawu pawato
From 3-word: 51.4337, -0.2141
 From Index: 51.4337, -0.2141

Coordinates: 21.2596, -157.8117
   To Index: 17384 5133 8891
  To 3-word: suwiti lepaha tiyoha
From 3-word: 21.2596, -157.8117
 From Index: 21.2596, -157.8117

Coordinates: -55.9652, -67.2256
   To Index: 5317 15428 13632
  To 3-word: kihayo yisobu sujaya
From 3-word: -55.9652, -67.2256
 From Index: -55.9652, -67.2256

Coordinates: 59.3586, 24.7447
   To Index: 23337 4732 15831
  To 3-word: tatiha tijehe haripu
From 3-word: 59.3586, 24.7447
 From Index: 59.3586, 24.7447

Coordinates: 29.2021, 81.5324
   To Index: 18625 5535 10268
  To 3-word: jamudu homamu puzaru
From 3-word: 29.2021, 81.5324
 From Index: 29.2021, 81.5324

Coordinates: -7.1662, 53.947
   To Index: 12942 12942 12942
  To 3-word: tonowe fatuyu jozuri
From 3-word: -7.1662, 53.947
 From Index: -7.1662, 53.947

Coordinates: 28.3852, -81.5638
   To Index: 18497 11324 1322
  To 3-word: tabesa tojopo heni
From 3-word: 28.3852, -81.5638
 From Index: 28.3852, -81.5638

END

use Test;
is @res.join("\n"), chomp $ref;
