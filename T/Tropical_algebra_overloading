#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Tropical_algebra_overloading
#c# 2021-08-11 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

# No need to overload, define our own operators with whatever precedence level we want. Here we're setting precedence equivalent to existing operators.

sub infix:<⊕> (Real $a, Real $b) is equiv(&[+]) { $a max $b }
sub infix:<⊗> (Real $a, Real $b) is equiv(&[×]) { $a + $b }
sub infix:<↑> (Real $a,  Int $b where * ≥ 0) is equiv(&[**]) { [⊗] $a xx $b }

use Test;

@res.push: is-deeply(      2 ⊗ -2,        0, '2 ⊗ -2 == 0' );
@res.push: is-deeply( -0.001 ⊕ -Inf, -0.001, '-0.001 ⊕ -Inf == -0.001' );
@res.push: is-deeply(      0 ⊗ -Inf,   -Inf, '0 ⊗ -Inf == -Inf' );
@res.push: is-deeply(    1.5 ⊕ -1,      1.5, '1.5 ⊕ -1 == 1.5' );
@res.push: is-deeply(   -0.5 ⊗ 0,      -0.5, '-0.5 ⊗ 0 == -0.5' );
@res.push: is-deeply(      5 ↑ 7,        35, '5 ↑ 7 == 35' );
@res.push: is-deeply( 5 ⊗ (8 ⊕ 7),  5 ⊗ 8 ⊕ 5 ⊗ 7, '5 ⊗ (8 ⊕ 7) == 5 ⊗ 8 ⊕ 5 ⊗ 7');
@res.push: is-deeply( 5 ↑ 7 ⊕ 6 ↑ 6,     36, '5 ↑ 7 ⊕ 6 ↑ 6 == 36');

.say for @res;

my $ref = q:to/END/;
True
True
True
True
True
True
True
True
END

my $xef = q:to/END/;
ok 1 - 2 ⊗ -2 == 0
ok 2 - -0.001 ⊕ -Inf == -0.001
ok 3 - 0 ⊗ -Inf == -Inf
ok 4 - 1.5 ⊕ -1 == 1.5
ok 5 - -0.5 ⊗ 0 == -0.5
ok 6 - 5 ↑ 7 == 35
ok 7 - 5 ⊗ (8 ⊕ 7) == 5 ⊗ 8 ⊕ 5 ⊗ 7
ok 8 - 5 ↑ 7 ⊕ 6 ↑ 6 == 36
END

use Test;
is @res.join("\n"), chomp $ref;
