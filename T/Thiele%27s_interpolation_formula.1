#!/usr/bin/env perl6
#n# NOT CURRENT, NOT SMOKE-TESTED
#u# http://rosettacode.org/wiki/Thiele\'s_interpolation_formula
#c# 2016-05-01 <>RC, 2018-06-05 >RC, 2018-10-16 >RC
#m# MOAR: OK
#j#  JVM: OK
#n# hash key order dependency (as of 2018-05-17, JVM doesn't have this 'upgrade')
#  made default table size 12, 'invcos' converges slowly

# Implemented to parallel the (generalized) formula. (i.e. clearer, but naive and very slow.)

# reciprocal difference:
multi sub ρ(&f, @x where * < 1) { 0 } # Identity
multi sub ρ(&f, @x where * == 1) { &f(@x[0]) }
multi sub ρ(&f, @x where * > 1) {
    ( @x[0] - @x[* - 1] )       # ( x - x[n] )
    / (ρ(&f, @x[^(@x - 1)])     # / ( ρ[n-1](x[0], ..., x[n-1])
    - ρ(&f, @x[1..^@x]) )       # - ρ[n-1](x[1], ..., x[n]) )
    + ρ(&f, @x[1..^(@x - 1)]);  # + ρ[n-2](x[1], ..., x[n-1])
}

# Thiele:
multi sub thiele($x, %f, $ord where { $ord == +%f }) { 1 } # Identity
multi sub thiele($x, %f, $ord) {
  my &f = {%f{$^a}};                # f(x) as a table lookup

  # Caveat: depends on the fact that Rakudo maintains key order within hashes
  my $a = ρ(&f, %f.keys.sort[^($ord +1)]);
  my $b = ρ(&f, %f.keys.sort[^($ord -1)]);

  my $num = $x - %f.keys.sort[$ord];
  my $cont = thiele($x, %f, $ord +1);

  # Thiele always takes this form:
  return $a - $b + ( $num / $cont );
}

## Demo
sub mk-inv(&fn, $d, $lim) {
  my %h;
  for 0..$lim { %h{ &fn($_ * $d) } = $_ * $d }
  return %h;
}

sub MAIN($tblsz = 12) {

  my ($sin_pi, $cos_pi, $tan_pi);
  my %invsin = mk-inv(&sin, 0.05, $tblsz); $sin_pi = 6 * thiele(0.5, %invsin, 0);
  my %invcos = mk-inv(&cos, 0.05, $tblsz); $cos_pi = 3 * thiele(0.5, %invcos, 0);
  my %invtan = mk-inv(&tan, 0.05, $tblsz); $tan_pi = 4 * thiele(1.0, %invtan, 0);

  say "pi = {pi}";
  say "estimations using a table of $tblsz elements:";
  say "sin interpolation: $sin_pi";
  say "cos interpolation: $cos_pi";
  say "tan interpolation: $tan_pi";

use Test;
if $*VM ~~ /jvm/ {
is ($sin_pi, $cos_pi, $tan_pi), '3.1415926535896332 3.1387243219841157 3.1416331607611547';
} else {
#is ($sin_pi, $cos_pi, $tan_pi), '3.1415926535896332 3.1387243219840935 3.14163334892717'; # 2018-07-31 sig dig default?
is ($sin_pi, $cos_pi, $tan_pi), '3.1415926535896332 3.138724321984095 3.14163334892717';
}

}
