#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Topswops
#c# 2020-10-29 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# yeah, still slow-ish on 10

# Alternately, using string manipulation instead. Much faster, though honestly, still not very fast.

sub swops($a is copy) {
    my int $count = 0;
    while (my \l = $a.ord) > 1 {
        $a = $a.substr(0, l).flip ~ $a.substr(l);
        ++$count;
    }
    $count
}

sub topswops($n) { max (1..$n).permutations.map: { .chrs.join.&swops } }

my @res.push: "$_ {topswops $_}" for 1 .. 9;

.say for @res;

my $ref = q:to/END/;
1 0
2 1
3 2
4 4
5 7
6 10
7 16
8 22
9 30
END
#10 38

use Test;
is @res.join("\n"), chomp $ref;
