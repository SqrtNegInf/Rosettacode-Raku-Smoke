#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Topswops
#c# 2018-12-01 <RC, 2019-10-14 >RC
#m# MOAR: OK
#j# JVM:  BROKEN
#n# for JVM, use .1 version
#n# bumping up to 9 swaps, but 10 still too slow ( > 7 min)
#n# even with ':25batch', 10 is slow: 1924.109u 52.896s 6:38.00 496.7%

sub swops(@a is copy) {
    my int $count = 0;
    until @a[0] == 1 {
        @a[ ^@a[0] ] .= reverse;
        ++$count;
    }
    $count
}

sub topswops($n) { max (1..$n).permutations.race(:8degree, :25batch).map: *.&swops }

my @res.push: "$_ {topswops $_}" for 1 .. 9; #10;

.say for @res;

my $ref = q:to/END/;
1 0
2 1
3 2
4 4
5 7
6 10
7 16
8 22
9 30
END
#10 38

use Test;
is @res.join("\n"), chomp $ref;
