#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Text_processing/2
#c# 2018-03-15 <RC
#m# MOAR: OK
#j# JVM:  OK

# Translation of: Perl
# Works with: Rakudo version 2010.11

my $fields = 49;

my ($good-records, %dates) = 0;
#for flat 1 .. * Z $*IN.lines -> $line, $s {
for flat 1 .. * Z slurp("ref/text-proc.txt").lines -> $line, $s {
    my @fs = split /\s+/, $s;
    @fs == $fields or die "$line: Bad number of fields";
    given shift @fs {
        m/\d**4 \- \d**2 \- \d**2/ or die "$line: Bad date format";
        ++%dates{$_};
    }
    my $all-flags-okay = True;
    for @fs -> $val, $flag {
        $val ~~ /\d+ \. \d+/ or die "$line: Bad value format";
        $flag ~~ /^ \-? \d+/ or die "$line: Bad flag format";
        $flag < 1 and $all-flags-okay = False;
    }
    $all-flags-okay and ++$good-records;
}

my @res1;
@res1.push: 'Good records: '~ $good-records;
@res1.push: 'Repeated timestamps:';
@res1.push: '  '~ $_ for grep { %dates{$_} > 1 }, sort keys %dates;

.say for @res1;

#The first version demonstrates that you can program Raku almost like Perl 5. Here's a more idiomatic Raku version that runs several times faster:

#my $good-records;
my $line;
my %dates2;
my $good-records2;


my @res2;

for slurp("ref/text-proc.txt").lines {
    $line++;
    / ^
    (\d ** 4 '-' \d\d '-' \d\d)
    [ \h+ \d+'.'\d+ \h+ ('-'?\d+) ] ** 24
    $ /
        or note "Bad format at line $line" and next;
    %dates2.push: $0 => $line;
    $good-records2++ if $1.all >= 1;
}

@res2.push: "$good-records2 good records out of $line total";

@res2.push: 'Repeated timestamps (with line numbers):';
@res2.push: $_ for sort %dates2.pairs.grep: *.value.elems > 1;

.say for @res2;

#`{
Note how this version does validation with a single Raku regex that is much more readable than the typical regex, and arguably expresses the data structure more straightforwardly.
Here we use normal quotes for literals, and \h for horizontal whitespace.
Variables like $good-record that are going to be autoincremented do not need to be initialized. (Raku allows hyphens in variable names, as you can see.)
The .push method on a hash is magical and loses no information; if a duplicate key is found in the pushed pair, an array of values is automatically created of the old value and the new value pushed. Hence we can easily track all the lines that a particular duplicate occurred at.
The .all method does "junctional" logic: it autothreads through comparators as any English speaker would expect. Junctions can also short-circuit as soon as they find a value that doesn't match, and the evaluation order is up to the computer, so it can be optimized or parallelized.
The final line simply greps out the pairs from the hash whose value is an array with more than 1 element. (Those values that are not arrays nevertheless have a .elems method that always reports 1.) The .pairs is merely there for clarity; grepping a hash directly has the same effect.
Note that we sort the pairs after we've grepped them, not before; this works fine in Raku, sorting on the key and value as primary and secondary keys. Finally, pairs and arrays provide a default print format that is sufficient without additional formatting in this case.
}

my $ref1 = qq:to/END/;
Good records: 5017
Repeated timestamps:
  1990-03-25
  1991-03-31
  1992-03-29
  1993-03-28
  1995-03-26
END

my $ref2 = qq:to/END/;
5017 good records out of 5471 total
Repeated timestamps (with line numbers):
1990-03-25	84 85
1991-03-31	455 456
1992-03-29	819 820
1993-03-28	1183 1184
1995-03-26	1910 1911
END

use Test;
is @res1.join("\n"), chomp $ref1;
is @res2.join("\n"), chomp $ref2;
