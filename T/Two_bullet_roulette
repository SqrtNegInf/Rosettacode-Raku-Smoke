#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Two_bullet_roulette
#c# 2020-11-30 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# ends up a little different than RC version

my @res;

srand 123456;

unit sub MAIN ($shots = 6);

my @cyl;

sub load () {
    @cyl.=rotate(-1) while @cyl[1];
    @cyl[1] = 1;
    @cyl.=rotate(-1);
}

sub spin () { @cyl.=rotate: (^@cyl).pick }

sub fire () { @cyl.=rotate; @cyl[0] }

sub LSLSFSF {
    @cyl = 0 xx $shots;
    load, spin, load, spin;
    return 1 if fire;
    spin;
    fire
}

sub LSLSFF {
    @cyl = 0 xx $shots;
    load, spin, load, spin;
    fire() || fire
}

sub LLSFSF {
    @cyl = 0 xx $shots;
    load, load, spin;
    return 1 if fire;
    spin;
    fire
}

sub LLSFF {
    @cyl = 0 xx $shots;
    load, load, spin;
    fire() || fire
}

my $trials = 10000;

for &LSLSFSF, &LSLSFF, &LLSFSF, &LLSFF -> $ref {
    #@res.push: sprintf '%7s: %%%.2f', $ref.gist.substr(1), ([+] ($ref() for ^$trials)) / $trials × 100;

    my $total += $ref() for ^$trials;
    @res.push: "{$ref.gist.substr(1).fmt('%7s')}: %{($total / $trials × 100).fmt('%.2f')}"
}

.say for @res;

my $moar-cronjob = q:to/END/;
LSLSFSF: %55.55
 LSLSFF: %57.81
 LLSFSF: %54.78
  LLSFF: %49.24
END

my $moar-terminal = q:to/END/;
LSLSFSF: %55.56
 LSLSFF: %57.80
 LLSFSF: %54.81
  LLSFF: %49.23
END

my $jvm = q:to/END/;
LSLSFSF: %55.99
 LSLSFF: %58.25
 LLSFSF: %55.43
  LLSFF: %50.33
END

use Test;
is @res.join("\n"), chomp $*VM ~~ /jvm/ ?? $jvm !! $*IN.t ?? $moar-terminal !! $moar-cronjob;

#`{
# Though if you go and look at the  Wikipedia article for the 1895 Nagant revolver mentioned in the task reference section, you'll see it is actually a 7 shot revolver... so, run again with 7 chambers:
raku roulette.raku 7

Sample output (7 shooter):
LSLSFSF: %49.29
 LSLSFF: %51.14
 LLSFSF: %48.74
  LLSFF: %43.08

# Or, how about a Ruger GP100 10 round revolver?
raku roulette.raku 10

Sample output (10 shooter):
LSLSFSF: %36.00
 LSLSFF: %37.00
 LLSFSF: %36.13
  LLSFF: %29.77
Doesn't change the answers, B (LSLSFF) is definitely the worst most likely choice in all cases.
}
