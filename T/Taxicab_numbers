#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Taxicab_numbers
#c# 2016-01-23 <RC, 2018-10-11 <RC
#m# MOAR: OK
#j# JVM:  OK
#f# RC file: taxicab_numbers.txt
#n# OEIS:A001235
#n# writing results to file as cannot get '@res.push:' to work right [ DH remove this file when clear this works ]

my @res;

constant @cu = (^Inf).map: { .³ }

my $start = 1;
my $end = 25;

my %taxi;
my int $taxis = 0;
my $terminate = 0;
my int $max = 0;

for 1 .. * -> $c1 {
    last if ?$terminate && ($terminate < $c1);
    for 1 .. $c1 -> $c2 {
        my $this = @cu[$c1] + @cu[$c2];
        %taxi{$this}.push: [$c2, $c1];
        if %taxi{$this}.elems == 2 {
            ++$taxis;
            $max max= $this;
        }
        $terminate = ceiling $max ** (1/3) if $taxis == $end and !$terminate;
    }
}

my $i = $start;

@res.push: sprintf "%4d%10d  =>%12s", $i++, $_.key, ($_.value.map({ sprintf "%4d³ + %-s", $_[0], "$_[1]\³" })).join: ",\t"
    for %taxi.grep( { $_.value.elems > 1 } ).sort( +*.key )[$start-1..$end-1];

.say for @res;

my $ref = q:to/END/;
   1      1729  =>   9³ + 10³,	   1³ + 12³
   2      4104  =>   9³ + 15³,	   2³ + 16³
   3     13832  =>  18³ + 20³,	   2³ + 24³
   4     20683  =>  19³ + 24³,	  10³ + 27³
   5     32832  =>  18³ + 30³,	   4³ + 32³
   6     39312  =>  15³ + 33³,	   2³ + 34³
   7     40033  =>  16³ + 33³,	   9³ + 34³
   8     46683  =>  27³ + 30³,	   3³ + 36³
   9     64232  =>  26³ + 36³,	  17³ + 39³
  10     65728  =>  31³ + 33³,	  12³ + 40³
  11    110656  =>  36³ + 40³,	   4³ + 48³
  12    110808  =>  27³ + 45³,	   6³ + 48³
  13    134379  =>  38³ + 43³,	  12³ + 51³
  14    149389  =>  29³ + 50³,	   8³ + 53³
  15    165464  =>  38³ + 48³,	  20³ + 54³
  16    171288  =>  24³ + 54³,	  17³ + 55³
  17    195841  =>  22³ + 57³,	   9³ + 58³
  18    216027  =>  22³ + 59³,	   3³ + 60³
  19    216125  =>  45³ + 50³,	   5³ + 60³
  20    262656  =>  36³ + 60³,	   8³ + 64³
  21    314496  =>  30³ + 66³,	   4³ + 68³
  22    320264  =>  32³ + 66³,	  18³ + 68³
  23    327763  =>  51³ + 58³,	  30³ + 67³
  24    373464  =>  54³ + 60³,	   6³ + 72³
  25    402597  =>  56³ + 61³,	  42³ + 69³
END

use Test;
is @res.join("\n"), chomp $ref;
