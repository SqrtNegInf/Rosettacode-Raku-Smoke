#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Topswops
#c# 2016-09-07 <>RC
#m# MOAR: OK
#j#  JVM: OK
#n# limit testing, full run takes over an hour

sub swops(@a is copy) {
    my $count = 0;
    until @a[0] == 1 {
        @a[ ^@a[0] ] .= reverse;
        $count++;
    }
    return $count;
}

#sub topswops($n) { (sort map &swops, (1..$n).permutations)[*-1] } # previous work-around for 'too many args' in next line
sub topswops($n) { [max] map &swops, (1..$n).permutations }

my @res.push: "$_ {topswops $_}" for 1 .. 7;
.say for @res;

my $ref = qq:to/END/;
1 0
2 1
3 2
4 4
5 7
6 10
7 16
END

use Test;
is @res.join("\n"), chomp $ref;
