#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Greatest_subsequential_sum
#c# 2015-12-06 <RC
#m# MOAR: OK
#j#  JVM: OK

sub maxsubseq (*@a) {
    my ($start, $end, $sum, $maxsum) = -1, -1, 0, 0;
    for @a.kv -> $i, $x {
        $sum += $x;
        if    $maxsum < $sum { ($maxsum, $end) = $sum, $i }
        elsif    $sum < 0    { ($sum, $start) = 0, $i     }
    }
    return @a[$start ^.. $end];
}

say my $result = maxsubseq( -1, -2,  3,  5,  6, -2, -1,  4, -4,  2, -1 );

use Test;
is $result, '3 5 6 -2 -1 4';
