#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Guess_the_number/With_feedback_(player)
#c# 2018-03-21 <RC
#m# MOAR: OK
#j# JVM:  OK
#=# RC pipe: l\nh\nh\nl\nh\nl\ne # for JVM
#n# 42 is the target number

srand 123456;

use Test;

multi sub MAIN() { MAIN(0, 100) }
multi sub MAIN($min is copy where ($min >= 0), $max is copy where ($max > $min)) {
    say "Think of a number between $min and $max and I'll guess it!";
    while $min <= $max {
        my $guess = (($max + $min)/2).floor;
        given lc prompt "My guess is $guess. Is your number higher, lower or equal? " {
            when /^e/ { say "I knew it!"; is $guess, 42; exit;}  # DH change
            when /^h/ { $min = $guess + 1      }
            when /^l/ { $max = $guess          }
            default   { say "WHAT!?!?!"        }
        }
    }
    say "How can your number be both higher and lower than $max?!?!?";
}
