#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Galton_box_animation
#c# 2016-02-09 <RC
#m# MOAR: OK
#j#  JVM: OK
#f# RC file: galton_box.dat
#n# minor idiomatification
#n# RC x-cli-x: 10 4 (this wasn't working for JVM?)
#n# 2023-01-04 replace call to built-in 'rand' for output stability

use myRNG;
my $rng = myRNG.new( :seed(123456) );

# UPPER HALF BLOCK and LOWER HALF BLOCK alternate to give a somewhat smooth animation.

my $fh = open("run/galton_box.dat", :w) || die;

my $row-count = 6;

constant $peg = '*';
constant @coin-icons = "\c[UPPER HALF BLOCK]", "\c[LOWER HALF BLOCK]";

sub display-board(@positions, @stats is copy, $halfstep) {
    my $coin = @coin-icons[$halfstep.Int];

    state @board-tmpl = {
        # precompute a board
        my @tmpl;
        sub out(*@stuff) {
            @tmpl.push: $[@stuff.join>>.ords.flat];
        }
        # three lines of space above
        out '  ', ' ' x (2 × $row-count) for 1..3; # DH

        # $row-count lines of pegs
        for flat ($row-count...1) Z (1...$row-count) -> $spaces, $pegs {
            out '  ', ' ' x $spaces, ($peg xx $pegs).join(' '), ' ' x $spaces;
        }
        # four lines of space below
        out '  ', ' ' x (2 × $row-count) for 1..4; # DH

        @tmpl
    }();

    my $midpos = $row-count + 2;

    my @output;
    {
        # collect all the output and output it all at once at the end
        sub   say(Str $foo) { @output.push: $foo, "\n" }
        sub print(Str $foo) { @output.push: $foo       }

        # make some space above the picture
        say '' for ^10;

        my @output-lines = map { [map *.clone, @$_].item }, @board-tmpl;
        # place the coins
        for @positions.kv -> $line, $pos {
            next unless $pos.defined;
            @output-lines[$line][$pos + $midpos] = $coin.ord;
        }
        # output the board with its coins
        for @output-lines -> @line {
            say @line>>.chr.join('');
        }

        # show the statistics
        my $padding = 0;
        while any(@stats) > 0 {
            $padding++;
            print '  ';
            @stats = do for @stats -> $stat {
                given $stat {
                    when 1 {
                        print "\c[UPPER HALF BLOCK]";
                        $stat - 1;
                    }
                    when * <= 0 {
                        print ' ';
                        0
                    }
                    default {
                        print "\c[FULL BLOCK]";
                        $stat - 2;
                    }
                }
            }
            say '';
        }
        say '' for $padding..^10; # DH works same as ...^10;
    }
    $fh.say: @output.join('');
}

sub simulate($coins is copy) {
    my $alive = True;

    sub hits-peg($x, $y) {
        if 3 <= $y < 3 + $row-count and -($y - 2) <= $x <= $y - 2 {
            return not ($x - $y) %% 2;
        }
        return False;
    }

    my @coins = Int xx (3 + $row-count + 4);
    my @stats = 0 xx ($row-count × 2);
    # this line will dispense coins until turned off.
    @coins[0] = 0;
    while $alive {
        $alive = False;
        # if a coin falls through the bottom, count it
        given @coins[*-1] {
            when *.defined {
                @stats[$_ + $row-count]++;
            }
        }

        # move every coin down one row
        for ( 3 + $row-count + 3 )...1 -> $line {
            my $coinpos = @coins[$line - 1];

            @coins[$line] = do if not $coinpos.defined {
                Nil
            } elsif hits-peg($coinpos, $line) {
                # when a coin from above hits a peg, it will bounce to either side.
                $alive = True;
               #($coinpos - 1, $coinpos + 1).pick;
                $rng.next-rat < 0.5 ?? $coinpos - 1 !! $coinpos + 1;
            } else {
                # if there was a coin above, it will fall to this position.
                $alive = True;
                $coinpos;
            }
        }
        # let the coin dispenser blink and turn it off if we run out of coins
        if @coins[0].defined {
            @coins[0] = Nil
        } elsif --$coins > 0 {
            @coins[0] = 0 
        }

        # smooth out the two halfsteps of the animation
        my $start-time;
        ENTER { $start-time = now }
        my $wait-time = now - $start-time;

        sleep 0.01 - $wait-time if $wait-time < 0.01;
        for @coin-icons.keys {
            sleep $wait-time max 0.1;
            display-board(@coins, @stats, $_);
        }
    }
}

#sub MAIN($coins =250, $peg-lines = 13) {
sub MAIN($coins =10, $peg-lines = 4) {

    $row-count = $peg-lines;
    simulate($coins);
}
