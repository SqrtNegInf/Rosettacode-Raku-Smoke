#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Generate_Chess960_starting_position
#c# 2016-06-02 <RC, 2018-03-28 >RC, 2018-04-03 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# 2021-06-04 results for test #2 changed
#n# 2023-02-09 test #2 again, replace it with 'myRNG'
#n# 2023-06-03 test #1 this time, replace it too

use myRNG;

# Knuth shuffle
sub pick-all (@a is copy) is export {
    my $rng = myRNG.new( :seed(123456) );
    for 1 ..^ @a -> $n {
        my $k = (0 .. $n)[($n+1) × $rng.next-rat];
        $k == $n or @a[$k, $n] = @a[$n, $k];
    }
    @a
}

# First, using a list with three rooks and no king, we keep generating a random piece order until the two bishops
# are on opposite colors. Then we sneakily promote the second of the three rooks to a king.

repeat until m/ '♗' [..]* '♗' / { $_ = < ♖ ♖ ♖ ♕ ♗ ♗ ♘ ♘ >.&pick-all.join }
s:2nd['♖'] = '♔';
put .comb;

use Test;
is (.comb), $*VM ~~ /jvm/ ?? '♕ ♘ ♖ ♘ ♔ ♗ ♗ ♖' !! '♖ ♔ ♗ ♘ ♖ ♘ ♕ ♗';

# Here's a more "functional" solution that avoids side effects

sub chess960 {
    .subst(:nth(2), /'♜'/, '♚') given
    first rx/ '♝' [..]* '♝' /,
    < ♛ ♜ ♜ ♜ ♝ ♝ ♞ ♞ >.&pick-all.join ; #xx *;
}

say my $result = chess960;
my $moar = '♜♛♝♞♚♞♜♝';
#my $jvm  = '♜♞♚♝♜♞♝♛';
is $result, $moar;

# We can also pregenerate the list of 960 positions, though the method we use below is a bit wasteful, since it
# generates 40320 candidates only to throw most of them away. This is essentially the same filtering algorithm
# but written in the form of a list comprehension rather than nested map and grep. (The list comprehension is actually faster currently.) Note that the constant is calculated at compile time, because, well, it's a constant. Just a big fancy one.

constant chess960-const1 =
   < ♛ ♜ ♜ ♜ ♝ ♝ ♞ ♞ >.permutations».join.unique.grep( / '♝' [..]* '♝' / )».subst(:nth(2), /'♜'/, '♚');

#.say for chess960-const1; 
is +chess960-const1, 960;

# Here's a much faster way (about 30x) to generate all 960 variants by construction. No need to filter for uniqueness, since it produces exactly 960 entries.

constant chess960-const2 = gather for 0..3 -> $q {
    (my @q = <♜ ♚ ♜>).splice($q, 0, '♛');
    for 0 .. @q -> $n1 {
        (my @n1 = @q).splice($n1, 0, '♞');
        for $n1 ^.. @n1 -> $n2 {
            (my @n2 = @n1).splice($n2, 0, '♞');
            for 0 .. @n2 -> $b1 {
                (my @b1 = @n2).splice($b1, 0, '♝');
                for $b1+1, $b1+3 ...^ * > @b1 -> $b2 {
                    (my @b2 = @b1).splice($b2, 0, '♝');
                    take @b2.join;
                }
            }
        }
    }
}

#CHECK { note "done compiling" }
is +chess960-const2, 960;
#say chess960-const2.pick;

#`{
done compiling
960
♜♚♝♜♞♛♞♝
If you run this you'll see that most of the time is spent in compilation, so in the case of separate precompilation the table of 960 entries merely needs to be deserialized back into memory. Picking from those entries guarantees uniform distribution over all possible boards.

}
