#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Modified_random_distribution
#c# 2021-02-28 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# 2023-01-10 replace call to built-in 'rand' for output stability

my @res;

use myRNG;
my $rng = myRNG.new( :seed(123456) );

sub modified_random_distribution ( Code $modifier --> Seq ) {
    return lazy gather loop {
        my ( $r1, $r2 ) = $rng.next-rat, $rng.next-rat;
        take $r1 if $modifier.($r1) > $r2;
    }
}
sub modifier ( Numeric $x --> Numeric ) {
    return 2 × ( $x < 1/2 ?? ( 1/2 - $x  )
                          !! ( $x  - 1/2 ) );
} 
sub print_histogram ( @data, :$n-bins, :$width ) { # Assumes minimum of zero.
    my %counts = bag @data.map: { floor( $_ × $n-bins ) / $n-bins };
    my $max_value = %counts.values.max;
    sub hist { '■' x ( $width × $^count / $max_value ) }
    #say ' Bin, Counts: Histogram';
    @res.push: sprintf "%4.2f, %6d: %s\n", .key, .value, hist(.value) for %counts.sort;
}

my @d = modified_random_distribution( &modifier );

print_histogram( @d.head(50_000), :n-bins(20), :width(64) );

.print for @res;

my $ref = q:to/END/;
0.00,   4757: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.05,   4287: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.10,   3863: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.15,   3147: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.20,   2786: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.25,   2269: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.30,   1714: ■■■■■■■■■■■■■■■■■■■■■■■
0.35,   1199: ■■■■■■■■■■■■■■■■
0.40,    787: ■■■■■■■■■■
0.45,    271: ■■■
0.50,    252: ■■■
0.55,    781: ■■■■■■■■■■
0.60,   1280: ■■■■■■■■■■■■■■■■■
0.65,   1703: ■■■■■■■■■■■■■■■■■■■■■■
0.70,   2264: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.75,   2786: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.80,   3110: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.85,   3703: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.90,   4272: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
0.95,   4769: ■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■■
END

use Test;
is @res.join(''), $ref;
