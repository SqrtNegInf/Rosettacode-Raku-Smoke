#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Multifactorial
#c# 2015-11-29 <RC
#m# MOAR: OK
#j#  JVM: OK

my @res;
my @rez;
for 1 .. 5 -> $degree {
    @rez.push: "$degree: " ~ map { [×] $_, *-$degree ...^ * <= 0 }, 1..10;
    sub mfact($n) { [×] $n, *-$degree ...^ * <= 0 };
    @res.push: "$degree: " ~ map &mfact, 1..10;       # by default topic var. passed to sub (not via '.&')
}
.say for @res;

my $ref = qq:to/END/;
1: 1 2 6 24 120 720 5040 40320 362880 3628800
2: 1 2 3 8 15 48 105 384 945 3840
3: 1 2 3 4 10 18 28 80 162 280
4: 1 2 3 4 5 12 21 32 45 120
5: 1 2 3 4 5 6 14 24 36 50
END

use Test;
is @res.join("\n"), chomp $ref;
is @rez.join("\n"), chomp $ref;
