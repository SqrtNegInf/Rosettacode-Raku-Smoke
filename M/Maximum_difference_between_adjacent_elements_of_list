#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Maximum_difference_between_adjacent_elements_of_list
#c# 2021-07-18 <RC
#m# MOAR: OK
#j# JVM:  OK

srand 123456;

my @res;

sub max-diff (*@list) {
   return 0 if +@list < 2;
   my $max = @list.rotor(2 => -1).max({ (.[0] - .[1]).abs }).map( (* - *).abs )[0];
   "$max @ elements { @list.rotor(2 => -1).grep( { (.[0] - .[1]).abs == $max } ).gist }"
}

sub min-diff (*@list) {
   return 0 if +@list < 2;
   my $min = @list.rotor(2 => -1).min({ (.[0] - .[1]).abs }).map( (* - *).abs )[0];
   "$min @ elements { @list.rotor(2 => -1).grep( { (.[0] - .[1]).abs == $min } ).gist }"
}

sub avg-diff (*@list) { (+@list > 1) ?? (@list.sum / +@list) !! 0 }

for (
      [ 1,8,2,-3,0,1,1,-2.3,0,5.5,8,6,2,9,11,10,3 ]
     ,[(rand × 1e6) xx 6]
     ,[ e, i, τ, π, ∞ ]
     ,[ 1.9+3.7i, 2.07-13.2i, 0.2+-2.2i, 4.6+0i ]
     ,[ 6 ]
     ,[]
#    ,[<One Two Three>]
    )
-> @list {
    @res.push: 'List: '~ ~ @list.raku;
    for ('  Maximum', &max-diff ,'Minimumum', &min-diff, '  Average', &avg-diff)
    -> $which, &sub {
        @res.push: "$which distance between list elements: " ~ &sub(@list);
    }
    @res.push: '';
}

.say for @res;

my $moar = q:to/END/;
List: [1, 8, 2, -3, 0, 1, 1, -2.3, 0, 5.5, 8, 6, 2, 9, 11, 10, 3]
  Maximum distance between list elements: 7 @ elements ((1 8) (2 9) (10 3))
Minimumum distance between list elements: 0 @ elements ((1 1))
  Average distance between list elements: 3.658824

List: [676457.4829055268e0, 233858.02988237413e0, 641877.8475869104e0, 623421.9182161683e0, 529583.505181364e0, 151267.8751551797e0]
  Maximum distance between list elements: 442599.45302315266 @ elements ((676457.4829055268 233858.02988237413))
Minimumum distance between list elements: 18455.92937074206 @ elements ((641877.8475869104 623421.9182161683))
  Average distance between list elements: 476077.7764879206

List: [2.718281828459045e0, <0+1i>, 6.283185307179586e0, 3.141592653589793e0, Inf]
  Maximum distance between list elements: Inf @ elements ((3.141592653589793 Inf))
Minimumum distance between list elements: 2.896386731590008 @ elements ((2.718281828459045 0+1i))
  Average distance between list elements: Inf+0.2i

List: [<1.9+3.7i>, <2.07-13.2i>, <0.2-2.2i>, <4.6+0i>]
  Maximum distance between list elements: 16.900855007957436 @ elements ((1.9+3.7i 2.07-13.2i))
Minimumum distance between list elements: 4.919349550499537 @ elements ((0.2-2.2i 4.6+0i))
  Average distance between list elements: 2.1925-2.925i

List: [6]
  Maximum distance between list elements: 0
Minimumum distance between list elements: 0
  Average distance between list elements: 0

List: []
  Maximum distance between list elements: 0
Minimumum distance between list elements: 0
  Average distance between list elements: 0

END

my $jvm = q:to/END/;
List: [1, 8, 2, -3, 0, 1, 1, -2.3, 0, 5.5, 8, 6, 2, 9, 11, 10, 3]
  Maximum distance between list elements: 7 @ elements ((1 8) (2 9) (10 3))
Minimumum distance between list elements: 0 @ elements ((1 1))
  Average distance between list elements: 3.658824

List: [413219.2266296165e0, 990414.373129817e0, 252255.44079393047e0, 705663.6259919212e0, 73438.42024000635e0, 536064.5079196139e0]
  Maximum distance between list elements: 738158.9323358865 @ elements ((990414.373129817 252255.44079393047))
Minimumum distance between list elements: 453408.18519799074 @ elements ((252255.44079393047 705663.6259919212))
  Average distance between list elements: 495175.9324508175

List: [2.718281828459045e0, <0+1i>, 6.283185307179586e0, 3.141592653589793e0, Inf]
  Maximum distance between list elements: Inf @ elements ((3.141592653589793 Inf))
Minimumum distance between list elements: 2.896386731590008 @ elements ((2.718281828459045 0+1i))
  Average distance between list elements: Inf+0.2i

List: [<1.9+3.7i>, <2.07-13.2i>, <0.2-2.2i>, <4.6+0i>]
  Maximum distance between list elements: 16.900855007957436 @ elements ((1.9+3.7i 2.07-13.2i))
Minimumum distance between list elements: 4.919349550499537 @ elements ((0.2-2.2i 4.6+0i))
  Average distance between list elements: 2.1925-2.925i

List: [6]
  Maximum distance between list elements: 0
Minimumum distance between list elements: 0
  Average distance between list elements: 0

List: []
  Maximum distance between list elements: 0
Minimumum distance between list elements: 0
  Average distance between list elements: 0

END

#List: ["One", "Two", "Three"]
#Cannot convert string to number: base-10 number must begin with valid digits or '.' in '⏏Two' (indicated by ⏏)
#  in sub max-diff at listdiff.p6 line 3
#  in block  at listdiff.p6 line 33
#  in block <unit> at listdiff.p6 line 20

use Test;
is @res.join("\n"), chomp $*VM ~~ /jvm/ ?? $jvm !! $moar;
