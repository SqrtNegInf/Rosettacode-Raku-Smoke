#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Amb
#c# 2016-11-30 <RC, 2018-06-03 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# the 'while' is cheating, not uploaded to RC

use MONKEY-SEE-NO-EVAL; # needed for 'amb' which constructs on-the-fly anon regex

srand 123456;

sub amb($var,*@a) {
    "[{ @a.pick(*).map: {"||\{ $var = '$_' }"} }]";
}

sub joins ($w1, $w2) {
    $w1.substr(*-1,1) eq $w2.substr(0,1)
}

my $ref = 'that thing grows slowly';
my $result;
while 1 {
'' ~~ m/
    :my ($a,$b,$c,$d);
    <{ amb '$a', <the that a> }>
    <{ amb '$b', <frog elephant thing> }>
    <?{ joins $a, $b }>
    <{ amb '$c', <walked treaded grows> }>
    <?{ joins $b, $c }>
    <{ amb '$d', <slowly quickly> }>
    <?{ joins $c, $d }>
    { $result = "$a $b $c $d"; last if $result eq $ref }
/
};

say $result;

use Test;
is $result, $ref;
