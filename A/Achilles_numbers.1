!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Achilles_numbers
#c# 2022-02-25 <RC
#m# MOAR: OK
#j# JVM:  BROKEN

my @res;

use Prime::Factor;
use Math::Root;

sub is-square-free (Int \n) {
    constant @p = ^100 .map: { next unless .is-prime; .Â² };
    for @p -> \p { return False if n %% p }
    True
}

sub powerful (\n, \k = 2) {
    my @p;
    p(1, 2Ã—k - 1);
    sub p (\m, \r) {
        @p.push(m) and return if r < k;
        for 1 .. (n / m).&root(r) -> \v {
            if r > k {
                next unless is-square-free(v);
                next unless m gcd v == 1;
            }
            p(m Ã— v ** r, r - 1)
        }
    }
    @p
}

my @achilles = powerful(10**5).sort.hyper.grep: {
    my $f = .&prime-factors.Bag;
    (+$f.keys > 1) && (1 == [gcd] $f.values) && (.sqrt.IntÂ² !== $_)
};

my \ðœ‘ = 0, |(1..*).hyper.map: -> \t { t Ã— [Ã—] t.&prime-factors.squish.map: { 1 - 1/$_ } }

my %ps = Set.new: @achilles;

my @strong = @achilles.grep: { ?%ps{ðœ‘[$_]} };

@res.push: "First 50 Achilles numbers:";
@res.push: @achilles[^50].batch(10)Â».fmt("%4d").join("\n");

@res.push: "\nFirst 30 strong Achilles numbers:";
@res.push:   @strong[^30].batch(10)Â».fmt("%5d").join("\n");

my $achilles = powerful(10**9).hyper(:500batch).grep( {
    my $f = .&prime-factors.Bag;
    (+$f.keys > 1) && (1 == [gcd] $f.values) && (.sqrt.IntÂ² !== $_)
} ).classify: { .chars }

@res.push: "\nNumber of Achilles numbers with:";
@res.push: "$_ digits: " ~ +$achilles{$_} // 0 for 2..9;

.say for @res;

my $ref = q:to/END/;
First 50 Achilles numbers:
  72  108  200  288  392  432  500  648  675  800
 864  968  972 1125 1152 1323 1352 1372 1568 1800
1944 2000 2312 2592 2700 2888 3087 3200 3267 3456
3528 3872 3888 4000 4232 4500 4563 4608 5000 5292
5324 5400 5408 5488 6075 6125 6272 6728 6912 7200

First 30 strong Achilles numbers:
  500   864  1944  2000  2592  3456  5000 10125 10368 12348
12500 16875 19652 19773 30375 31104 32000 33275 37044 40500
49392 50000 52488 55296 61731 64827 67500 69984 78608 80000

Number of Achilles numbers with:
2 digits: 1
3 digits: 12
4 digits: 47
5 digits: 192
6 digits: 664
7 digits: 2242
8 digits: 7395
9 digits: 24008
END

use Test;
is @res.join("\n"), chomp $ref;
