#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Associative_array/Merging
#c# 2020-01-13 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

# I must @res.push:  I somewhat disagree with the terminology. The requested operation is an update not a merge. Demonstrate both an update and a merge. Associative arrays are commonly called hashes in Perl 6.

# Show original hashes
my %base   = :name('Rocket Skates'), :price<12.75>, :color<yellow>;
my %update = :price<15.25>, :color<red>, :year<1974>;

# Need to assign to anonymous hash to get the desired results and avoid mutating
# TIMTOWTDI
@res.push:  "\nUpdate:\n"~ join "\n", sort %=%base, %update;
# Same
@res.push:  "\nUpdate:\n"~ {%base, %update}.sort.join: "\n";

@res.push:  "\nMerge:\n"~ join "\n", sort ((%=%base).push: %update)».join: ', ';
# Same
@res.push:  "\nMerge:\n"~ ({%base}.push: %update)».join(', ').sort.join: "\n";

# Demonstrate unmutated hashes
#@res.push:  "\n", %base, "\n", %update;

.say for @res;

my $ref = q:to/END/;

Update:
color	red
name	Rocket Skates
price	15.25
year	1974

Update:
color	red
name	Rocket Skates
price	15.25
year	1974

Merge:
color	yellow, red
name	Rocket Skates
price	12.75, 15.25
year	1974

Merge:
color	yellow, red
name	Rocket Skates
price	12.75, 15.25
year	1974
END

#{color => yellow, name => Rocket Skates, price => 12.75}
#{color => red, price => 15.25, year => 1974}

use Test;
is @res.join("\n"), chomp $ref;
