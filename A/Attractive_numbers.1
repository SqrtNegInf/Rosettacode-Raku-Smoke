#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Attractive_numbers
#c# 2019-04-11 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# as of 2019-08-12 a speedier version via Perl/ntheory library

my @res;

use Prime::Factor;

sub display ($n,$m) { ($n..$m).grep: *.&prime-factors.elems.is-prime }

sub count ($n,$m) { +($n..$m).grep: *.&prime-factors.elems.is-prime }

sub comma { $^i.flip.comb(3).join(',').flip }

# The Task
@res.push: "Attractive numbers from 1 to 120:\n" ~
display(1, 120)Â».fmt("%3d").rotor(20, :partial).join: "\n";

# Robusto!
for 1, 1000,  1, 10000,  2**73 + 1, 2**73 + 100 -> $a, $b {
    @res.push: "\nCount of attractive numbers from {comma $a} to {comma $b}:\n" ~
    count $a, $b
}

.say for @res;

my $ref = q:to/END/;
Attractive numbers from 1 to 120:
  4   6   8   9  10  12  14  15  18  20  21  22  25  26  27  28  30  32  33  34
 35  38  39  42  44  45  46  48  49  50  51  52  55  57  58  62  63  65  66  68
 69  70  72  74  75  76  77  78  80  82  85  86  87  91  92  93  94  95  98  99
102 105 106 108 110 111 112 114 115 116 117 118 119 120

Count of attractive numbers from 1 to 1,000:
636

Count of attractive numbers from 1 to 10,000:
6396

Count of attractive numbers from 9,444,732,965,739,290,427,393 to 9,444,732,965,739,290,427,492:
58
END

use Test;
is @res.join("\n"), chomp $ref;
