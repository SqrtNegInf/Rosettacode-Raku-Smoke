#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Anti-primes
#c# 2018-12-06 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# OEIS:A002182
#n# 2018-12-10: confirmed that there is a race condition if .hyper is used, 
#n# manifests itself more or less easily on different OS's:  macOS 'El Capitan' always fails, 
#n# but under high CPU/memory load same seen on any macOS, and Linux, Windows

my @res;

# At its heart, this task is almost exactly the same as Proper_Divisors, it is just asking for slightly different results. Much of this code is lifted straight from there.
# Implemented as an auto-extending lazy list. Displaying the count of anti-primes less than 5e5 also because... why not.

sub propdiv (\x) {
    my @l = 1 if x > 1;
    (2 .. x.sqrt.floor).map: -> \d {
        unless x % d { @l.push: d; my \y = x div d; @l.push: y if y != d }
    }
    @l
}

my $last = 0;

#my @anti-primes = lazy 1, |(|(2..59), 60, *+60 … *).hyper.grep: -> $c {
my @anti-primes = lazy 1, |(|(2..59), 60, *+60 … *).grep: -> $c {
    my \mx = +propdiv($c);
    next if mx <= $last;
    $last = mx;
    $c
}

my $upto = 5e5;

@res.push: "First 20 anti-primes:\n{ @anti-primes[^20] }";

@res.push: "\nCount of anti-primes <= $upto: {+@anti-primes[^(@anti-primes.first: * > $upto, :k)]}";

.say for @res;

my $ref = q:to/END/;
First 20 anti-primes:
1 2 4 6 12 24 36 48 60 120 180 240 360 720 840 1260 1680 2520 5040 7560

Count of anti-primes <= 500000: 35
END

use Test;
is @res.join("\n"), chomp $ref;
