#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Arrays
#c# 2017-06-17 <RC
#m# MOAR: OK
#j#  JVM: OK

# At its most basic, an array in Perl 6 is quite similar to an array in Perl 5. 

my @arr;

push @arr, 1;
push @arr, 3;

@arr[0] = 2;

say @arr[0];

#`{
Some further exposition:
In Perl 6, arrays have a very specific definition: "A collection of Scalar containers that do the Positional Role." Scalar container means it is mutable and may contain any object; an Integer, a Rational number, a String, another Array, whatever... literally any other object that can be instantiated in Perl 6. The Positional Role means that it uses integer indexing for access. The index must be a positive integer, an expression that evaluates to a positive integer, or something that can be coerced to a positive integer. Arrays are always indexed from 0. The starting index can not be changed. 
Arrays are unconstrained by default. They may hold any number of any type of object up to available memory. They do not need to be pre-allocated. Simply assigning (or even referring in some cases) to an index slot is enough to autovivify the container and allocate enough memory to hold the assigned object. Memory will automatically be allocated and will grow and shrink as necessary to hold the values assigned.
Values may be pushed onto the end of an array, popped off of the end, shifted off of the front or unshifted onto the front.
}

my @array;

   @array.push: 'value';
   my $value = @array.pop;
   @array.unshift: 'value';
   $value = @array.shift;

# Arrays may be constrained to only accept a certain number of objects or only a certain type of object.

   my Int @array2; # can only hold Integer objects. Assigning any other type will cause an exception.
   my @array3[9];  # can only 10 objects (zero indexed). Trying to assign to an index greater than 9 with cause an exception. 

# Arrays are constructed with square brackets, an explicit constructor, or by coercing some other object either explicitly using a coercer or implicitly by simply assigning to an array variable. These are all arrays:

   my @d1 = [1, 2, 3, 4];
   my @d2 = ['a', 'b', 'c', 'd'];
   my @d3 = Array.new<this is an array of words>;
   my @d4 = ('as', 'is', 'this').Array;
   my @implicit = <yep, this too>;

# Array variables in Perl 6 are variables whose names bear the @ sigil, and are expected to contain some sort of list-like object. Of course, other variables may also contain these objects, but @-sigiled variables always do, and are expected to act the part. Array storage slots are accessed through postcircumfix square bracket notation. Unlike Perl 5, @-sigiled variables are invariant on access, whether you are accessing one slot, many slots, or all of the slots. The first slot in @array is @array[0] not $array[0]. @array and $array are two different unconnected variables. 

   my @ar = 1..20;
   @ar[1];     # a single value in the 2nd slot
   @ar[*-1];   # a single value in the last slot
   @ar[1..5];  # an array slice, 2nd through 6th slots
   @ar[1,3,7]; # slice, 2nd, 4th and 8th slot
   @ar[8,5,2]; # can be in any order
   @ar[*];     # all the slots
   @ar[];      # all the slots (zen slice)
   @ar[^10];   # first 10 slots (upto 10 or 0..9)
   @ar.head(5);# first 5 slots
   @ar.tail(2);# last two

# Multi-dimensioned arrays also use postcircumfix square brackets for access. If the array is not ragged, (every sub array is the same size) you may use semicolon subscripting.

   @array[1][1];  # 2nd item in the second slot
   @array[1;1];   # same thing, implies rectangular (non-ragged) arrays

# There are several constructs that do the Positional Role similar to arrays. They are ordered and use integer indexing and are often used in similar circumstances, however, they are immutable. Values in slots can not be changed. They can not be pushed to or popped from.

# List: A fixed collection of immutable values. Lists are constructed similarly to arrays, but
# note that the values are bound ':=' not assigned '='.

   my @b1 := 1, 2, 3, 4;
   my @b2 := 'a', 'b', 'c', 'd';
   my @b3 := <this is a list of words>;
#   @implicit := (<this is too>); 

# Range: Iterable list of consecutive numbers or strings with a lower and an upper boundary. (That boundary may be infinite.) Reified on demand. 

   my @c1 := 2..20;    # integers two through twenty
   my @c2 := 1..Inf;   # natural numbers
   my @c3 := 'a'..'z'; # lowercase latin letters
#  my @c4 := '⁰'..'⁹';  # superscript digits      
# DH @c4 has wrong contents?
# should be: '⁰¹²³⁴⁵⁶⁷⁸⁹'
# instead:   ⁰:ⁱ:⁲:⁳:⁴:⁵:⁶:⁷:⁸:⁹  # when joined by ':'
# ⁰ : 8304 : SUPERSCRIPT ZERO
# ⁱ : 8305 : SUPERSCRIPT LATIN SMALL LETTER I
# ⁲ : 8306 : <reserved-2072>
# ⁳ : 8307 : <reserved-2073>
# ⁴ : 8308 : SUPERSCRIPT FOUR

exit;

# Sequence: Iterable list of objects with some method to determine the next (or previous) item in the list. Reified on demand. Will try to automatically deduce simple arithmetic or geometric sequences. Pass in a code object to calculate more complex sequences.

  my @a1 = 0,2,4 ... 64;   # even numbers up to 64
  my @a2 = 1,2,4 ... 64;   # geometric increase
  my @a3 = 1,1, *+* ... *; # infinite Fibonacci sequence
  my @a4 = 1,1,{$^n2 + $^n1 + 1} ... *; # infinite Leonardo numbers

# Postcircumfix indexing works for any object that has a Positional role, it need not be in a @-sigiled variable, or indeed, in a variable at all.

   [2,4,6,8,10][1];                            # 4 - anonymous array
   <my dog has fleas>[*-2];                    # 'has' - anonymous list
   sub a {(^Inf).grep: *.is-prime}; a()[99];   # 541 - (100th prime) subroutine returning a sequence
   my $lol = ((1,2), (3,4), (5,6)); $lol[1];   # (3 4) - list of lists in a Scalar variable

use Test;
is a()[99], 541;
is @a4[^10], '1 1 3 5 9 15 25 41 67 109';
