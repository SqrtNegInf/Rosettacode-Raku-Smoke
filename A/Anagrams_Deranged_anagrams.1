#!/usr/bin/env perl6
#n# NOT CURRENT, NOT SMOKE-TESTED
#u# http://rosettacode.org/wiki/Anagrams/Deranged_anagrams
#c# 2016-07-30 <RC
#m# MOAR: OK
#j#  JVM: OK

# Works with: Rakudo version 2016.07

my @anagrams = 'ref/unixdict.txt'.IO.words
    .map(*.comb.cache)                # explode words into lists of characters
    .classify({ .sort.join }).values  # group words with the same characters
    .grep(* > 1)                      # only take groups with more than one word
    .sort(-*.[0])                     # sort by length of the first word
;

for @anagrams -> @group {
    for @group.combinations(2) -> [@a, @b] {
        if none @a Zeq @b {
            say "{@a.join}   {@b.join}";
            exit;
        }
    }
}

# constitutionalism   misconstitutional

# excitation   intoxicate

# TODO: Replace { .sort.join } with *.sort.join once this bugfix is shipped in the next Rakudo release.
