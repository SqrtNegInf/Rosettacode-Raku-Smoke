#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Averages/Mean_angle
#c# 2015-11-17 <RC
#m# MOAR: OK
#j#  JVM: OK

#This solution refuses to return an answer when the angles cancel out to a tiny magnitude.

sub deg2rad { $^d × pi / 180 }
sub rad2deg { $^r × 180 / pi }
sub phase ($c)  {
    my ($mag,$ang) = $c.polar;
    return NaN if $mag < 1e-16;
    $ang;
}

sub meanAngle { rad2deg phase [+] map { cis deg2rad $_ }, @^angles }

say meanAngle($_).fmt("%.2f\tis the mean angle of "), $_ for
    [350, 10],
    [90, 180, 270, 360],
    [10, 20, 30];

use Test;
is-approx (meanAngle([350,10]).round(.2)), 0, 'Very close to zero';
is        (meanAngle([90,180,270,360])), 'NaN', 'NaN!';
is-approx (meanAngle([10,20,30]).round(.2)), 20, 'That is more like it';
