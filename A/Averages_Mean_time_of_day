#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Averages/Mean_time_of_day
#c# 2017-09-22 <RC
#m# MOAR: OK
#j# JVM:  OK

# Works with: Rakudo version 2015.12

sub tod2rad($_) { [+](.comb(/\d+/) Z× 3600,60,1) × τ / 86400 }

sub rad2tod ($r) {
    my $x = $r × 86400 / τ;
    (($x xx 3 Z/ 3600,60,1) Z% 24,60,60).fmt('%02d',':');
}

sub phase ($c) { $c.polar[1] }

sub mean-time (@t) { rad2tod phase [+] map { cis tod2rad $_ }, @t }

my @times = ["23:00:17", "23:40:20", "00:12:45", "00:17:19"];

say my $result = "{ mean-time(@times) } is the mean time of @times[]";

my $ref = qq:to/END/;
23:47:43 is the mean time of 23:00:17 23:40:20 00:12:45 00:17:19
END

use Test;
is $result, chomp $ref;
