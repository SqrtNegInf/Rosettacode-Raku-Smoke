#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/ADFGVX_cipher
#c# 2021-08-16 <RC
#m# MOAR: OK
#j# JVM:  BROKEN

srand 123456;

my @res;

use Terminal::Boxer;

my @header   = < A D F G V X >;
my $polybius = (flat 'A'..'Z', 0..9).pick(*).join;

my $key-length = 9;
my $key = uc 'ref/unixdict.txt'.IO.words.grep( { (.chars == $key-length) && (+.comb.Set == +.comb) } ).roll;

my %cypher   = (@header X~ @header) Z=> $polybius.comb;

my $message = 'Attack at 1200AM';

@res.push: "Key: $key\n";
@res.push: "Polybius square:\n", ss-box :7col, :3cw, :indent("\t"), '', |@header, |(@header Z $polybius.comb.batch: 6);
@res.push: "Message to encode: $message";
@res.push: "\nEncoded: " ~ my $encoded = encode $message;
@res.push: "\nDecoded: " ~ decode $encoded;

sub encode ($text is copy) {
    $text = $text.uc.comb(/<[A..Z 0..9]>/).join;
    my @order = $key.comb.pairs.sort( *.value )».key;
    my @encode = %cypher.invert.hash{ $text.comb }.join.comb.batch($key-length).map: { [$_] };
    ((^$key-length).map: { @encode».[@order]».grep( *.defined )».[$_].grep( *.defined ).join }).Str;
}

sub decode ($text is copy) {
    my @text = $text.split(' ')».comb;
    my $chars = @text[0].chars;
    $_ = flat |$_, ' ' if .chars < $chars for @text;
    my @order = $key.comb.pairs.sort( *.value )».key.pairs.sort( *.value )».key;
    %cypher{ ( grep { /\w/ }, flat [Z] @order.map( { |@text.batch($key-length)».[$_] } ) ).batch(2)».join }.join;
}

.say for @res;

my $ref = q:to/END/;
Key: ANTIPODES

Polybius square:

	┌───┬───┬───┬───┬───┬───┬───┐
	│   │ A │ D │ F │ G │ V │ X │
	├───┼───┼───┼───┼───┼───┼───┤
	│ A │ I │ 6 │ H │ 7 │ C │ Y │
	├───┼───┼───┼───┼───┼───┼───┤
	│ D │ 2 │ Z │ 4 │ O │ 0 │ R │
	├───┼───┼───┼───┼───┼───┼───┤
	│ F │ S │ J │ T │ 9 │ W │ Q │
	├───┼───┼───┼───┼───┼───┼───┤
	│ G │ D │ K │ 1 │ G │ 8 │ M │
	├───┼───┼───┼───┼───┼───┼───┤
	│ V │ N │ F │ U │ 5 │ X │ V │
	├───┼───┼───┼───┼───┼───┼───┤
	│ X │ A │ E │ 3 │ B │ P │ L │
	└───┴───┴───┴───┴───┴───┴───┘

Message to encode: Attack at 1200AM

Encoded: XVDX XFX AGA FXV AGA FFV FAD AFG FDD

Decoded: ATTACKAT1200AM
END

use Test;
is @res.join("\n"), chomp $ref;
