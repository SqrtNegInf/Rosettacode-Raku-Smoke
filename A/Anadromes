#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Anadromes
#c# 2022-06-23 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

my @words = 'ref/words.txt'.IO.slurp.words.grep: *.chars > 6;

my %words = @words.pairs.invert;

@words.map: { %words{$_}:delete and @res.push: sprintf "%10s ↔ %s", $_, .flip if ($_ ne .flip) && %words{.flip} }

.say for @res;

my $ref = q:to/END/;
   amaroid ↔ diorama
   degener ↔ reneged
   deifier ↔ reified
   deliver ↔ reviled
   dessert ↔ tressed
  desserts ↔ stressed
   deviler ↔ relived
  dioramas ↔ samaroid
   gateman ↔ nametag
   leveler ↔ relevel
   pat-pat ↔ tap-tap
  redrawer ↔ rewarder
   reknits ↔ stinker
   relever ↔ reveler
   reliver ↔ reviler
   revotes ↔ setover
   sallets ↔ stellas
END

use Test;
is @res.join("\n"), chomp $ref;
