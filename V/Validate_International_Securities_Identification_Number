#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Validate_International_Securities_Identification_Number
#c# 2016-08-07 <RC, 2017-02-08 <RC, 2019-02-26 >RC
#m# MOAR: OK
#j#  JVM: OK
#n# migrated from 'Calculate_International_Securities_Identification_Number'

# Using the luhn-test function defined at Luhn test of credit card numbers
sub luhn-test ($number --> Bool) {
    my @digits = $number.comb.reverse;
    my $sum = @digits[0,2...*].sum
            + @digits[1,3...*].map({ |($_ Ã— 2).comb }).sum;
    return $sum %% 10;
}

my $ISIN = /
    ^ <[A..Z]>**2 <[A..Z0..9]>**9 <[0..9]> $
    <?{ luhn-test $/.comb.map({ :36($_) }).join }>
/;

my @res.push: "$_ is { m/$ISIN/ ?? "valid" !! "not valid"}" for <
US0378331005
US0373831005
U50378331005
US03378331005
AU0000XVGZA3
AU0000VXGZA3
FR0000988040
>;

my $ref = qq:to/END/;
US0378331005 is valid
US0373831005 is not valid
U50378331005 is not valid
US03378331005 is not valid
AU0000XVGZA3 is valid
AU0000VXGZA3 is valid
FR0000988040 is valid
END

use Test;
is @res.join("\n"), chomp $ref;
