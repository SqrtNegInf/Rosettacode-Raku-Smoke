#!/usr/bin/env perl6
#u# http://www.rosettacode.org/wiki/Rosetta_Code/Rank_languages_by_popularity
#t# network
#c# 2016-05-21 <RC, 2018-10-28 >RC
#m# MOAR: BROKEN
#j#  JVM: BROKEN
#n# after RC site upgrade, the source files no longer present, use -1 version only

# both out-of-date
my $languages =  qx{curl -s -o - 'http://rosettacode.org/wiki/Category:Programming_Languages'};
my $categories = qx{curl -s -o - 'http://www.rosettacode.org/w/index.php?title=Special:Categories&limit=5000'};

my @lines = $languages.lines;
shift @lines until @lines[0] ~~ / '<h2>Subcategories</h2>' /;
my \languages = set gather for @lines {
    last if / '/bodycontent' /;
    take ~$0 if
        / '<li><a href="/wiki/Category:' .*? '" title="Category:' .*? '">' (.*?) '</a></li>' /;
}

@lines = $categories.lines;
my @freq = sort -*.[0], gather for @lines {
    take [+$1.subst(',', ''), ~$0] if
        / '<li><a href="/wiki/Category:' .*? '" title="Category:' .*? '">'
        (.*?) <?{ ~$0 ∈ languages }>
        '</a>' .*? '(' (<[, 0..9]>+) ' member' /;
}

my @res;
for @freq.kv -> $i, @l {
    @res.push: sprintf '%d:\t%4d - %s', $i+1, |@l;
    last if 20 == $i;
}
.say for @res;

say 'freq: ' ~ +@freq;

use Test;
ok 740 < +@freq;
@res.join("\n") ~~ m/(\d+) ' - Raku'/;
ok 1219 ≤ +$0;
