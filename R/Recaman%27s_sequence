#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Recaman%27s_sequence
#c# 2018-08-06 <RC
#m# MOAR: OK
#j# JVM:  OK

# Works with: Rakudo version 2018.06

my @recamans = 0, {
   state %seen;
   state $term;
   $term++;
   my $this = $^previous - $term;
   $this = $previous + $term unless ($this > 0) && !%seen{$this};
   %seen{$this} = True;
   $this
} â€¦ *;

my @res.push: "First fifteen terms of Recaman's sequence: "~ @recamans[^15];

@res.push: "First duplicate at term: a[{ @recamans.first({@recamans[^$_].Bag.values.max == 2})-1 }]";

my @seen;
my int $i = 0;
loop {
    next if (my int $this = @recamans[$i++]) > 1000 or @seen[$this];
    @seen[$this] = 1;
    @res.push: "Range 0..1000 covered by terms up to a[{$i - 1}]" and last if ++$ == 1001;
}

.say for @res;

my $ref = qq:to/END/;
First fifteen terms of Recaman's sequence: 0 1 3 6 2 7 13 20 12 21 11 22 10 23 9
First duplicate at term: a[24]
Range 0..1000 covered by terms up to a[328002]
END

use Test;
is @res.join("\n"), chomp $ref;
