#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Rosetta_Code/Rank_languages_by_number_of_users
#t# network
#c# 2018-05-19 <RC
#m# MOAR: OK
#j# JVM:  BROKEN

BEGIN { die 'ecosystem...' if $*VM ~~ /jvm/; }  # wait for zef-j

use HTTP::UserAgent;
use URI::Escape;
use JSON::Fast;

my $client = HTTP::UserAgent.new;

my $url = 'http://rosettacode.org/mw';

my $start-time = now;

say "========= Generated: { DateTime.new(time) } =========";

my $lang = 1;
my $rank = 0;
my $last = 0;
my $tie = ' ';
my $minimum = 25;

my @res.push: $_ for
    mediawiki-query(
        $url, 'pages',
        :generator<categorymembers>,
        :gcmtitle<Category:Language users>,
        :gcmlimit<350>,
        :rawcontinue(),
        :prop<categoryinfo>
    )

    .map({ %( count => .<categoryinfo><pages> || 0,
              lang  => .<title>.subst(/^'Category:' (.+) ' User'/, -> $/ {$0}) ) })

    .sort( { -.<count>, .<lang> } )

    .map( { last if .<count> < $minimum; display(.<count>, .<lang>) } );
.say for @res;

say "========= elapsed: {(now - $start-time).round(.01)} seconds =========";

sub display ($count, $which) {
    if $last != $count { $last = $count; $rank = $lang; $tie = ' ' } else { $tie = 'T' };
    sprintf "#%3d  Rank: %2d %s  with %-4s users:  %s", $lang++, $rank, $tie, $count, $which;
}

sub mediawiki-query ($site, $type, *%query) {
    my $url = "$site/api.php?" ~ uri-query-string(
        :action<query>, :format<json>, :formatversion<2>, |%query);
    my $continue = '';

    gather loop {
        my $response = $client.get("$url&$continue");
        my $data = from-json($response.content);
        take $_ for $data.<query>.{$type}.values;
        $continue = uri-query-string |($data.<query-continue>{*}».hash.hash or last);
    }
}

sub uri-query-string (*%fields) {
    join '&', %fields.map: { "{.key}={uri-escape .value}" }
}

use Test;
@res.join("\n") ~~ m/'with ' (\d+) \s+ 'users:  Raku'/;
ok 26 ≤ +$0;  # this value may go up or down... [down, since I moved my listing to 'Raku']
