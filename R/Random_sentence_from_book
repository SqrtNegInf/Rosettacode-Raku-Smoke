#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Random_sentence_from_book
#c# 2021-09-06 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# 2022-12-22 replace call to built-in 'rand' for output stability

my @res;

use myRNG;
my $rng = myRNG.new( :seed(123456) );

my $text = 'ref/waroftheworlds.txt'.IO.slurp.subst(/.+ '*** START OF THIS' .+? \n (.*?) 'End of the Project Gutenberg EBook' .*/, {$0} );

$text.=subst(/ <+punct-[.!?\’,]> /, ' ', :g);
$text.=subst(/ (\s) '’' (\s)  /, '', :g);
$text.=subst(/ (\w) '’' (\s)  /, {$0~$1}, :g);
$text.=subst(/ (\s) '’' (\w)  /, {$0~$1}, :g);

my (%one, %two);

for $text.comb(/[\w+(\’\w+)?]','?|<punct>/).rotor(3 => -2) {
    %two{.[0]}{.[1]}{.[2]}++;
    %one{.[0]}{.[1]}++;
}

sub weightedpick (%hash) { my @foo = %hash.keys.map( { $_ xx %hash{$_} } ).sort; @foo[@foo.elems × $rng.next-rat] }
#sub weightedpick (%hash) { %hash.keys.map( { $_ xx %hash{$_} } ).&pickall.head } # doesn't actually shuffle?
#sub weightedpick (%hash) { %hash.keys.map( { $_ xx %hash{$_} } ).pick }  # and could just as well be .roll

sub sentence {
    my @sentence = <. ! ?>[3 × $rng.next-rat];
   #my @sentence = <. ! ?>.roll;  # which is not really different than .pick here
    @sentence.push: weightedpick( %one{ @sentence[0] } );
    @sentence.push: weightedpick( %two{ @sentence[*-2] }{ @sentence[*-1] } // %('.' => 1) )[0]
      until @sentence[*-1] ∈ <. ! ?>;
    @sentence.=squish;
    shift @sentence;
    redo if @sentence < 7;
    @sentence.join(' ').tc.subst(/\s(<:punct>)/, {$0}, :g);
}

@res.push: sentence() ~ "\n" for ^4; #^10;

.say for @res;

my $ref = q:to/END/;
Were the powder from the country forthwith.

Forty millions of miles of void.

Cried a boatman, and Shut up, you fool!

What’s going on as it began, leaving the evening papers had contained only a reprieve.

END

use Test;
is @res.join("\n"), chomp $ref;
