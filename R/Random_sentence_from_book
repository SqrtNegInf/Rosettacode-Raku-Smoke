#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Random_sentence_from_book
#c# 2021-09-06 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# for MOAR: 'sort' trick not working, 'srand' bug bites?

srand 123456;

my @res;

my $text = 'ref/waroftheworlds.txt'.IO.slurp.subst(/.+ '*** START OF THIS' .+? \n (.*?) 'End of the Project Gutenberg EBook' .*/, {$0} );

$text.=subst(/ <+punct-[.!?\’,]> /, ' ', :g);
$text.=subst(/ (\s) '’' (\s)  /, '', :g);
$text.=subst(/ (\w) '’' (\s)  /, {$0~$1}, :g);
$text.=subst(/ (\s) '’' (\w)  /, {$0~$1}, :g);

my (%one, %two);

for $text.comb(/[\w+(\’\w+)?]','?|<punct>/).rotor(3 => -2) {
    %two{.[0]}{.[1]}{.[2]}++;
    %one{.[0]}{.[1]}++;
}

sub weightedpick (%hash) { %hash.keys.map( { $_ xx %hash{$_} } ).sort.pick }
#sub weightedpick (%hash) { %hash.keys.map( { $_ xx %hash{$_} } ).pick }

sub sentence {
    my @sentence = <. ! ?>.roll;
    @sentence.push: weightedpick( %one{ @sentence[0] } );
    @sentence.push: weightedpick( %two{ @sentence[*-2] }{ @sentence[*-1] } // %('.' => 1) )[0]
      until @sentence[*-1] ∈ <. ! ?>;
    @sentence.=squish;
    shift @sentence;
    redo if @sentence < 7;
    @sentence.join(' ').tc.subst(/\s(<:punct>)/, {$0}, :g);
}

@res.push: sentence() ~ "\n" for ^4; #^10;

.say for @res;

my $moar = q:to/END/;
Are we such apostles of mercy as to complain if the nebular hypothesis has any truth, older than our world and long before this earth ceased to be quite silent and abandoned as I went down again in the Londoner’s mind, and startling intelligence so much as they could read without any provocation, as a seated figure in soot smudged shirt sleeves, and with an extraordinary pitch, but nothing to tell me of the whole of London and the thunder burst like a poisoned dart, was this cylinder.

No, sir, and the machine to the waterline.

Five of the interruption of telegraphic communication.

Or did they give the artillery man.

END

my $jvm = q:to/END/;
His voice and face were eager.

Man has bought his birthright of the Thunder Child could be felt.

Or did they give the artillery the ghost of a battering ram might have thought a house.

His voice and face were eager.

END

use Test;
my $ref = $*VM ~~ /jvm/ ?? $jvm !! $moar;
is @res.join("\n"), chomp $ref;
