#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Rosetta_Code/Find_unimplemented_tasks
#c# 2018-04-30 <RC, 2020-07-19 >RC
#m# MOAR: OK
#j# JVM:  BROKEN

BEGIN { die 'ecosystem...' if $*VM ~~ /jvm/; }  # wait for zef-j

# Works with: Rakudo version 2018.04.1
use HTTP::UserAgent;
use URI::Escape;
use JSON::Fast;
use Sort::Naturally;

unit sub MAIN( Str :$lang = 'Raku' );

my $client = HTTP::UserAgent.new;
my $url = 'http://rosettacode.org/mw';

my @total;
my @impl;

@total.append: .&get-cat for 'Programming_Tasks', 'Draft_Programming_Tasks';
@impl = get-cat $lang;

say "Unimplemented tasks in $lang:";
.say for (@total (-) @impl).keys.sort: &naturally;

sub get-cat ($category) {
    flat mediawiki-query(
        $url, 'pages',
        :generator<categorymembers>,
        :gcmtitle("Category:$category"),
        :gcmlimit<350>,
        :rawcontinue(),
        :prop<title>
    ).map({ .<title> });
}

sub mediawiki-query ($site, $type, *%query) {
    my $url = "$site/api.php?" ~ uri-query-string(
        :action<query>, :format<json>, :formatversion<2>, |%query);
    my $continue = '';

    gather loop {
        my $response = $client.get("$url&$continue");
        my $data = from-json($response.content);
        take $_ for $data.<query>.{$type}.values;
        $continue = uri-query-string |($data.<query-continue>{*}Â».hash.hash or last);
    }
}

sub uri-query-string (*%fields) { %fields.map({ "{.key}={uri-escape .value}" }).join("&") }

use Test;
ok 1000 <  +@total;
ok 950  <= +@impl;
ok  grep /'Tamagotchi emulator'/, @total and grep /' Write to Windows event log'/, @total;
nok grep / 'Tamagotchi emulator' | ' Write to Windows event log' /, @impl;
