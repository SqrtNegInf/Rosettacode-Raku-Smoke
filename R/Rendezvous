#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Rendezvous
#c# 2016-08-22 <RC
#m# MOAR: OK
#j#  JVM: OK
#f# RC file: rendezvous.txt
#n# is there actually anything that 'srand' can control?

#srand 123456;

my $fh = open('run/rendezvous.txt', :w);

class X::OutOfInk is Exception {
    method message() { "Printer out of ink" }
}

class Printer {
    has Str      $.id;
    has Int      $.ink = 5;
    has Lock     $!lock .= new;
    has ::?CLASS $.fallback;

    method printz ($line) {
        $!lock.protect: {
            if    $!ink      { $fh.say: "$!id: $line"; $!ink-- }
            elsif $!fallback { $!fallback.printz: $line }
            else             { die X::OutOfInk.new }
        }
    }
}

my $printer =
    Printer.new: id => 'main', fallback =>
    Printer.new: id => 'reserve';

sub client ($id, @lines) {
    start {
        for @lines {
            $printer.printz: $_;
            CATCH {
                #when X::OutOfInk { note "<$id stops for lack of ink>"; exit }
                when X::OutOfInk { $fh.say: "<$id stops for lack of ink>"; exit }
            }
        }
        #note "<$id is done>";
        $fh.say: "<$id is done>";
    }
}


await
    client('Humpty', q:to/END/.lines),
        Humpty Dumpty sat on a wall.
        Humpty Dumpty had a great fall.
        All the king's horses and all the king's men,
        Couldn't put Humpty together again.
        END
    client('Goose', q:to/END/.lines);
        Old Mother Goose,
        When she wanted to wander,
        Would ride through the air,
        On a very fine gander.
        Jack's mother came in,
        And caught the goose soon,
        And mounting its back,
        Flew up to the moon.
        END
