#!/usr/bin/env perl6
#n# NOT CURRENT, NOT SMOKE-TESTED
#u# http://rosettacode.org/wiki/Langton's_ant#Perl_6 
#c# 2016-01-19 <RC
#m# MOAR: OK
#j#  JVM: OK

srand 123456;

my @res;

# In this version we use 4-bits-per-char graphics to shrink the output to a quarter the area of ASCII graphics.

constant @vecs = [1,0,1], [0,-1,1], [-1,0,1], [0,1,1];
constant @blocky = ' ▘▝▀▖▌▞▛▗▚▐▜▄▙▟█'.comb;
constant $size = 100;
enum Square <White Black>;
my @plane = [White xx $size] xx $size;
my ($x, $y) = $size/2, $size/2;
my $dir = @vecs.keys.pick;
my $moves = 0;
loop {
    given @plane[$x][$y] {
        when :!defined { last }
        when White { $dir--; $_ = Black; }
        when Black { $dir++; $_ = White; }
    }
    ($x,$y,$moves) »+=« @vecs[$dir %= @vecs];
}
@res.push: "Out of bounds after $moves moves at ($x, $y)";
for 0,2,4 ... $size - 2 -> $y {
    @res.push: join '', gather for 0,2,4 ... $size - 2 -> $x {
        take @blocky[ 1 × @plane[$x][$y]
                    + 2 × @plane[$x][$y+1]
                    + 4 × @plane[$x+1][$y]
                    + 8 × @plane[$x+1][$y+1] ];
    }
}

.say for @res;

my $ref = qq:to/END/;
Out of bounds after 11655 moves at (100, 27)











                                               ▗█▝
                                              ▗▀█▟
                                             ▗▀▚▘▜
           ▗▝  ▙▝                           ▗▀▚▖▚▐
          ▗▜▖▐▀▛▄                          ▗▀▚▖▙▐▘
          ▖▖▌▘▄▀▘                         ▗▀▚▖▙▐▘ 
           ▄▜▛▄▚▝▝                       ▗▀▚▖▙▐▘  
           ▖▛▙▄▚▛▀▙▝ ▙▐▝▙▝              ▗▀▚▖▙▐▘   
            ▄█▚▌▚▌▌▐ █▌ ▌▄ ▗▝          ▗▀▚▖▙▐▘    
             ▛█▖▀▗▌▗▛▐▘  ▄ ▚▖▝        ▗▀▚▖▙▐▘     
             ▜▌█ ▛▄▞▞▛█▙▀▘▗▄▗▚▝▗▝    ▗▀▚▖▙▐▘      
             ▘▜▄▐▀▟▖▗ ▜▐▘▙ ▞▘▟▖▙▛▝  ▗▀▚▖▙▐▘       
            ▛▗▖▛█▌▛▚ ▙▄▌█▗▜█▙▝▞  ▀ ▗▀▚▖▙▐▘        
           ▗▘▗▛ ▐▀█▝▘▚▗▗▛▛▜▚██▀▝▗▘▗▀▚▖▙▐▘         
           ▖▟▌▀▖▚▀▄ ▟█▟█▞▚▛▗▟▀▀▖▘▗▀▚▖▙▐▘          
           ▗▘ ▖▝ ▜▟▙▜▞▖▌▌█▌▙▗▛▗▟▗▀▚▖▙▐▘           
           ▖▝▖▐▗▜▀▚▐▛▐▐▝▙▞▄▞▛▟▗▘▘▚▖▙▐▘            
            ▖▚▖ ▚▌▟▛▐▗ ▜▄█▞▝▀▗▄▘▚▖▙▐▘             
             ▀▐▌█▄▜▖▗ ▘▄▛▗▖▜▛▐▄▚▖▙▐▘              
             ▖▜▗ ▘█▐ ▞▀▖▝▖▜▛▐▗ ▗▄ ▘               
              ▗▌▄▙▟▙▙▐▟▞▜▙▞▚▜▀▛▖▟▙▟               
              ▖▟▞▖█▝▖ ▛▛▐▀▚▛▛▚▝▜▝▝▘               
               ▖▘▄▜▘▟▄▝▖▘▛▌▖▀▖▛▗▚▜                
                 ▖▗  ▞▙    ▄▖▞▀▖▘                 
                  ▖▟▘▝    ▄▜▝▙▘                   
                   ▖▘▖▌▌▌▌▌▘▖▘                    













END

use Test;
#is @res.join("\n"), chomp $ref;
