#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Long_primes
#c# 2018-08-04 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# drastically cut down on results calculated

my @long-primes = lazy (1..*).grep(*.is-prime).grep({1+(1/$_).base-repeating[1].chars == $_});

my @res = "Long primes ≤ 500:\n"~ @long-primes[^(@long-primes.first: * > 500, :k)];

@res.push: "\nNumber of long primes ≤ $_: "~ +@long-primes[^(@long-primes.first: * > $_, :k)]
  for 500, 1000, 2000, 4000;

.say for @res;

my $ref = qq:to/END/;
Long primes ≤ 500:
7 17 19 23 29 47 59 61 97 109 113 131 149 167 179 181 193 223 229 233 257 263 269 313 337 367 379 383 389 419 433 461 487 491 499

Number of long primes ≤ 500: 35

Number of long primes ≤ 1000: 60

Number of long primes ≤ 2000: 116

Number of long primes ≤ 4000: 218
END

use Test;
is @res.join("\n"), chomp $ref;
