#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Conjugate_transpose
#c# 2016-01-04 <RC
#m# MOAR: OK
#j#  JVM: OK
#f# RC file: conjugate_transpose.txt

# Works with: Rakudo version 2015-12-13
my $out = open('run/conjugate_transpose.txt', :w);

for [ # Test Matrices
       [   1, 1+i, 2i],
       [ 1-i,   5, -3],
       [0-2i,  -3,  0]
    ],
    [
       [1, 1, 0],
       [0, 1, 1],
       [1, 0, 1]
    ],
    [
       [0.707 ,    0.707,  0],
       [0.707i, 0-0.707i,  0],
       [0     ,        0,  i]
    ]
    -> @m {
        $out.say: "\nMatrix:";
        @m.&say-it;
        my @t = @m>>.conj.&mat-trans;
        $out.say: "\nTranspose:";
        @t.&say-it;
        $out.say: "Is Hermitian?\t{is-Hermitian(@m, @t)}";
        $out.say: "Is Normal?\t{is-Normal(@m, @t)}";
        $out.say: "Is Unitary?\t{is-Unitary(@m, @t)}";
    }

say qq{See 'conjugate_transpose.txt' for output};

$out.close;

sub is-Hermitian (@m, @t, --> Bool) {
    so @m>>.Complex eqv @t>>.Complex
 }

sub is-Normal (@m, @t, --> Bool) {
    so mat-mult(@m, @t, 1e-15)>>.Complex eqv mat-mult(@t, @m, 1e-15)>>.Complex
}

sub is-Unitary (@m, @t, --> Bool) {
    so mat-mult(@m, @t, 1e-3)>>.Complex eqv mat-ident(+@m)>>.Complex;
}

sub mat-trans (@m) { map { [ @m[*;$_] ] }, ^@m[0] }

sub mat-ident ($n) { [ map { [ flat 0 xx $_, 1, 0 xx $n - 1 - $_ ] }, ^$n ] }

sub mat-mult (@a, @b, $e) {
    my @p;
    for ^@a X ^@b[0] -> ($r, $c) {
        @p[$r][$c] += @a[$r][$_] Ã— @b[$_][$c] for ^@b;
        @p[$r][$c] .= round($e); # avoid floating point math errors
    }
    @p
}

sub say-it (@array) { ($out.say: $_>>.fmt("%9s")) for @array }

#`{
Matrix:
[        1      1+1i      0+2i]
[     1-1i         5        -3]
[     0-2i        -3         0]

Transpose:
[        1      1+1i      0+2i]
[     1-1i         5        -3]
[     0-2i        -3         0]
Is Hermitian?	True
Is Normal?	True
Is Unitary?	False

Matrix:
[        1         1         0]
[        0         1         1]
[        1         0         1]

Transpose:
[        1         0         1]
[        1         1         0]
[        0         1         1]
Is Hermitian?	False
Is Normal?	True
Is Unitary?	False

Matrix:
[    0.707     0.707         0]
[ 0+0.707i  0-0.707i         0]
[        0         0      0+1i]

Transpose:
[    0.707  0-0.707i         0]
[    0.707  0+0.707i         0]
[        0         0      0-1i]
Is Hermitian?	False
Is Normal?	True
Is Unitary?	True
}
