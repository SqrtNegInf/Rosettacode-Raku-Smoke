#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Combinations
#c# 2015-10-12 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# with JVM 2021.12, now works

sub combinations(Int $n, Int $k) {
    return ([],) unless $k;
    return if $k > $n || $n <= 0;
    my @c = ^$k;
    gather loop {
      take [@c];
      next if @c[$k-1]++ < $n-1;
      my $i = $k-2;
      $i-- while $i >= 0 && @c[$i] >= $n-($k-$i);
      last if $i < 0;
      @c[$i]++;
      while ++$i < $k { @c[$i] = @c[$i-1] + 1; }
    }
}
.say for combinations(5,3);

use Test;
my @ref = (
<0 1 2>,
<0 1 3>,
<0 1 4>,
<0 2 3>,
<0 2 4>,
<0 3 4>,
<1 2 3>,
<1 2 4>,
<1 3 4>,
<2 3 4>
);
is combinations(5,3), @ref, '(5,3) yields 10 combinations.';
