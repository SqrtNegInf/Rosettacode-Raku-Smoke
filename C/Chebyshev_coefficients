#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Chebyshev_coefficients
#c# 2016-03-07 <RC
#m# MOAR: OK
#j#  JVM: OK
#n# 2022-06-04 replace '0.5' with '1/2' or '½' works, other cosmetics (not on RC)
#n#            using 'X' a little faster, but precedence issue here: ((@pi_n».cos X× bma) X+ bpa )».&$func

sub chebft ( Code $func, Real \a, Real \b, Int \n ) {

    my \bma = ½ × ( b - a );
    my \bpa = ½ × ( b + a );

    my @pi_n = ( (^n).list »+» ½ ) »×» ( π / n );
    my @f    = ( @pi_n».cos »×» bma »+» bpa )».&$func;        # '&' not required, but a little clearer?
    my @sums = map { [+] @f »×« ( @pi_n »×» $_ )».cos }, ^n;

    return @sums »×» ( 2 / n );
}

my @res.push: .fmt('%+13.7e') for chebft &cos, 0, 1, 10;
#my @res = (.fmt('%+13.7e') for chebft &cos, 0, 1, 10);   # equiv to above

.say for @res;

my $moar = qq:to/END/;
+1.6471695e+00
-2.3229937e-01
-5.3715115e-02
+2.4582353e-03
+2.8211906e-04
-7.7222292e-06
-5.8985565e-07
+1.1521427e-08
+6.5962992e-10
-1.0021994e-11
END

my $jvm = qq:to/END/;
+1.6471695e+00
-2.3229937e-01
-5.3715115e-02
+2.4582353e-03
+2.8211906e-04
-7.7222292e-06
-5.8985565e-07
+1.1521428e-08
+6.5962991e-10
-1.0022017e-11
END

if ($*VM ~~ /moar/ and $*KERNEL eq 'linux') {
$moar = qq:to/END/;
+1.6471695e+00
-2.3229937e-01
-5.3715115e-02
+2.4582353e-03
+2.8211906e-04
-7.7222292e-06
-5.8985565e-07
+1.1521428e-08
+6.5962991e-10
-1.0022017e-11
END
}

use Test;
is @res.join("\n"), chomp $*VM ~~ /jvm/ ?? $jvm !! $moar;
