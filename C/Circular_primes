#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Circular_primes
#c# 2020-04-06 <RC, 2020-04-07 <>RC
#m# MOAR: OK
#j# JVM:  BROKEN

my @res;

use ntheory:from<Perl5> qw[is_prime];

sub isCircular(\n) {
   return False unless n.is-prime;
   my @circular = n.comb;
   return False if @circular.min < @circular[0];
   for 1 ..^ @circular -> $i {
      return False unless .is-prime and $_ >= n given @circular.rotate($i).join;
   }
   True
}

@res.push: "The first 19 circular primes are:";
@res.push: ((2..*).hyper.grep: { isCircular $_ })[^19];

@res.push: "\nThe next 4 circular primes, in repunit format, are:";
loop ( my $i = 7, my $count = 0; $count < 4; $i++ ) {
    ++$count, @res.push: "R($i)" if (1 x $i).is-prime
}

@res.push: "\nRepunit testing:";

#(5003, 9887, 15073, 25031, 35317, 49081).map: {
(5003, 9887).map: {
    @res.push: "R($_): Prime? "~ ? is_prime("{1 x $_}")
}
.say for @res;

my $ref = q:to/END/;
The first 19 circular primes are:
2 3 5 7 11 13 17 37 79 113 197 199 337 1193 3779 11939 19937 193939 199933

The next 4 circular primes, in repunit format, are:
R(19)
R(23)
R(317)
R(1031)

Repunit testing:
R(5003): Prime? False
R(9887): Prime? False
END
#R(15073): Prime? False
#R(25031): Prime? False
#R(35317): Prime? False
#R(49081): Prime? True

use Test;
is @res.join("\n"), chomp $ref;
