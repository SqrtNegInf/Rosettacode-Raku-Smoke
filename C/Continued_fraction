#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Continued_fraction
#c# 2015-10-31 <>RC
#m# MOAR: OK
#j#  JVM: OK

sub continued-fraction(:@a, :@b, Int :$n = 100)
{
    my $x = @a[0];
    $x = @a[$_ - 1] + @b[$_] / $x for reverse 1 ..^ $n;
    $x;
}

printf "âˆš2 â‰ˆ%.9f\n", my $r1 = continued-fraction(:a(1, |(2 xx *)), :b(Nil, |(1 xx *)));
printf "e  â‰ˆ%.9f\n", my $e1 = continued-fraction(:a(2, |(1 .. *)), :b(Nil, 1, |(1 .. *)));
printf "Ï€  â‰ˆ%.9f\n", my $p1 = continued-fraction(:a(3, |(6 xx *)), :b(Nil, |((1, 3, 5 ... *) X** 2)));

# Perl6 has a builtin composition operator. We can use it with the
# triangular reduction metaoperator, and evaluate each resulting function at
# infinity (any value would do actually, but infinite makes it consistent
# with this particular task).

sub continued-fraction1(@a, @b) {
    map { .(Inf) }, [\o] map { @a[$_] + @b[$_] / * }, ^Inf
}

printf "âˆš2 â‰ˆ%.9f\n", my $r2 = continued-fraction1((1, |(2 xx *)), (1 xx *))[10];
printf "e  â‰ˆ%.9f\n", my $e2 = continued-fraction1((2, |(1 .. *)), (1, |(1 .. *)))[10];
printf "Ï€  â‰ˆ%.9f\n", my $p2 = continued-fraction1((3, |(6 xx *)), ((1, 3, 5 ... *) X** 2))[100];

use Test;
plan 6;
is-approx $r1, 2**0.5, 'âˆš2';
is-approx $e1, ğ‘’,      'ğ‘’';  # Unicode (not for JVM)
is-approx $p1, Ï€,      'Ï€';  #          "
is-approx $r2, sqrt(2),'âˆš2';
is-approx $e2, e,      'e';
is-approx $p2, pi,     'pi';
