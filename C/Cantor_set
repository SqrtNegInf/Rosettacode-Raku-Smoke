#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Cantor_set
#c# 2018-04-20 <RC
#m# MOAR: OK
#j# JVM:  OK

sub cantor ( Int $height ) {
    my $width = 3 ** ($height - 1);

    my @lines = ( "\c[FULL BLOCK]" x $width ) xx $height;

    sub trim-middle-third( $len, $start, $index ) {
        my $seg = $len div 3 or return;

        for $index ..^ $height X 0 ..^ $seg -> ( $i, $j ) {
            @lines[$i].substr-rw( $start + $seg + $j, 1 ) = ' ';
        }

        trim-middle-third( $seg, $start + $_, $index + 1 ) for 0, $seg × 2;
    }

    trim-middle-third( $width, 0, 1 );
    @lines
}

.say for cantor(5);

my $ref = qq:to/END/;
█████████████████████████████████████████████████████████████████████████████████
███████████████████████████                           ███████████████████████████
█████████         █████████                           █████████         █████████
███   ███         ███   ███                           ███   ███         ███   ███
█ █   █ █         █ █   █ █                           █ █   █ █         █ █   █ █
END

use Test;
is (cantor(5)).join("\n"), chomp $ref;
