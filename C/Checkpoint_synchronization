#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Checkpoint_synchronization
#c# 2018-12-20 <RC
#m# MOAR: OK
#j# JVM:  OK

my @res;

use Test;

my $TotalWorkers = 2;
my $BatchToRun = 3;
my @TimeTaken = (1..5); # in seconds
 
my $batch_progress = 0;
my @batch_lock = map { Semaphore.new(1) } , ^$TotalWorkers;
my $lock = Lock.new;

for ^$TotalWorkers -> $i {
   Thread.start(
      sub {
         @batch_lock[$i].acquire;
         assembly_line($i);
      }
   );
}
 
sub assembly_line ($ID) {
   my $wait;
   for ^$BatchToRun -> $j {
      $wait = @TimeTaken.roll;
      say "Worker ",$ID," at batch $j will work for ",$wait," seconds ..";
      sleep($wait);
      $lock.protect: {
         my $k = ++$batch_progress;
         print "Worker ",$ID," is done and update batch $j complete counter ";
         say "to $k of $TotalWorkers";
         if ($batch_progress == $TotalWorkers) {
            say ">>>>> batch $j completed.";
            @res[$j] = True;
            $batch_progress = 0; # reset for next batch
            for @batch_lock { .release }; # and ready for next batch
         };
       };
 
       { say 'ok 1 -'; exit } if @res[0] and @res[1] and @res[2];
       @batch_lock[$ID].acquire; # for next batch
   }
}
