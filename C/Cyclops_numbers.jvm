#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Cyclops_numbers
#c# 2021-06-23 <RC, 2021-06-25 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# limit testing as very slow...

my @res;

use Lingua::EN::Numbers;

my @cyclops = 0, |flat lazy ^∞ .map: -> $exp {
      my @oom = (exp($exp, 10) ..^ exp($exp + 1, 10)).grep: { !.contains: 0 }
      |@oom.map: { $_ ~ 0 «~« @oom }
}

my @prime-cyclops = @cyclops.grep: { .is-prime };

for '',                   @cyclops,
    'prime ',             @prime-cyclops,
    'blind prime ',       @prime-cyclops.grep( { .trans('0' => '').is-prime } ),
    'palindromic prime ', @prime-cyclops.grep( { $_ eq .flip } )
  -> $type, $iterator {
    @res.push: "\n\nFirst 10 {$type}cyclops numbers:\n" ~ $iterator[^10].batch(10)».fmt("%7d").join("\n") ~
        "\n\nFirst {$type}cyclops number > 10,000,000: " ~ comma($iterator.first: * > 1e7 ) ~
        " - at (zero based) index: " ~ comma $iterator.first: * > 1e7, :k;
}

.say for @res;

my $ref = q:to/END/;


First 10 cyclops numbers:
      0     101     102     103     104     105     106     107     108     109

First cyclops number > 10,000,000: 111,101,111 - at (zero based) index: 538,084

First 10 prime cyclops numbers:
    101     103     107     109     307     401     409     503     509     601

First prime cyclops number > 10,000,000: 111,101,129 - at (zero based) index: 39,319

First 10 blind prime cyclops numbers:
    101     103     107     109     307     401     503     509     601     607

First blind prime cyclops number > 10,000,000: 111,101,161 - at (zero based) index: 11,393

First 10 palindromic prime cyclops numbers:
    101   16061   31013   35053   38083   73037   74047   91019   94049 1120211

First palindromic prime cyclops number > 10,000,000: 114,808,411 - at (zero based) index: 66
END

use Test;
is @res.join(''), chomp $ref;
