#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Catalan_numbers
#c# 2016-01-23 <RC
#m# MOAR: OK
#j#  JVM: OK

#`{
For Catalan1:
...-1 yields: infinite sequence
... 0 yields: infinite sequence
... 1 yields: 1
... 2 yields: 1, 1, 2
... 3 yields: infinite sequence

Catalan2 increases just by one result at-a-time
}

constant Catalan1 = 1, { [+] @_ Z× @_.reverse } ... *;
constant Catalan2 = 1, |[\×] (2, 6 ... *) Z/ 2 .. *;

my @res1 = Catalan1[^16];
my @res2 = Catalan2[^16];

.say for @res1;

my $ref = qq:to/END/;
1
1
2
5
14
42
132
429
1430
4862
16796
58786
208012
742900
2674440
9694845
END

use Test;
is @res1.join("\n"), chomp $ref;
is @res1, @res2;
