#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Continued_fraction/Arithmetic/Construct_from_rational_number
#c# 2015-11-01 <RC
#m# MOAR: OK
#j#  JVM: OK

sub r2cf(Rat $x is copy) {
    gather loop {
	$x -= take $x.floor;
	last unless $x > 0;
	$x = 1 / $x;
    }
}

my @res.push: r2cf(.Rat) for <1/2 3 23/8 13/11 22/7 1.41 1.4142136>;

say my $result = @res.join("\n");

sub r2cf1(Rat $x is copy) { gather $x [R/]= 1 while ($x -= take $x.floor) > 0 }
#say r2cf1(.Rat) for <1/2 3 23/8 13/11 22/7 1.41 1.4142136>;

my $ref = qq:to/END/;
0 2
3
2 1 7
1 5 2
3 7
1 2 2 3 1 1 2
1 2 2 2 2 2 2 2 2 2 6 1 2 4 1 1 2
END

use Test;
is $result, chomp $ref;
is r2cf(1.4142136.Rat),  '1 2 2 2 2 2 2 2 2 2 6 1 2 4 1 1 2', 'simple sub works for sqrt(2)';
is r2cf1(1.4142136.Rat), '1 2 2 2 2 2 2 2 2 2 6 1 2 4 1 1 2', 'one-liner also works for sqrt(2)';
