#!/usr/bin/env raku
#u# http://rosettacode.org/wiki/Cullen_and_Woodall_numbers
#c# 2022-01-11 <RC
#m# MOAR: OK
#j# JVM:  OK
#n# OEIS:A002064 Cullen numbers: a(n) = n*2^n + 1.
#n# OEIS:A003261 Woodall (or Riesel) numbers: n*2^n - 1.
#n# OEIS:A005849 Indices of prime Cullen numbers: numbers k such that k*2^k + 1 is prime.
#n# OEIS:A002234 Numbers k such that the Woodall number k*2^k - 1 is prime.

my @res;

my @cullen  = ^∞ .map: { $_ × 1 +< $_ + 1 };
my @woodall = ^∞ .map: { $_ × 1 +< $_ - 1 };

@res.push: "First 20 Cullen numbers: ( n × 2**n + 1)\n"~     @cullen[1..20];
@res.push: "\nFirst 20 Woodall numbers: ( n × 2**n - 1)\n"~ @woodall[1..20];
#@res.push: "\nFirst 5 Cullen primes: (in terms of n)\n"~     @cullen.grep( &is-prime, :k )[^5];
@res.push: "\nFirst 12 Woodall primes:  (in terms of n)\n"~ @woodall.grep( &is-prime, :k )[^12];

.say for @res;

my $ref = q:to/END/;
First 20 Cullen numbers: ( n × 2**n + 1)
3 9 25 65 161 385 897 2049 4609 10241 22529 49153 106497 229377 491521 1048577 2228225 4718593 9961473 20971521

First 20 Woodall numbers: ( n × 2**n - 1)
1 7 23 63 159 383 895 2047 4607 10239 22527 49151 106495 229375 491519 1048575 2228223 4718591 9961471 20971519

First 12 Woodall primes:  (in terms of n)
2 3 6 30 75 81 115 123 249 362 384 462
END

#First 5 Cullen primes: (in terms of n)
#1 141 4713 5795 6611

use Test;
is @res.join("\n"), chomp $ref;
