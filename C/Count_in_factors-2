#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Count_in_factors
#c# 2015-10-27 <RC
#m# MOAR: OK
#j#  JVM: OK

#The first twenty numbers:
my $ref = qq:to/END/;
1: 1
2: 2
3: 3
4: 2 × 2
5: 5
6: 2 × 3
7: 7
8: 2 × 2 × 2
9: 3 × 3
10: 2 × 5
11: 11
12: 2 × 2 × 3
13: 13
14: 2 × 7
15: 3 × 5
16: 2 × 2 × 2 × 2
17: 17
18: 2 × 3 × 3
19: 19
20: 2 × 2 × 5
END

# Here is a solution inspired from Almost_prime 'C' language. 

sub factor($n is copy) {
    $n == 1 ?? 1 !!
    gather {
	$n /= take 2 while $n %% 2;
	$n /= take 3 while $n %% 3;
	loop (my $p = 5; $p×$p <= $n; $p+=2) {
	    $n /= take $p while $n %% $p;
	}
	take $n unless $n == 1;
    }
}

my @res.push: ("$_:", join " \x00d7 ", factor $_) for 1 .. 20;
.say for @res;

use Test;
is @res.join("\n"), chomp $ref;
