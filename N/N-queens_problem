#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/N-queens_problem
#c# 2015-11-29 <>RC
#m# MOAR: OK
#j#  JVM: OK
#n# OEIS:A000170
#n# Neither pretty nor efficient, a simple backtracking solution

use Test;

sub MAIN(\N = 8) {
    sub collision(@field, $row) {
        for ^$row -> $i {
            my $distance = @field[$i] - @field[$row];
            return True if $distance == any(0, $row - $i, $i - $row);
        }
        False;
    }
    sub search(@field, $row) {
        return @field if $row == N;
        for ^N -> $i {
            @field[$row] = $i;
            return search(@field, $row + 1) || next
                unless collision(@field, $row);
        }
        ()
    }
    for 0 .. N / 2 {
        if search [$_], 1 -> @f {
            say @f;
            is @f, '0 4 7 5 2 6 1 3';
            last;
        }
    }
}

