#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Nth_root
#c# 2016-03-05 <RC, 2023-09-29 <RC
#m# MOAR: OK
#j#  JVM: OK

sub nth-root ($n, $A, $p=1e-9)
{
    my $x0 = $A / $n;
    loop {
        my $x1 = (($n-1) × $x0 + $A / ($x0 ** ($n-1))) / $n;
        return $x1 if abs($x1-$x0) < ($x0×$p).abs;
        $x0 = $x1;
    }
    die 'never get here'
}

sub infix:<√>($n, $A) {
    .tail given $A / $n, { (($n-1) * $_+ $A / ($_** ($n-1))) / $n } ... * ≅ *;
}

say nth-root(3,2 ** 3);
say nth-root(10,7131.5 ** 10);

use Test;
is nth-root(3,2 ** 3), 2;
is nth-root(10,7131.5 ** 10), 7131.5;
is-approx nth-root(.5,7), 49;
is-approx (4√pi)**4, pi;
