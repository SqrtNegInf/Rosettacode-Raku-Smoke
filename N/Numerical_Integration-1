#!/usr/bin/env perl6
#u# http://rosettacode.org/wiki/Numerical_Integration
#c# 2015-12-16 <RC
#t# toodamnslow
#m# MOAR: OK
#j#  JVM: OK
#   20161123: 18213u 129.950s 5:11:41.81 98%
#   20181004: 12358u  77.530s 3:33:55.73 96%
#   20220304: 12698u 110.17s  3:36:05.82 99%
#   _NEXT_

# Works with: Rakudo version 2015-09-24
sub leftrect(&f, $a, $b, $n) {
    my $h = ($b - $a) / $n;
    $h × [+] do f($_) for $a, $a+$h ... $b-$h;
}

sub rightrect(&f, $a, $b, $n) {
    my $h = ($b - $a) / $n;
    $h × [+] do f($_) for $a+$h, $a+$h+$h ... $b;
}

sub midrect(&f, $a, $b, $n) {
    my $h = ($b - $a) / $n;
    $h × [+] do f($_) for $a+$h/2, $a+$h+$h/2 ... $b-$h/2;
}

sub trapez(&f, $a, $b, $n) {
    my $h = ($b - $a) / $n;
    # Too many arguments in flattening array.
#   $h / 2 × [+] f($a), f($b), |do f($_) × 2 for $a+$h, $a+$h+$h ... $b-$h;
    my $partial-sum += f($_) × 2 for $a+$h, $a+$h+$h ... $b-$h; #DH
    $h / 2 × [+] f($a), f($b), $partial-sum;
}

sub simpsons(&f, $a, $b, $n) {
    my $h = ($b - $a) / $n;
    my $h2 = $h/2;
    my $sum1 = f($a + $h2);
    my $sum2 = 0;

    for $a+$h, *+$h ... $b-$h {
        $sum1 += f($_ + $h2);
        $sum2 += f($_);
    }
    ($h / 6) × (f($a) + f($b) + 4×$sum1 + 2×$sum2);
}

sub tryem($f, $a, $b, $n, $exact) {
    my @res;
    @res.push: "$f\n   in [$a..$b] / $n";
    "my &f = $f;
    @res.push: '              exact result: '~ $exact;
    @res.push: '     rectangle method left: '~ leftrect  &f, $a, $b, $n;
    @res.push: '    rectangle method right: '~ rightrect &f, $a, $b, $n;
    @res.push: '      rectangle method mid: '~ midrect   &f, $a, $b, $n;
    @res.push: 'composite trapezoidal rule: '~ trapez    &f, $a, $b, $n;
    @res.push: '   quadratic simpsons rule: '~ simpsons  &f, $a, $b, $n;".EVAL
}

# Note that these integrations are done with rationals rather than floats, so should be fairly precise (though of course with so few iterations they are not terribly accurate (except when they are)).  Some of the sums do overflow into Num (floating point)--currently rakudo allows 64-bit denominators--but at least all of the interval arithmetic is exact.

use Test;

my $ref1 = q:to/END1/;
{ $_ ** 3 }
   in [0..1] / 100
              exact result: 0.25
     rectangle method left: 0.245025
    rectangle method right: 0.255025
      rectangle method mid: 0.249988
composite trapezoidal rule: 0.250025
   quadratic simpsons rule: 0.25
END1
my @res1 = tryem '{ $_ ** 3 }', 0, 1, 100, 0.25;
.say for @res1; say '';
is @res1.join("\n"), chomp $ref1;

my $ref2 = q:to/END2/;
1 / *
   in [1..100] / 1000
              exact result: 4.605170185988092
     rectangle method left: 4.654991057514682
    rectangle method right: 4.556981057514681
      rectangle method mid: 4.604762548678376
composite trapezoidal rule: 4.605986057514682
   quadratic simpsons rule: 4.605170384957145
END2
my @res2 = tryem '1 / *', 1, 100, 1000, log(100);
.say for @res2; say '';
is @res2.join("\n"), chomp $ref2;

my $ref3 = q:to/END3/;
*.self
   in [0..5000] / 5000000
              exact result: 12500000
     rectangle method left: 12499997.5
    rectangle method right: 12500002.5
      rectangle method mid: 12500000
composite trapezoidal rule: 12500000
   quadratic simpsons rule: 12500000
END3
my @res3 = tryem '*.self', 0, 5_000, 5_000_000, 12_500_000;
.say for @res3; say '';
is @res3.join("\n"), chomp $ref3;

my $ref4 = q:to/END4/;
*.self
   in [0..6000] / 6000000
              exact result: 18000000
     rectangle method left: 17999997
    rectangle method right: 18000003
      rectangle method mid: 18000000
composite trapezoidal rule: 18000000
   quadratic simpsons rule: 18000000
END4
my @res4 = tryem '*.self', 0, 6_000, 6_000_000, 18_000_000;
.say for @res4; say '';
is @res4.join("\n"), chomp $ref4;
